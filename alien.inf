!% -~S
!% -G
!% -D
!% $MAX_STATIC_DATA=20000
!% $MAX_PROP_TABLE_SIZE=58000
!% $MAX_VERBS=153

!
! "A·L·I·E·N" - Edición especial
!
! Copyright (c) 2008-2011 Ricardo Pérez López (Alpha Aventuras)
!

! This program is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! This program is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.



! ====================================================================
!
! CONSTANTES
!
! ====================================================================



Constant Historia "~A·L·I·E·N~ - Edición especial";
Constant Titular "^(c) 2008-2011 Alpha Aventuras^^
                  Teclea CRÉDITOS si quieres saber más acerca de la aventura, o
                  AYUDA cuando estés bloqueado.^";
Release 1;

! Queremos usar el comando 'Salidas'
Constant ADMITIR_COMANDO_SALIDAS;
! No queremos usar los comandos 'Lugares' y 'Objetos'
Constant NO_LUGARES;
! No queremos usar puntuación
Constant NO_PUNTUACION;
! Queremos que se muestren las deducciones del parser
!Constant IMPRIMIR_DEDUCCIONES;
! Hay curiosidades al final del juego
Constant HAY_CURIOSIDADES;

! Rutas de los móviles:
!Constant RUTA_NINGUNA = 0;
!Constant RUTA_ALEATORIA = 1;
!Constant RUTA_SEGUIR_JUGADOR = 2;

! Máximo nivel de carga para la batería:
Constant MAX_NIVEL_CARGA = 150;

! Alto de la ventana gráfica
!Constant ALTO_VENTANA_GRAFICA = 350; !500; !315;
Constant GRAFICOS_PEQUENOS = 300;
Constant GRAFICOS_MEDIANOS = 400;
Constant GRAFICOS_GRANDES  = 500;

! Canales de sonido:
! Constant NUM_CANALES = 6;
Constant CANAL_FONDO = 9;
! Este canal se comparte entre el detector de movimiento y la sirena de emergencia:
Constant CANAL_DETECTOR = 1;
Constant CANAL_TELETIPO = 2;

! Vamos a usar Damusix junto con SGW+DMX
Constant SGW_CON_DAMUSIX;
Constant SONIDO_BUCLE_INFINITO = -1;

! El timer a aplicar para el trueno:
Constant TIMER_TRUENO = 4000;
! El timer para el laberinto es más largo:
Constant TIMER_LABERINTO = 8000;

! ====================================================================
!
! VARIABLES GLOBALES
!
! ====================================================================


! Alto de la ventana gráfica:
Global altoVentanaGrafica = 400;

! Hay gráficos o no:
Global hayGraficos = true;

! Si se ha activado el listado automático de salidas:
Global mostrarSalidas = true;

! Se pone a true y false en la rutina PermitirEmpujarDir, y luego se consulta en
! MensajesLibreria:
Global estoyEmpujando = false;

! Interruptor de pistas:
Global hayPistas = 0;

! Interruptor para indicar si hay que dibujar o no la línea de estado
! (ver rutina Inicializar):
Global dibujarEstado = false;



! ====================================================================
!
! INCLUDES Y REPLACES
!
! ====================================================================



Replace DibujarLineaEstado;

! En este juego no se usa el BuscarEn (MIRA EN, BUSCA EN/DENTRO/DE/SOBRE, REGISTRA...)
Replace BuscarEnSub;

! En este juego no se usa el Consultar (CONSULTA, LEE, BUSCA EN x SOBRE y...)
Replace ConsultarSub;

! En este juego no se habla, ni se responde, ni se dice:
Replace HablarSub;
Replace ResponderSub;
Replace DecirSub;

! Sustituimos las rutinas de REINICIAR, RESTAURAR y UNDO (Teclado):
Replace ReiniciarSub;
Replace RestaurarSub;
Replace Teclado;

! Con el siguiente Replace conseguimos que el objeto empujado hacia una dirección
! aparezca en la descripción de la localidad de destino del "empuje":

Replace PermitirEmpujarDir;


! ====================================================================


Include "EParser";

Replace EsperarTecla;
Replace InitGlkWindow;
Replace HandleGlkEvent;
Include "SGW+DMX_NG";    ! entre EParser y Acciones
Include "alien.bli";     ! Los recursos gráficos de la aventura


[ Curiosidades;
  print "- La aventura está basada en una idea original de Manolo (nuestro grafista).^^";
  EsperarTecla();
  print "- La primera versión de A·L·I·E·N fue desarrollada hace más de quince años.^^";
  EsperarTecla();
  print "- Esa primera versión la escribió Manolo en Spectrum usando el PAWS original^";
  print "  comprado a AD.^^";
  EsperarTecla();
  print "- Inicialmente, esta aventura iba a ser un remake de ese ~clásico~, pero durante^";
  print "  el proceso decidimos ampliar bastante más la historia.^^";
  EsperarTecla();
  print "- Hay una segunda parte de la versión original en Spectrum perdida en una casette^";
  print "  olvidada dentro de alguna caja extriaviada en casa de Manolo.^^";
  EsperarTecla();
  print "- El ~laberinto~ inicial estaba formado sólo por cinco localidades, para después^";
  print "  pasar a tener más de 70, y finalmente acabar teniendo alrededor de 30.^^";
  EsperarTecla();
  print "- Todos las imágenes del juego son dibujos originales de Manolo, diseñados para^";
  print "  la ocasión.^^";
  EsperarTecla();
  print "- ¿Has visto ya el documento ~";
  style bold;
  print "Holocubo.pdf";
  style roman;
  print "~ usando la contraseña apropiada?^";
  print "  Contiene información muy interesante sobre el xenoforme.^^";
  EsperarTecla();
  print "- Si te estás preguntado para qué sirve la palanca... Efectivamente: no sirve^";
  print "  para nada en absoluto.^^";
!  print "- El sonido de fondo dentro de la nave es un extracto del tema ~Overflight~,^";
!  print "  incluido en el álbum ~The Secret Life of Angels~ del grupo ~SkinMechanix~.^";
!  print "  Más información en Magnatune (http://www.magnatune.com).^^";
!  EsperarTecla();
];


Object MensajesLibreria
  with
    antes [;
      Mirar:
        ! Copiamos el código de Mensajes.h. Sólo cambiamos el segundo
        ! EscribirListaDesde, para que cuando se deje en el suelo la
        ! linterna, no diga: "Puedes ver una linterna (en la que ves una batería)."
        switch (ml_n) {
          1: print " (sobre ", (el) ml_o, ")";
          2: print " (en ", (el) ml_o, ")";
          3: print " (como ", (object) ml_o, ")";
          4: print "^Sobre ", (el) ml_o;
             EscribirListaDesde(child(ml_o), ESPANOL_BIT + RECURSIVO_BIT +
                                INFOPARCIAL_BIT + BREVE_BIT + HAY_BIT + OCULTAR_BIT);
             ".";
          default:
            if (ml_o ~= localizacion) {
              if (ml_o has soporte)
                print "^Sobre ";
              else
                print "^En ";
              print (el) ml_o;
              print " puedes ver ";
            } else
              print "^También puedes ver ";
            if (n == 5) print "también ";    ! ?????
            ! Quitamos RECURSIVO_BIT y INFOPARCIAL_BIT:
            EscribirListaDesde(child(ml_o), ESPANOL_BIT + BANDERAUX_BIT +
                               BREVE_BIT + OCULTAR_BIT);
            if (ml_o ~= localizacion) ".";
            ".";
        }

      Prompt:
        if (localizacion == procesador) {
          procesador.comprobar_segunda_parte();
        }
        print "^> ";
        rtrue;

      Ir:
        switch (ml_n) {
          2: if (estoyEmpujando) {
               "^No puedes seguir empujando porque no hay salida hacia ",
                (el) uno, ".";
             } else {
               "No puedes ir por ahí, porque no hay salida hacia ",
                (el) uno, ".";
             }
          5: if (estoyEmpujando) {
               "^No puedes empujar nada a través ", (del) ml_o, ".";
             } else {
               "No puedes pasar a través ", (del) ml_o, ".";
             }
        }
        
      Coger:
        if (ml_n == 1) {
          print "Recoges ";
          if (ml_o has movido) {
            print (el) ml_o;
          } else {
            print (un) ml_o;
          }
          ".";
        }

      Dejar:
        if (ml_n == 4) {
          "Dejas ", (el) ml_o, ".";
        }

      Sacar:
        if (ml_n == 3) {
          if (palabra_verbo == 'quita') {
            "Quitas ", (el) ml_o, " ", (del) otro, ".";
          } else {
            "Sacas ", (el) ml_o, " ", (del) otro, ".";
          }
        }
      Miscelanea:
        switch (ml_n) {
           4: TextoLlamativo(" ¡Enhorabuena! Has acabado la aventura ");
              rtrue;
          10: rtrue;
          19: "Has tenido días mejores...";
          30: switch (random(3)) {
                1: "No encuentro eso que dices.";
                2: "No veo nada parecido por aquí.";
                3: "Parece que no hay nada de eso.";
              };
          27: "Las palabras se te agolpan en los labios.";
          38: switch (random(5)) {
                1: "¿Qué quieres decir con eso?";
                2: "Lo siento, no te entiendo.";
                3: "Intenta ser un poco más preciso.";
                4: "No comprendo eso que dices.";
                5: "¿Qué intentas decir?";
              }
        }
    ];


! ====================================================================


Include "Acciones";


[ AlAlcance actor p;

  ! Ponemos al alcance el decorado de los trajes, si uno de éstos (o los dos) también
  ! está al alcance. Lo ideal sería poder usar la rutina PruebaDeAlcance, pero eso
  ! provocaría una recursión infinita. Por eso tenemos que hacer las comprobaciones
  ! "a mano":
  if (razon_alcance == RAZON_PARSING) {
    p = parent(tu_traje);

    if (tu_traje hasnt ausente && (tu_traje in jugador or LugarReal() ||
        (p in LugarReal() && (p has abierto or transparente)))) {
      PonerAlAlcance(decorado_traje);
    } else {
      p = parent(traje_jason);
      
      if (traje_jason hasnt ausente && (traje_jason in jugador or LugarReal() ||
          (p in LugarReal() && (p has abierto or transparente)))) {
        PonerAlAlcance(decorado_traje);
      } else {
        ! Con el traje inútil hay que hacer menos comprobaciones, porque no se puede
        ! mover de donde está:
        if (traje_inutil hasnt ausente && traje_inutil in LugarReal()) {
          PonerAlAlcance(decorado_traje_inutil);
        }
      }
    }
  }
  
  ! Ponemos a Jason al alcance para hablar, ya que tenemos comunicador:
  if (razon_alcance == RAZON_HABLAR or RAZON_CADA_TURNO &&
      comunicador in jugador && comunicador has puesto) {
    if (jason has ausente) {
      PonerAlAlcance(jason_inerte);
    } else if (jason notin LugarReal() || localizacion == laoscuridad) {
      PonerAlAlcance(jason);
    }
  }
  
  ! Lo mismo para la computadora:
  if (razon_alcance == RAZON_HABLAR or RAZON_CADA_TURNO &&
      comunicador in jugador && comunicador has puesto) {
    PonerAlAlcance(computadora);
  }
  
  ! Ponemos la compuerta de la nave al alcance de la computadora, para que pueda abrirla
  ! aunque no estemos físicamente delante de la puerta:
  if (actor == computadora) {
    PonerAlAlcance(puerta_nave);
    PonerAlAlcance(compartimento_trajes);
  }
  
  ! Así le puedes decir "jason, sigueme" o "jason, quedate" incluso aunque no esté en la
  ! misma localidad que tú:
  if (accion_que_seria == ##Seguir or ##Quedar or ##Esperar && actor == jason) {
    PonerAlAlcance(jugador);
  }
  
  rfalse;
];


! Cambiar el comentario si se desean mensajes en primera persona
! La librería Mensajes normal se incluye automáticamente.
! Include "Mensajes";
! Include "Msg1P";

! Esto es para poder usar las palabras 'abrir' y 'cerrar' como temas de conversación,
! y, al mismo tiempo, poder usarlas como infinitivos de verbos:
[ VerboDesconocido x;
  if (x == 'abrir')    return 'abre';
  if (x == 'cerrar')   return 'cierra';
  if (x == 'cortar')   return 'corta';
  if (x == 'sacar')    return 'saca';
  if (x == 'cargar')   return 'carga';
  if (x == 'recargar') return 'recarga';
  if (x == 'salir')    return 'sal';
  if (x == 'arreglar') return 'arregla';
  if (x == 'reparar')  return 'repara';
  rfalse;
];


Include "Rastros_NG";     ! Mis rastros son: sangre del alien, cable blanco y cable negro
Include "BajoNivel";      ! Necesario para Barra:
Include "barra";          ! Para cambiar la barra de estado


Object barra_estado
  class objeto_barra_estado
  with
    modo BE_COMPUESTO,
    disposicion
      1  1 true BE_LOCALIDAD
      29 1 true BE_TEXTO
      45 1 true BE_TEXTO
      65 1 true BE_TEXTO
      68 1 true BE_TURNOS,
    texto [ x;
      switch (x) {
        1: if (linterna has encendida && bateria in linterna) {
             print_ret " | Linterna: ", bateria.nivel_carga(), "%";
           }
        2: if (rifle in jugador && detector_movimiento has encendido &&
               alien.distancia > 0 && alien.distancia < 6 && alien hasnt ausente) {
             if (parent(alien) == LugarReal()) {
               print_ret " | Detector: 0 m.";
             } else {
               print " | Detector: ", 10 * alien.distancia, " m. ";
               switch (alien.sentido) {
                 obj_n:      "N";
                 obj_s:      "S";
                 obj_e:      "E";
                 obj_o:      "O";
                 obj_arriba: "AR";
                 obj_abajo:  "AB";
               }
             }
             rtrue;
           }
        3: print " |";
      }
    ],
    lineas_inv
      BE_INV_TOTAL;


[ DibujarLineaEstado;
  if (dibujarEstado) {
    barra_estado.dibujar();
  }
];


! Hay que definirla antes de incluir Moviles_NG:
Class Lugar
  class LugarConRastro
  with
    sgw_mus Estancia_ogg,
    sgw_vol 50,
    describir [;
      ImprimirOEjecutar(self, descripcion);
      if (mostrarSalidas) {
        print "^";
!        style reverse;
        glk($0086, style_Emphasized);
        <Salidas>;
        style roman;
        rtrue;
      } else {
        rtrue;
      }
    ],
    cantidad,
    antes [;
      Examinar, Tocar:
        if (uno == obj_abajo) {              ! Examinamos el suelo
          if (EsExterior(self)) {
            ! Si estamos en el exterior...
            "Una superficie de polvo y arena en forma de barro que se acumula en las suelas
             de tus botas. Además, la lluvia se encharca con relativa frecuencia.";
          } else {
            "Una superficie metálica y sólida.";
          }
        } else if (uno == obj_arriba && self ~= hueco_techo) {   ! Examinamos el techo
          if (EsExterior(self)) {
            ! Si estamos en el exterior...
            if (tu_traje in self && tu_traje has puesto) {
              "La intensa lluvia baña tu casco.";
            } else {
              "La intensa lluvia ciega tus ojos.";
            }
          } else {
            "Tuberías y estructuras metálicas recorren el techo.";
          }
        }        
    ],
    despues [;
      Ir:
        ! Cuando el jugador se mueve a otra localidad, se comprueba si hay que activar el
        ! sonido de fondo
        if (control_sonido_fondo.hay_sonido && LugarReal() provides sgw_mus) {
          EncenderMusicaSub(true);
        } else {
          control_sonido_fondo.desactivar();
        }
        ! Si no está el alien presente, se resetea su oportunidad a cero y
        ! su capricho a 50:
        if (alien notin LugarReal()) {
          if (alien.oportunidad ~= 0) {
            alien.oportunidad = 0;
          }
          if (alien.comportamiento == 1 or 3 && alien.capricho == 0) {
            alien.capricho = 50;
          }
        }
    ],
    cada_turno [;
      ! Así conseguimos que el alien vaya el doble de lento que nosotros cuando nos
      ! persiga (alternamos persecución con detención). Pero irá igual de rápido cuando
      ! estemos dentro de la zona de emergencia con la puerta cerrada (para que no deje
      ! de decir que hay algo intentando abrir la puerta):
      if (alien.comportamiento == 3) {
        if (alien.tipo_de_movimiento == MOVIMIENTO_PERSEGUIR &&
            (puerta_emergencia has abierta ||
            (puerta_emergencia hasnt abierta &&
              (LugarReal() ofclass ZonaEmergencia && parent(alien) ofclass ZonaEmergencia) ||
              (~~(LugarReal() ofclass ZonaEmergencia) &&
               ~~(parent(alien) ofclass ZonaEmergencia))))) {
          PNJ_Ruta(alien, MOVIMIENTO_NINGUNO);
        } else {
          PNJ_Ruta(alien, MOVIMIENTO_PERSEGUIR, jugador);
        }
      }
      
      ! En el exterior, de vez en cuando suena un trueno. Usamos control_timer.activar()
      ! porque ese método comprueba previamente si ya se había activado previamente el
      ! mismo timer, con lo cual el timer no se reactiva si se vuelve a este punto antes
      ! de que expire el timer anterior:
      if (EsExterior(self)) {
        control_timer.activar(TIMER_TRUENO);
      } else if ((self ofclass Laberinto || self ofclass Balcon) &&
                 alien.comportamiento == 1 or 2) {
        control_timer.activar(TIMER_LABERINTO);
      } else {
        control_timer.desactivar();
     }
    ],
  has
    luz;


Include "Moviles_NG";     ! La librería Moviles retocada por mí
Include "PnjPuertas_NG";  ! La librería PnjPuertas retocada por mí (muy poco)
Include "PNJactor_NG";    ! Librerías que hacen que un PNJ pueda coger y dejar
Include "PNJacciones_NG"; ! objetos como el PJ (poner detrás de Moviles y PnjPuertas)
Include "Etemas";         ! Los temas de conversación
Include "Decorado";       ! Objetos de decorado
Include "Array";          ! Necesario para escr
Include "Escr_NG";        ! Teletipo para sacar mensajes letra a letra
Include "Pistas";         ! Las pistas creadas con ZIPI


Class DecoradoAmpliado
  class Decorado
  with
    antes [;
      Examinar:
        if (PruebaDeAlcance(tu_traje, jugador) ||
            PruebaDeAlcance(traje_jason, jugador)) {
          PonerAlAlcance(decorado_traje);
        }
        rfalse;
      Coger: "No puedes hacerlo, ya que está", (n) self, " fij", (o) self,
             " en su sitio.";
      Empujar: print_ret (_El) self,
               " no parece que pueda", (n) self, " ser empujad", (o) self, ".";
      Oler: "No parece que huela", (n) self, " a nada especial.";
      Escuchar: "No produce", (n) self, " ningún sonido.";
      BuscarEn: "No hay nada que buscar en eso.";
      Tocar: "No notas nada especial al tacto.";
      default: "No hay ninguna razón para hacer eso.";
  ];


Class EscenarioAbrible
  with
    si_abierto [; print (_El) self, " está abiert", (o) self, ".^"; rtrue; ],
    si_cerrado [; print (_El) self, " está cerrad", (o) self, ".^"; rtrue; ],
    ! Cuando está abierto, pasa de ser escenario a ser estático. Esto se hace para que
    ! se muestre cuando está abierto, usando la propiedad si_abierto. En realidad, la
    ! propiedad si_cerrado no se usa, porque cuando está cerrado es un escenario, y por
    ! tanto no se describe.
    abrir [;
      give self abierto;
      give self ~escenario;
      give self estatico;
    ],
    cerrar [;
      give self ~abierto;
      give self escenario;
      give self ~estatico;
    ],
    despues [;
      Abrir:
        self.abrir();
      Cerrar:
        self.cerrar();
    ],
  has
    escenario abrible;


! Un contenedor que, cuando se examina, describe su contenido. Un objeto de esta clase
! debe usar la propiedad descripcion_real en lugar de descripcion:

Class Contenedor
  with
    descripcion_real "",
    descripcion [;
      print (string) self.descripcion_real, " ";
      if (self has abierto) {
        if (children(self) == 0) {
          "Está abiert", (o) self, ", pero vací", (o) self, ".";
        } else {
          print "Está abiert", (o) self, ", y en su interior puedes ver ";
          EscribirListaDesde(child(self), ESPANOL_BIT);
          print ".^";
        }
      } else {
        if (self has transparente) {
          print "Está cerrad", (o) self, ", pero al ser transparente puedes ver ";
          if (children(self) == 0) {
            "que está vacío.";
          } else {
            print "que en su interior hay ";
            EscribirListaDesde(child(self), ESPANOL_BIT);
            print ".^";
          }
        } else {
          "Está cerrad", (o) self, ".";
        }
      }
    ],
  has
    recipiente;


! Controla los efectos de sonido. Cada objeto ControlSonido trabaja sobre un canal:

Class ControlSonido
  with
    sonido null,      ! El sonido que está sonando en este momento
    canal,            ! El canal por el que suena
    volumen 100,      ! $10000,
    activar [ efecto rep;
      self.sonido = efecto;
      if (rep) {
        Damusix.AsignarCanal(efecto, self.canal, self.volumen, rep);
      } else {
        Damusix.AsignarCanal(efecto, self.canal, self.volumen, 1);
      }
      Damusix.Tocar(efecto);
    ],
    desactivar [;
      Damusix.Parar(self.sonido);
      self.sonido = null;
    ],
    cambiar_volumen [ v;
      self.volumen = v;
      Damusix.VolumenCanal(self.canal, v);
    ],
    iniciar [;
      Damusix.VolumenCanal(self.canal, self.volumen);
    ];
    

! Guarda el sonido de fondo que está sonando actualmente. Se usa para cuando el jugador se
! mueve de una localidad a otra que tiene el mismo sonido de fondo que la localidad de origen
! (en ese caso no detiene el sonido y lo vuelve a arrancar, sino que lo deja sonando):

Object control_sonido_fondo
  class ControlSonido
  with
    hay_sonido true,
    canal CANAL_FONDO,
    volumen 50; ! $5000;

Object control_sonido_detector
  class ControlSonido
  with
    hay_detector true,
    canal CANAL_DETECTOR,
    volumen 70; ! $7000;

Object control_sonido_teletipo
  class ControlSonido
  with
    canal CANAL_TELETIPO,
    iniciar [;
      Damusix.VolumenCanal(self.canal, self.volumen);
      Damusix.AsignarCanal(Teletipo_ogg, self.canal, self.volumen, 1);
    ];


! Controla el timer. Se usa sobre todo para el efecto del relámpago en el exterior,
! pero también porque la rutina EsperarTecla desactiva el timer actual cuando
! sale, así que aquí guardaremos el timer actual para que EsperarTecla pueda
! restaurarlo:

Object control_timer
  with
    timer_actual 0,
    activar [ t;
      if (t ~= self.timer_actual) {
        self.timer_actual = t;
        glk_request_timer_events(t);
      }
    ],
    desactivar [;
      self.activar(0);
    ];



! ====================================================================
!
! HABITACIONES
!
! ====================================================================



! El atributo general indica si no es la primera vez que se sale de la nave. Se usa para
! explicar (o no) lo de la zona intermedia de descompresión

Object nave1 "Interior de la nave"
  class Lugar
  with
    descripcion "La nave de exploración Alpha dispone de todo lo necesario para satisfacer
                 los requisitos más básicos en cuanto a soporte vital y contención. Los
                 instrumentos brillan y parpadean de forma insana, y frente a ellos se
                 disponen los dos únicos asientos del vehículo. Sus angostas paredes
                 aumentan la sensación de claustrofobia y los enormes ventanales ofrecen
                 una vista desoladora del exterior.",
    afuera puerta_nave,
    al_o nave2,
    sgw_img Nave1_jpg,
    sgw_mus Nave_ogg,
    cerrar [;
      puerta_nave.cerrar();
      HablaMadre("Cerrando compuerta de la nave.");
    ],
    antes [ i k t;
      Examinar:
        if (uno == obj_afuera) {
          ImprimirOEjecutar(decorado_nave1, desc_ventanales);
          rtrue;
        }

      Sentarse:
        "No es necesario sentarse para poder operar con los controles de la nave, ni
         para hablar con su computadora.";

      Ir:
        if (uno == obj_afuera) {
          ! Si el procesador atmosférico funciona, no hay que entrar a mirar si llevan trajes
          if (procesador.arreglado()) {
  !          self.mostrar_quien_sale(jason in self &&
  !                                  jason.tipo_de_movimiento == MOVIMIENTO_PERSEGUIR &&
  !                                  jason.perseguido == jugador);
            rfalse;
          }
              
          if (puerta_nave hasnt abierta) {
            rfalse;
          }
          
          k = 0;
          ! Comprueba si el jugador lleva puesto el traje. Para ello, recorre todos los
          ! objetos del jugador en busca de uno que sea de la clase TrajeAtmosferico y
          ! que además lo lleve puesto encima.
          objectloop (i in jugador) {
            if (i ofclass TrajeAtmosferico && i has puesto)
              k++;
          }
          if (k == 0) {
            HablaMadre("No debería salir sin llevar puesto su traje atmosférico.");
            "^Decides seguir sus sabios consejos.";
          }
          ! Comprobar si Jason está aquí, y sigue al jugador, y lleva puesto el traje.
          ! Si no lo lleva puesto, se lo pone antes de salir.
          if (jason in self && jason.tipo_de_movimiento == MOVIMIENTO_PERSEGUIR &&
              jason.perseguido == jugador) {
            t = 0;
            objectloop (i in jason) {
              if (i ofclass TrajeAtmosferico)
                t = i;
            }
            
            ! Si no lleva ningún traje atmosférico, intenta cogerlo:
            if (t == 0) {
  !            PNJ_Ruta(jason, MOVIMIENTO_NINGUNO);
              if (traje_jason notin jason) {
                if (PuedeVer(jason, traje_jason)) {
                  HablaJason("De acuerdo, pero antes cogeré el traje atmosférico.");
                  if (~~(jason.intentar_coger(traje_jason))) {
                    PNJ_Ruta(jason, MOVIMIENTO_NINGUNO);
  !                  rfalse;
                  } else {
                    give traje_jason puesto;
                    move jason to exterior1;
                  }
                  print "^";
                } else {
                  HablaJason("No puedo salir de la nave. No llevo puesto mi traje, y
                              no lo veo por aquí.");
  !                rfalse;
                }
              } else {
                if (traje_jason hasnt puesto) {
                  give traje_jason puesto;
                  HablaJason("De acuerdo, pero antes me pondré el traje atmosférico.");
                  print "^";
                  move jason to exterior1;
                }
              }
  !            print "[Jason se queda dentro, al no disponer de protección vital para
  !                    el exterior.]^^";
            } else {
              ! Si lo tiene, sale o se lo pone antes.
              if (t hasnt puesto) {
                give t puesto;
  !              jason.precamino = RUTA_SEGUIR_JUGADOR;
                PNJ_Ruta(jason, MOVIMIENTO_PERSEGUIR, jugador);
                print "[Jason se pone el traje antes de salir.]^^";
              } else {
                print "[Jason sale de la nave con su traje puesto.]^^";
              }
              move jason to exterior1;
            }
            self.mostrar_quien_sale(traje_jason in jason && traje_jason has puesto); ! t ~= 0
            rfalse;
          }
          self.mostrar_quien_sale(0);
          rfalse;
        }
    ],
    mostrar_quien_sale [ c;
      ! El parámetro c indica si el jugador sale solo de la nave, o Jason va con él
      ! c = true: Salen juntos ; c = false: El jugador sale solo
      
      ! Si es la primera vez que se sale:
      if (self hasnt general) {
        give self general;
        if (c) {
          ! Salen juntos:
          print "Ataviados con vuestros trajes atmosféricos, Jason y tú cruzais la
                 compuerta de la nave y os introducís en un reducido habitáculo destinado
                 a la descompresión y aislamiento del exterior. Madre cierra la puerta que
                 acabais de pasar, se observan chorros de aire y vapor saliendo de todas
                 los rincones del cubículo y, tras varios segundos, la compuerta exterior
                 se abre, dejando vía libre para que podáis salir de la nave.^";
          EsperarTecla();
          "^Lentamente vais bajando por la rampa de acceso. La lluvia impregna vuestros
           trajes, y vuestros pies tocan finalmente el barro de la superficie." ;
        } else {
          ! Sólo sale el jugador:
          print "Ataviado con tu traje atmosférico, cruzas la
                 compuerta de la nave y te introduces en un reducido habitáculo destinado
                 a la descompresión y aislamiento del exterior. Madre cierra la puerta que
                 acabas de pasar, se observan chorros de aire y vapor saliendo de todas
                 los rincones del cubículo y, tras varios segundos, la compuerta exterior
                 se abre, dejando vía libre para que puedas salir de la nave.^";
          EsperarTecla();
          "^Lentamente vas bajando por la rampa de acceso. La lluvia impregna tu
           traje, y tus pies tocan finalmente el barro de la superficie." ;
        }
      } else {
        if (~~(procesador.arreglado())) {
          if (c) {
            "Jason y tú salís al exterior sin problemas, protegidos por vuestros trajes
             atmosféricos.";
          } else {
            "Sales al exterior sin problemas, protegido por tu traje atmosférico.";
          }
        }
!        print " A continuación, Madre cierra la compuerta de la nave.^";      
      }
    ],      
    despues [ jason_viene;
      Ir:
        ! Si venimos de fuera:
        if (uno == obj_adentro) {
          jason_viene = jason in exterior1 &&
                        jason.tipo_de_movimiento == MOVIMIENTO_PERSEGUIR &&
                        jason.perseguido == jugador;
          if (jason_viene) {
            print "Entráis ";
          } else {
            print "Entras ";
          }
          print "al interior de la nave a través del compartimento de descompresión.^";
          self.cerrar();
          if (jason in self && jason.tipo_de_movimiento ~= MOVIMIENTO_PERSEGUIR) {
            PNJ_Ruta(jason, MOVIMIENTO_PERSEGUIR, jugador);
          } else {
            if (jason_viene) {
              move jason to nave1;
              PNJ_Ruta(jason, MOVIMIENTO_PERSEGUIR, jugador);
            }
          }
        }
    ],
    ! Comprueba si se ha llegado al final del juego. Para ello, comprueba que
    ! Jason está aquí, y que la vaina está abierta. Esto último significará que
    ! lo hemos sacado de la vaina, y por tanto, si hemos llegado hasta aquí, es
    ! que hemos acabado la aventura:
    cada_turno [ t;
      if (jason in self && vaina has abierta) {
        Bandera_Fin(2);
        if (hayGraficos) {
          t = true;
          ApagarGraficosSub(true);
        }
        EsperarTecla();
        clearMainWindow();
        tt_final.visualiza();
        Damusix.TocarV(Grito_Jason_ogg);
        if (t) {
          EncenderGraficosSub(true);
          viewImageCenter(Oscuridad_jpg);
          viewImageCenter(Logo_Alien_jpg);
        }
      }
    ];


Object decorado_nave1 nave1
  class DecoradoAmpliado
  with
    desc_instrumentos "Los paneles de instrumentación están llenos de indicadores y
                       botones por todas partes. En un lateral también se aprecia
                       un cargador de baterías.",
    desc_ventanales "A través de las ventanas que dan al exterior sólo se aprecia una
                     oscuridad tan sólo rota por el tintinear de la lluvia
                     impactando en el cristal y por la potencia de los focos que
                     iluminan el lugar de aterrizajecon un fulgor fantasmagórico.",
    desc_asientos "Son unos asientos mullidos y cómodos. Algo que contrasta mucho con
                   la deshumanización del resto del decorado.",
    desc_paredes "En una de las paredes puedes observar un compartimento destinado a
                  almacenar los trajes atmosféricos.",
    antes [;
      Tocar:
        switch (self.cantidad) {
          'ventanales', 'ventanal',
          'cristales', 'cristal',
          'ventanas', 'ventana': "Una superficie lisa que, sin embargo, traspasa la
                                  humedad del exterior.";
          'paneles', 'panel',
          'instrumentos', 'instrumento',
          'indicadores', 'indicador': "La mayoría de las funciones de la nave están
                                       gobernadas por Madre, así que lo mejor será
                                       tocar lo menos posible.";
          'asientos', 'asiento': "Son mullidos y confortables, pero ahora no es el
                                  momento de sentarse.";
          'paredes', 'pared': "Son frías y sólidas al tacto.";
        }
    ],
    describir
      'paneles'      [; return self.desc_instrumentos; ] G_PLURAL
      'panel'        [; return self.desc_instrumentos; ] 0
      'instrumentos' [; return self.desc_instrumentos; ] G_PLURAL
      'instrumento'  [; return self.desc_instrumentos; ] 0
      'indicadores'  [; return self.desc_instrumentos; ] G_PLURAL
      'indicador'    [; return self.desc_instrumentos; ] 0
      'botones'      [; return self.desc_instrumentos; ] G_PLURAL
      'boton'        [; return self.desc_instrumentos; ] 0
      'ventanales'   [; return self.desc_ventanales; ] G_PLURAL 
      'ventanal'     [; return self.desc_ventanales; ] 0
      'ventanas'     [; return self.desc_ventanales; ] G_FEMENINO + G_PLURAL
      'ventana'      [; return self.desc_ventanales; ] G_FEMENINO
      'cristales'    [; return self.desc_ventanales; ] G_PLURAL
      'cristal'      [; return self.desc_ventanales; ] 0
      'lluvia'       [; return self.desc_ventanales; ] G_FEMENINO
      'asientos'     [; return self.desc_asientos; ] G_PLURAL
      'asiento'      [; return self.desc_asientos; ] 0
      'paredes'      [; return self.desc_paredes; ] G_FEMENINO + G_PLURAL
      'pared'        [; return self.desc_paredes; ] G_FEMENINO;


Object nave "nave"
  with
    nombre_m 'vehiculo',
    nombre_f 'nave' 'alpha',
    adjetivos 'exploracion',
    genero G_FEMENINO,
    esta_en nave1 nave2 exterior1,
    antes [;
      Abrir:
        <<Abrir puerta_nave>>;
      Cerrar:
        <<Cerrar puerta_nave>>;
      Examinar:
        switch (LugarReal()) {
          nave1, nave2: ImprimirOEjecutar(LugarReal(), descripcion);
          exterior1: "Parece más grande por fuera...";
        }
        rtrue;
    ],
  has
    escenario;
 
    
Object descompresion "cámara de descompresión"
  with
    nombre_m 'habitaculo',
    nombre_f 'camara' 'cabina' 'descompresion' 'despresurizacion',
    genero G_FEMENINO,
    descripcion "Es el recinto intermedio entre la nave y el exterior. Se usa para
                 acomodar la presión y la atmósfera al salir o entrar en la nave.",
    antes [;
      Meterse:
        <<Salir>>;
    ],
  has
    escenario;


Object puerta_nave "compuerta de la nave" nave1
  class PNJPuerta
  class EscenarioAbrible
  with
    ! La librería PnjPuertas ya incluye la palabra 'puerta' como nombre
    nombre 'compuerta',
    adjetivos 'nave' 'vehiculo',
    descripcion [;
      if (self hasnt abierta) {
        print "La puerta está cerrada, impidiendo el acceso al ";
        if (LugarReal() == nave1) {
          "exterior.";
        } else {
          "interior.";
        }
      } else {
        print "La puerta está abierta y permite acceder al ";
        if (LugarReal() == nave1) {
          print "exterior";
        } else {
          print "interior";
        }
        " de la nave.";
      }
    ],
    pnj_abrir [ act;
      act = act;
      if (self hasnt abierta) {
        print "La puerta de la nave está cerrada.^";
        rfalse;
      } else {
        return 2;
      }
    ],
    afuera exterior1,
    adentro nave1,
    interior nave1,
    exterior exterior1,
    antes [;
      Abrir:
        if (self hasnt abierta) {
          "No puedes abrir la compuerta directamente. Pide ayuda a la computadora.";
        } else {
          "La compuerta de la nave ya está abierta.";
        }
      Cerrar:
        if (self has abierta) {
          "No puedes cerrar la compuerta directamente. Pide ayuda a la computadora.";
        } else {
          "La compuerta de la nave ya está cerrada.";
        }
      ! Esto es para que la librería no diga que está abierta o cerrada:
      Examinar:
        ImprimirOEjecutar(self, descripcion);
        rtrue;
    ],
  has
    femenino;


Object compartimento_trajes "compartimento de trajes" nave1
  class Contenedor
  class EscenarioAbrible  
  with
    nombre 'compartimento',
    adjetivos 'trajes',
    descripcion_real "El compartimento donde se almacenan los trajes se encuentra
                      acoplado a una de las paredes de la nave.",
    antes [;
      Abrir, Cerrar:
        "No puede hacerse directamente. Pide ayuda a la computadora.";
    ],
  has
    escenario transparente;
    

Object cargador "cargador" nave1
  with
    nombre 'cargador' 'recargador',
!    esta_en nave1 oficina aula,
    adjetivos 'bateria' 'baterias',
    descripcion [;
      print "Es un pequeño compartimento metálico con la forma y el tamaño justos para
             poder contener una batería estándar.";
      if (bateria in self) {
        print " Dentro está la batería de la linterna.";
      }
      "";
    ],
    antes [;
      Coger:
        "El cargador está fijo en el panel de instrumentos.";
    ],
    ! Impide que puedas meter otra cosa que no sean las baterías:
    reaccionar_antes [;
      RecargarEn:
        if (bateria in jugador || parent(bateria) in jugador) {
          if (bateria in linterna && linterna hasnt abierta) {
            give linterna abierta;
            print "Abres la linterna para sacar la batería. ";
          }
          move bateria to jugador;
          <<Meter bateria cargador>>;
        } else {
          "No tienes nada que recargar.";
        }
        
      Meter:
        if (otro == self) {
          if (uno ~= bateria) {
            HablaMadre("El cargador sólo admite baterías de formato estándar.");
            rtrue;
          }
        }
    ],
    despues [;
      Recibir:
        bateria.energia = MAX_NIVEL_CARGA;
        print "Metes la batería de la linterna dentro del cargador...^";
        if (LugarReal() == nave1) {
          HablaMadre("Batería recargada al 100%.");
        }
        rtrue;
    ],
  has
    escenario recipiente abierto;


Class TrajeAtmosferico
  with
    nombre 'traje' 'atmosferico' 'trajes//p' 'atmosfericos//p',
!    nombre_corto "traje atmosférico",
!    plural "trajes atmosféricos",
!    describir [;
!      "El traje atmosférico ", (string) self.color, " yace inerte en el suelo.";
!    ],
    color "",
    lateral "",
    ! Los trajes se muestran juntos al listarse una localidad o un inventario. Quedaría:
    ! "Puedes ver dos trajes atmosféricos (tu traje y el traje de Jason)"
    listar_juntos "trajes atmosféricos",
    descripcion [;
      "Es un traje hermético, compuesto por un mono de un material resistente, botas,
       guantes y un casco destinado a proporcionar aislamiento y oxígeno al usuario.
       Es de color ", (string) self.color, ", y lleva impreso ", (string) self.lateral,
       " en un lateral.";
    ],
    antes [ l;
      Tocar:
        "El traje atmosférico está compuesto de un tejido fino pero muy resistente,
         permitiendo una gran movilidad sin perder sus características de aislamiento.";
      Desvestir:
        ! El jugador (o Jason) muere si se quita el traje fuera de la nave si no está
        ! arreglado el procesador atmosférico
        if (actor == jugador) {
          l = LugarReal();
        } else {
          l = parent(actor);
        }
        if (l ~= nave1 or nave2 && circuito hasnt general) {
          if (actor == jugador) {
            "No deberías quitarte el traje fuera de la nave hasta que el aire sea
             respirable.";
          } else {
            HablaJason("No quiero morir de asfixia, señor...");
            rtrue;
          }
        }
        rfalse;
    ],
    despues [;
      Examinar:
        ! El jugador ya puede ver las botas, los guantes y el casco
        give self transparente;
        ! Hacer un salto de línea para que quede mejor
!        "";
    ],
    listarse [;
      if (etapa_inventario == 2) {
        if (self has puesto) {
          print " (puesto)";
        }
        rtrue;
      }
    ],
  has
    prenda transparente;


Object tu_traje "traje" compartimento_trajes
  class TrajeAtmosferico
  with
    nombre 'mi' 'tu',
    ! Para poder decir: "coge mi traje", o "coge el traje rojo"
    adjetivos 'rojo',
    ! Para que diga: "puedes ver tu traje"
    articulos "Tu" "tu" "tu",
    color "rojo",
    lateral "tu nombre",
    reaccionar_antes [;
      Tocar:
        if (self has puesto && uno ~= soldador && uno ~= rastro_acido) {
          "Tu tacto es muy poco sensible a través de los guantes.";
        }
      Oler:
        if (self has puesto) {
          "Apenas puedes oler nada a través del casco.";
        }
      Escuchar:
        if (self has puesto && (~~(parent(self) ofclass ZonaNorte))) {
          "El casco te impide oir nada con claridad.";
        }
      Probar:
        if (self has puesto) {
          "No puedes llevarte nada a la boca con el casco puesto.";
        }
    ],
    antes [;
      ! El jugador no puede ponerse dos trajes a la vez
      Vestir:
        if (traje_jason in jugador && traje_jason has puesto) {
          print_ret "Ya llevas puesto ", (el) traje_jason, ".";
        }
    ];
    

Object traje_jason "traje de Jason" compartimento_trajes
  class TrajeAtmosferico
  with
    ! Para poder decir: "jason, coge tu traje", "coge el traje de jason" o
    ! "coge el traje verde"
    adjetivos 'jason' 'verde',
    ! No sé si es realmente necesario, pero lo pongo por si acaso...
    articulo "el",
    color "verde",
    lateral "el nombre de Jason",
    listarse [;
      if (parent(self) == jason) {  ! En el caso de que el traje lo lleve Jason:
        if (accion == ##Inv) {
          print "mi traje";         ! Caso "jason, inventario"
        } else {
          print "su traje";         ! Caso "ex jason"
        }
      } else {
        print "el traje de Jason";  ! En el caso de que el traje no lo lleve Jason
      }
      if (self has puesto) {
        print " (puesto)";
      }
      rtrue;
    ],
    antes [;
      Vestir:
        if (uno == traje_jason) {
          "No es de tu talla.";
        }
        if (tu_traje in jugador && tu_traje has puesto) {
          print_ret "Ya llevas puesto ", (el) tu_traje, ".";
        }
    ];


! El decorado de los trajes (ver rutina AlAlcance):

Object decorado_traje
  class DecoradoAmpliado
  with
    desc_guantes "Sujetos firmemente al resto del traje, son flexibles y muy cómodos.",
    desc_botas "Recias y fuertes, para su uso en cualquier entorno.",
    antes [;
      Tocar:
        switch (self.cantidad) {
          'guantes', 'guante': "Son guantes fuertes y resistentes.";
          'botas', 'bota': "Las botas son gruesas, y tienen una suela dura como el
                            granito.";
          'casco': "El casco es liso y suave al tacto, pero tan duro como el metal, si
                    bien no da sensación de frío.";
        }
      Vestir:
        <<Vestir tu_traje>>;
      Desvestir:
        <<Desvestir tu_traje>>;
    ],
    describir
      'guantes' [; return self.desc_guantes; ] G_PLURAL
      'guante'  [; return self.desc_guantes; ] 0
      'botas'   [; return self.desc_botas; ] G_FEMENINO + G_PLURAL
      'bota'    [; return self.desc_botas; ] G_FEMENINO
      'casco' "Un casco herméticamente cerrado que te permite respirar en este nocivo
               ambiente, y provisto de un enorme visor." 0;


Object traje_inutil "restos del traje de Jason"
  with
    nombre 'restos' 'traje',
    adjetivos 'jason',
    articulo "los",
    descripcion "Esto es lo que ha quedado del traje de Jason tras su enfrentamiento con
                 la criatura. Tiene una generosa hendidura en un lateral. Sin duda, está
                 del todo inservible.",
    inicial "El traje de Jason reposa inservible en el suelo de la sala.", 
    antes [;
      Examinar:
        rfalse;
      default:
        "No merece la pena. No sirve para nada ya.";
    ],
  has
    nombreplural transparente;
    

Object decorado_traje_inutil
  class DecoradoAmpliado
  with
    desc_guantes "Los guantes han perdido su estanqueidad con el resto del traje.",
    desc_botas "Las botas están llenas de barro y de la misma sustancia pegajosa que
                se puede apreciar en toda la estancia.",
    antes [;
      Tocar:
        switch (self.cantidad) {
          'guantes', 'guante': "Son guantes fuertes y resistentes.";
          'botas', 'bota': "Están pegajosas debido a la asquerosa sustancia que las
                            recubre.";
          'casco': "La rotura del casco parece causada por algún tipo de sustancia
                    cáustica, más que por un objeto cortante o romo.";
        }
    ],
    describir
      'guantes' [; return self.desc_guantes; ] G_PLURAL
      'guante'  [; return self.desc_guantes; ] 0
      'botas'   [; return self.desc_botas; ] G_FEMENINO + G_PLURAL
      'bota'    [; return self.desc_botas; ] G_FEMENINO
      'casco'   "El casco está roto por la parte lateral del visor." 0;


Object nave2 "Parte posterior de la nave"
  class Lugar
  with
    descripcion "Esta zona de la nave está destinada a bodega y almacenamiento. El armario
                 plateado y brillante situado al fondo es el único mobiliario visible. Por
                 lo demás, destaca el enorme contraste con la proa de la nave en cuanto a
                 falta de decoración e instrumentación.",
    al_e nave1,
    sgw_img Nave2_jpg,
    sgw_mus Nave_ogg;


Object decorado_nave2 nave2
  class DecoradoAmpliado
  with
    desc_paredes "Las paredes en esta zona de la nave resultan más austeras y frías.",
    antes [;
      if (self.cantidad == 'paredes' or 'pared') {
        "Frías y secas al tacto.";
      }
    ],
    describir
      'nave'    [; return nave2.descripcion; ] G_FEMENINO
      'paredes' [; return self.desc_paredes; ] G_FEMENINO + G_PLURAL
      'pared'   [; return self.desc_paredes; ] G_FEMENINO;


Object armario "armario" nave2
  class Contenedor
  class EscenarioAbrible  
  with
    nombre 'armario',
    descripcion_real "Un armario metálico, de aspecto plateado y liso. Sobresale de una
                      pared al fondo.",
    antes [;
      Tocar: "Suave, liso... pero fuerte al mismo tiempo.";
    ];


Object destornillador "destornillador" armario
  with
    nombre 'destornillador' 'atornillador',
    descripcion "Un destornillador manual de precisión, con puntas intercambiables para
                 poder trabajar con tornillos de cualquier forma y tamaño estándar.";


Object palanca "palanca" armario
  with
    nombre 'palanca',
    descripcion "Una palanca de un metal duro pero ligero.",
  has
    femenino;


Object linterna "linterna" armario
  with
    nombre 'linterna',
    descripcion [;
      print "Una linterna ";
      if (self has encendida)
        print "(encendida)";
      else
        print "(apagada)";
      ".";
    ],
    antes [;
      Recibir:
        if (uno ~= bateria) {
          "En la linterna sólo puedes meter una batería estándar.";
        }
      Examinar:
        print "Es un dispositivo que proyecta un haz de luz directo muy potente";
        if (self hasnt encendida) {
          print ". Está apagada";
          if (self has abierta) {
            print ", pero no la podrás encender hasta que la cierres";
          }
        } else {
          if (bateria.energia < 10) {
            print ". Brilla con una luz muy tenue";
          } else {
            print ". Está encendida";
          }
        }
        
        if (bateria notin self) {
          ". Además, le falta la batería.";
        } else {
          ". Lleva dentro una batería de energía que actualmente está al ",
           bateria.nivel_carga(), "% de su capacidad de carga.";
        }
      Quemar: <<Encender self>>;
      Encender:
        if (bateria notin self) {
          "A la linterna le falta la batería.";
        }
        if (self has abierta) {
          "Tienes que cerrar la linterna primero.";
        }
        if (bateria.energia <= 0) {
          "Desgraciadamente, parece que se ha agotado la batería.";
        }
    ],
    encender [;
      give self encendida;
      give self luz;
      ArrancarDaemon(self);
    ],
    apagar [;
      give self ~encendida;
      give self ~luz;
      PararDaemon(self);
    ],
    despues [;
      Abrir:
        if (self has encendida) {
          self.apagar();
          "Abres la linterna. Al hacerlo, esta se apaga automáticamente. Dentro ves
           la batería que le suministra energía.";
        } else {
          if (bateria in linterna) {
            "Abres la linterna. Dentro ves la batería que le suministra energía.";
          }
        }
      Encender:
        self.encender();
      Apagar:
        self.apagar();
      Recibir:
        print "Colocas la batería dentro de la linterna, y a continuación la cierras.^";
        tate_callao = 1;
        <Cerrar self>;
        tate_callao = 0;
        rtrue;
    ],
    daemon [;
      if (self has encendida && bateria.energia > 0) {
        bateria.energia--;
        
        if (bateria.energia == 0) {
          self.apagar();
          "^La linterna se ha quedado sin batería.";
        }
        if (bateria.energia < 10) {
          "^La linterna se está quedando sin batería...";
        }
      }
    ],
    listarse [;
      if (etapa_inventario == 2) {
        if (self has encendida) {
          print " (encendida)";
        } else {
          print " (apagada)";
        }
        rtrue;
      }
    ],
  has
    femenino conmutable abrible recipiente transparente;


Object bateria "batería" linterna
  with
    nombre_f 'bateria' 'pila',
    nombre_fp 'baterias' 'pilas',
    genero G_FEMENINO,
    descripcion [ p;
      print "La batería de la linterna es del tipo RU75 universal. ";
      p = parent(self);

      if (~~((p ofclass Lugar) || (p ofclass Movil) || (bateria in jugador))) {
        print "Está colocada en ", (el) p, ". ";
      }
      
      "Actualmente está al ", self.nivel_carga(), "% de su capacidad.";
    ],
    nivel_carga [; return (self.energia * 100) / MAX_NIVEL_CARGA; ],
    energia MAX_NIVEL_CARGA,
    antes [;
      Encender, Atar:
        <<Atar otro uno>>;
    ];


Object exterior1 "Junto a la nave"
  class Lugar
  with
    descripcion "Estás en el exterior, junto a tu nave. La tenue luz blanquecina que flota
                 en el ambiente apenas te deja discernir las enormes y majestuosas formas
                 de la colonia minera. A causa de la lluvia, una neblina fantasmagórica lo
                 envuelve todo, lo que hace que las siluetas de la colonia se difuminen.
                 La temperatura es elevada, y la sensación de agobio, palpable.",
    sgw_img Exterior1_jpg,
    sgw_mus Lluvia_ogg,
    sgw_vol 50,
    adentro puerta_nave,
    al_s exterior3,
    al_n junto_procesador,
    antesPNJ [;
      Ir:
        if (uno == obj_adentro && puerta_nave hasnt abierta) {
          HablaJason("La compuerta de la nave está cerrada.");
        }
    ],
    despues [;
      Ir:
        ! Si venimos de dentro de la nave, Madre cierra la puerta automáticamente
        if (uno == obj_afuera) {
          nave1.cerrar();
        }
    ];


Object decorado_exterior1 exterior1
  class DecoradoAmpliado
  with
    desc_niebla "Es un efecto provocado por la intensa lluvia, que te impide ver con
                 claridad los límites exteriores de la colonia.",
    describir
      'luz'     "Es un halo inmaterial que lo envuelve todo con un manto azulado." G_FEMENINO
      'lluvia'  "Las gotas de lluvia caen con fuerza, casi con intención." G_FEMENINO
      'neblina' [; return self.desc_niebla; ] G_FEMENINO
      'niebla'  [; return self.desc_niebla; ] G_FEMENINO
      'colonia' "La colonia minera se alza no muy lejos, esperando." G_FEMENINO;


Object junto_procesador "Frente al procesador atmosférico"
  class Lugar
  with
    descripcion "Te encuentras junto a la entrada al recinto de la maquinaria de
                 purificación ambiental. En el interior es donde se produce y se renueva
                 el aire para la colonia. Una puerta metálica da acceso al procesador, y
                 junto a ella se ha situado un panel de control. El edificio es altísimo,
                 y su fachada frontal tiene signos de haber sido maltratada. Sigue
                 lloviendo, y el calor es asfixiante.",
    sgw_img Junto_Procesador_jpg,
    sgw_mus Junto_Procesador_ogg,
    sgw_vol 50,
    al_s exterior1,
    adentro puerta_procesador,
    tenga_cuidado false,
    antes [;
      Ir:
        if (uno == obj_adentro && (~~(procesador.arreglado())) &&
            jason in LugarReal() && puerta_procesador has abierta &&
            ~~(self.tenga_cuidado)) {
          self.tenga_cuidado = true;
          print "Jason apoya una mano en tu hombro y mirándote fijamente te dice...^";
          HablaJason("Tenga cuidado...");
          EsperarTecla();
        }
    ],
    antesPNJ [;
      Ir:
        if (otro == obj_adentro) {
          if (puerta_procesador has abierta) {
            if (linterna in jason) {
              if (bateria notin linterna) {
                HablaJason("Está demasiado oscuro para ver nada, y a la linterna le
                            falta la batería.");
                rtrue;
              }
              if (bateria.energia <= 0) {
                HablaJason("Está demasiado oscuro para ver nada, y la linterna no
                            tiene energía suficiente.");
                rtrue;
              }
              print "Jason ";
              if (linterna hasnt encendida) {
                give linterna ~abierta;
                linterna.encender();
                print "enciende la linterna. A continuación, ";
              }
              print "se acerca a la puerta y se asoma al interior. Mueve el haz de
                     la linterna varias veces en distintas direcciones, y concluye:^";
              HablaJason("Parece que todo está tranquilo. Puede pasar, pero tenga
                          cuidado.");
            } else {
              if (linterna in jugador) {
                HablaJason("Está demasiado oscuro para ver nada. Páseme su linterna.");
              } else {
                HablaJason("Está demasiado oscuro para ver nada. Deberíamos buscar
                            una fuente de luz.");
              }
            }
          } else {
            HablaJason("Primero habrá que buscar la forma de abrir la puerta.");
          }
          rtrue;
        }
    ];


Object decorado_junto_procesador junto_procesador
  class DecoradoAmpliado
  with
    desc_paredes "La lluvia y otros fenómenos atmosféricos han hecho mella en la otrora
                  lisa y pulida superficie de la pared del complejo.",
    describir
      'paredes'  [; return self.desc_paredes; ] G_FEMENINO + G_PLURAL
      'pared'    [; return self.desc_paredes; ] G_FEMENINO
      'fachada'  [; return self.desc_paredes; ] G_FEMENINO
      'edificio' "La construcción tiene una amplia base cilíndrica que se prolonga con
                  forma semicónica elevándose muy alto hacia el cielo." 0
      'luz'      "Es un halo inmaterial que lo envuelve todo con un manto blanco-azulado."
                 G_FEMENINO
      'lluvia'   "Las gotas de lluvia caen con fuerza, casi con intención."
                 G_FEMENINO
      'entrada'  "Sobria y, en cierta forma, intimidante..."
                 G_FEMENINO;


Object cables "cables" junto_procesador
  with
    nombre 'cable' 'cables//p',
    descripcion [;
      if (bateria notin panel_control) {
        "Son unos siete u ocho cables de varios colores. Sobresalen, inertes, del
         panel de control.";
      } else {
        "Dos de los cables están temporalmente unidos a los bornes de la batería.";
      }
    ],
    antes [;
      Tocar:
        if (panel_control has general) {
          "Ahora parece que los cables dan algo de calambre.";
        } else {
          "Incluso mojados como están, no te provocan ni siquiera un ligero
           calambre, lo que te hace pensar que por ellos no circula energía.";
        }
        
      Encender:
        <<Atar uno otro>>;
      
      Recibir:
        <<Atar uno otro>>;

      Atar:
        if (otro ~= nothing && otro ~= cables) {
          if (otro == bateria) {
            <<Meter bateria panel_control>>;
          } else {
            "No es lógico conectar los cables ", (al) otro, ".";
          }
        } else {
          "No consigues nada uniendo los cables entre sí.";
        }
    ],
  has
    escenario nombreplural;

    
Object puerta_procesador "puerta del procesador atmosférico" junto_procesador
  class PNJPuerta
  class EscenarioAbrible
  with
    ! La librería PnjPuertas ya incluye la palabra 'puerta' como nombre
    nombre 'compuerta',
    adjetivos 'procesador' 'metalica',
    descripcion [;
      if (self hasnt abierta)
        "La puerta impide el acceso al interior del procesador atmosférico.";
      else
        "La puerta está abierta y permite el paso al procesador atmosférico.";
    ],
    adentro procesador,
    afuera junto_procesador,
    interior procesador,
    exterior puerta_procesador,     
    antes [;
      Abrir:
        if (self hasnt abierta) {
          "Para abrir la puerta necesitarás accionar algún mecanismo.";
        } else {
          "La puerta ya está abierta.";
        }
      Cerrar:
        "No puedes hacerlo con el panel de control en esas condiciones.";
      ! Esto es para que la librería no diga que está abierta o cerrada:
      Examinar:
        ImprimirOEjecutar(self, descripcion);
        rtrue;
    ],
  has
    femenino;


! La puerta sólo se puede abrir usando el panel de control.
! general sirve para indicar que el panel está listo para recibir el código de apertura
! (es decir: tiene dentro la batería y tiene energía suficiente)

Object panel_control "panel de control" junto_procesador
  with
    nombre 'cuadro' 'mandos' 'panel' 'control' 'teclado' 'display' 'pantalla'
           'visualizador' 'proteccion',
    descripcion [;
      print "Un cuadro de mandos con un display visualizador y un teclado, situado junto
             a la puerta. Su panel de protección está abierto, faltan algunos tornillos y
             varios cables sobresalen de su interior.";
      if (bateria in self) {
        print " Varios de sus cables están temporalmente unidos a los bornes de una
               batería. ";
        if (puerta_procesador hasnt abierta) {
          print "En el display puedes leer ~";
          style bold;
          print "Semilla: 9957. ¿Código?";
          style roman;
          print "~.";
        }
      } else {
        if (puerta_procesador hasnt abierta) {
          print " Aunque su aspecto no es muy bueno, concluyes que lo único que
                  necesitaría para funcionar correctamente es algún aporte de energía
                  eléctrica.";
        } else {
          print " Parece totalmente muerto.";
        }
      }
      "";
    ],
    ! codigo_viejo: se usa en el objeto computadora
    ! codigo_viejo == 0: El jugador aún no ha intentado el código antiguo
    ! codigo_viejo == 1: El jugador ha intentado meter el código antiguo
    codigo_viejo 0,
    ! Impide que puedas meter otra cosa que no sean las baterías
    reaccionar_antes [;
      Meter:
        if (otro == self && uno ~= bateria) {
          ! Intentamos meter algo que no es la batería
          "No parece que tenga demasiado sentido poner eso ahí.";
        }
    ],
    ! general sirve para indicar que la batería está puesta y tiene energía suficiente
    antes [ t ;
      Encender, Quemar, Apagar:
        "No es algo que necesite encenderse o apagarse.";
      Teclear:
        if (self hasnt general) {
          "El panel de control no tiene energía suficiente.";
        }
        t = DireccionDePalabra(2);
        if (LongitudDePalabra(2) == 6 && t->0 == '2' && t ->1 == '6' &&
            t->2 == '8' && t->3 == '7' && t->4 == '4' && t->5 == '1') {
          ! Código nuevo: 268741
          puerta_procesador.abrir();
          junto_procesador.sgw_img = Junto_Procesador_Abierto_jpg;
          viewImageCenter(junto_procesador.sgw_img);
          give self ~general;
          bateria.energia = 0;
          print "El display dice: ~";
          style bold;
          print "Código correcto. Acceso permitido.";
          style roman;
          "~^^La puerta se abre y, a continuación, una enorme chispa salta de los bornes de
          conexión de la batería. Finalmente, el panel de control se apaga por falta de
          energía.";
        } else if (LongitudDePalabra(2) == 6 && t->0 == '7' && t ->1 == '5' &&
            t->2 == '9' && t->3 == '8' && t->4 == '1' && t->5 == '2') {
          ! Código viejo: 759812
          if (self.codigo_viejo == 0) {
            self.codigo_viejo = 1;
            print "El display dice: ~";
            style bold;
            print "Código incorrecto. Acceso denegado.";
            style roman;
            print "~^^Parece que ese código ya no sirve, después de que el sistema de
                      control del panel se haya reiniciado.^";
            if (SeVen(jugador, jason)) {
              if (SeVen(jugador, robot)) {
                print "Jason se dirige al androide:^";
                jason.hablar = false;
                robot.hablar = false;
                HablaJason("759812... 759812... ¡Esa no es la clave, saco de tuercas!");
                HablaRobot("Código... cambiado... cambiado...");
                HablaJason("A ti sí que te voy a cambiar la cara como no te calles...");
              } else {
                jason.hablar = false;
                HablaJason("¡Ey! ¿Es que alguien está mintiendo aquí?");
              }
            }
            rtrue; 
          } else {
            print "El display insiste: ~";
            style bold;
            print "Código incorrecto. Acceso denegado.";
            style roman;
            "~. Habrá que buscar el nuevo código.";
          }
        } else {
          print "El display muestra: ~";
          style bold;
          print "Código incorrecto. Acceso denegado.";
          style roman;
          "~.";
        }
      Recibir:
        if (puerta_procesador has abierta) {
          ! Si la puerta ya está abierta, no tiene sentido volver a meter la batería
          "La puerta ya está abierta, y el panel está destrozado. No tiene sentido volver
           a colocar la batería ahí.";
        }
      Abrir:
        "El panel de protección ya está abierto.";
      Cerrar:
        "No puedes: está roto.";
      Atar:
        if (otro == bateria) {
          <<Meter bateria panel_control>>;
        } else {
          "No es lógico conectar la batería ", (al) otro, ".";
        }
    ],
    despues [;
      BuscarEn:
        if (bateria notin self) {
          "Está lleno de cables de varios colores.";
        }
        
      Recibir:
        print "Metes la batería dentro del panel y ajustas varios cables sueltos a sus
               bornes, ";
        if (bateria.energia < 20) {
          "pero no parece dar mucho resultado. Probablemente la batería no tenga suficiente
           energía.";
        } else {
          give self general;
          print "provocando una lluvia de chispas. El display se enciende y suena un
                 zumbido. En la pantalla aparecen símbolos y caracteres que indican
                 que el sistema de control del panel se está reiniciando. Finalmente,
                 en el display puedes leer: ~";
          style bold;
          print "Semilla: 9957. ¿Código?";
          style roman;
          "~.";
        }
      DejarSalir:
        if (self has general) {
          give self ~general;
          "Al sacar la batería, el panel de control se apaga y enmudece.";
        } else {
          "Sacas la batería, soltando los cables que la mantenían unida al panel de
           control.";
        }
    ],                 
  has
    escenario recipiente abierto;


Object procesador "Procesador atmosférico"
  class Lugar
  with
    descripcion [;
      print "Este es el corazón de la maquinaria de purificación ambiental. Una red de
             tuberías y dispositivos de refrigeración y renovación se extiende hasta
             donde alcanza tu vista. Hay charcos de agua por todas partes debido a las
             goteras. La humedad en el ambiente provoca que el calor aquí dentro sea casi
             insoportable. La instalación está gobernada por una unidad central rodeada
             por cientos de cables.";
      if (self.arreglado()) {
        print " La maquinaria opera ahora a plena potencia, suministrando oxígeno y aire
               perfectamente respirable a las zonas cercanas al procesador de aire.";
      }
      "";
    ],
    sgw_img Procesador_jpg,
    sgw_mus Maquinaria_ogg,
    afuera puerta_procesador,
    antes [;
      Ir:
        if (uno == obj_afuera && alien.comportamiento > 0 && rifle notin jugador) {
          "¡No te vayas sin dejar aquí el arma de Jason! Podría serte de mucha utilidad.";
        } 
    ],
    cada_turno [;
      self.comprobar_segunda_parte();
    ],
    ! ¿Se ha arreglado el procesador atmosférico?
    arreglado [;
      return circuito has general;
    ],
    ! Comprobamos si hay que pasar a la seguda parte (el alien se lleva a Jason):
    comprobar_segunda_parte [;
      if (alien.comportamiento == 0) {
        ! Si hay luz, y está el alien, y entras solo, te mata. Si entras con Jason, se lo
        ! lleva a él
        if (HayLuz(self) && alien in self) {
          if (jason notin self) {
            remove comunicador;         ! Esto es para que Jason no hable
            give comunicador ausente;   ! cuando nos están matando
            print "^El monstruo gira la cabeza hacia ti, y emite un chillido
                   insoportable, agudo, que te hiela la sangre nada más oirlo. Te quedas
                   petrificado ante tal visión, y sólo tienes fuerzas para observar
                   a la criatura acercarse cada vez más deprisa hasta tu posición. Por tu
                   mente pasa entonces una única idea: ~Ojalá estuviera Jason aquí...~.
                   Instantes después, todo es oscuridad y silencio...^^";
            EsperarTecla();
            Bandera_Fin(1);
            "Cuando despiertas, notas que sólo puedes mover los ojos. Estás completamente
             inmóvil, atrapado dentro de una especie de capullo gelatinoso de una enorme
             dureza. Y es cuando te das cuenta de que lo más terrible no es eso, sino la
             certeza de saber que nadie podrá oir tus gritos...^";
          } else {
            SegundaParte();
            ! En un par de turnos se oye a alien rompiendo la puerta del complejo
            ! (ver rutina ruido_puerta_rota.tiempo_agotado)
            ArrancarReloj(ruido_puerta_rota, 2);
            Damusix.TocarV(Grito_Jason_ogg);
            viewImageCenter(Alien_Rapta_Jason_jpg);
            print "^El monstruo gira la cabeza hacia vosotros, y emite un chillido
                   insoportable, agudo, que os hiela la sangre nada más oirlo. Tú te quedas
                   petrificado ante tal visión, pero Jason grita salvajemente y comienza
                   a disparar su rifle contra la criatura, que eleva su grito sobre el del
                   soldado. Rápidamente, el ser se dirige hasta donde se encuentra Jason, y
                   de un sólo movimiento lo atrapa por el cuello y sale corriendo por la
                   puerta del procesador atmosférico. Comienzas a respirar de nuevo cuando
                   te das cuenta de que estás solo...^";
            EsperarTecla();
            viewImageCenter(procesador.sgw_img);
          }
        }
      }
    ],
  has ~luz;


Object unidad_central "unidad central" procesador
  with
    nombre 'instalacion' 'unidad' 'central',
    descripcion "La unidad central gobierna el funcionamiento del procesador atmosférico.
                 Es, básicamente, una masa informe de cables y tubos. Una caja de
                 conexiones es lo que más llama tu atención.",
  has escenario femenino;


Object decorado_procesador procesador
  class DecoradoAmpliado
  with
    desc_cables "Los cables entran y salen por decenas de la unidad central.",
    desc_tubos "Metros y metros de tubos lisos y estriados serpentean por la sala y hacia
                el exterior.",
    desc_charcos "El agua estancada se forma a base de paciencia y gotas de agua que se
                  filtran por el techo.",
    describir
      'cables'   [; return self.desc_cables; ] G_PLURAL
      'cable'    [; return self.desc_cables; ] 0
      'tubos'    [; return self.desc_tubos; ] G_PLURAL
      'tuberias' [; return self.desc_tubos; ] G_FEMENINO + G_PLURAL
      'tuberia'  [; return self.desc_tubos; ] G_FEMENINO
      'tubo'     [; return self.desc_tubos; ] 0
      'charcos'  [; return self.desc_charcos; ] G_PLURAL
      'charco'   [; return self.desc_charcos; ] 0
      'agua'     [; return self.desc_charcos; ] G_FEMENINO;

      
Object conexiones "caja de conexiones" procesador
  class Contenedor
  class EscenarioAbrible
  with
    nombre 'caja',
    adjetivos 'conexiones',
    descripcion_real "Una caja de conexiones llena de cables y de circuitos electrónicos
                      interconectados unos con otros.",
    si_abierta "La caja de conexiones de la unidad central está abierta.",
    con_llave destornillador,
    antes [;
      ! Para el caso de ATORNILLAR y DESATORNILLAR
      Girar:
        if (destornillador in jugador) {
          <<QuitarCerrojo uno destornillador>>;
        } else {
          "No tengo la herramienta apropiada.";
        }
      Abrir:
        ! Si intentas abrir con algo que no sea el destornillador
        if (self has cerrojoechado) {
          switch (otro) {
            nothing: "La tapa de la caja está sujeta con un fuerte tornillo.";
            destornillador: break;
            default: print_ret (_El) otro, " no sirve para quitar el tornillo.";
          }
        }
      Recibir:
        ! Intentas meter algo dentro de la caja
        "No cabe nada dentro; sólo puede contener circuitos.";
      QuitarCerrojo:
        if (otro ~= nothing) {
          if (self hasnt abierta) {
            ! Se intenta abrir la caja con algo
            if (self has cerrojoechado && otro ~= destornillador) {
              print_ret (_El) otro, " no sirve para quitar el tornillo.";
            }        
            if (self hasnt cerrojoechado) {
              "La caja ya está lo bastante abierta como para poder trabajar con su
               contenido.";
            }
          } else {
            "La caja ya está lo bastante abierta como para poder trabajar con su
             contenido.";
          }
        } else {
          "Necesitas usar algo para poder quitarle el tornillo a la tapa.";
        }
    ],
    despues [;
      QuitarCerrojo:
        print "Abres la caja de conexiones quitando un tornillo de cierre que impedía
               abrir la tapa. A continuación tiras el tornillo lejos de ti, inservible
               ya.^";
        remove tornillo;
        give tornillo ausente;
        tate_callao = 1;  ! Para que no diga que abres la caja a continuación
        <Abrir self>;     ! Abres la caja (pero el intérprete no dice nada)
        tate_callao = 0;  ! Vuelves a la situación normal
        rtrue;    
    ],
  has
    femenino cerrojo cerrojoechado;


Object tornillo "tornillo" procesador
  with
    nombre 'tornillo',
    descripcion "Es un tornillo bastante fuerte, de un tamaño típico, con punta 4-X.",
    antes [;
      Examinar: rfalse;
      Abrir:
        <<Abrir conexiones otro>>;
      Girar:
        <<Girar conexiones otro>>;
      QuitarCerrojo:
        <<QuitarCerrojo conexiones otro>>;
      Coger, Sacar, Empujar, Tirar:
        "Para poder retirar el tornillo necesitarás una herramienta.";
    ],
  has
    escenario;


! El circuito. El atributo general indica que está arreglado

Object circuito "circuito fotónico" conexiones
  with
    nombre_m 'circuito' 'fotonico' 'danado' 'defectuoso' 'estropeado' 'abierto',
    nombre_f 'conexion' 'danada' 'abierta',
    genero G_MASCULINO,
    descripcion [;
      print "Es un circuito elaborado con material polimérico. Por su ubicación,
             parece destinado a gestionar una parte importante del sistema de
             control del procesador atmosférico. ";
      if (self hasnt general) {
        "Parece tener una conexión rota en una de sus esquinas. Haría falta
         soldarla.";
      } else {
        "Ves el circuito arreglado gracias a la conexión de emergencia llevada a
         cabo por el robot.";
      }
    ],
    antes [;
      Arreglar, Cerrar:
        "La única forma de arreglar este circuito es soldando la conexión que tiene
         abierta.";
      Soldar:
        "No dispones de las herramientas adecuadas para poder soldar.";
    ],
  has
    estatico;


Object exterior3 "Exterior de la colonia"
  class Lugar
  with
    descripcion "Estás cerca del complejo de la colonia minera. Una zona amplia y yerma,
                 una superficie fangosa de terreno duro, predregoso en algunos puntos,
                 sin vida, carente de todo tipo de vegetación y cubierto de charcos y
                 lodo húmedo. Multitud de piezas mecánicas aparecen desperdigadas por
                 el suelo.",
    sgw_img Exterior3_jpg,
    sgw_mus Lluvia_ogg,
    sgw_vol 50,
    al_n exterior1,
    al_e junto_puerta,
    antes [;
      Examinar:
        if (uno == obj_abajo) {
          ImprimirOEjecutar(decorado_exterior3, desc_lodo);
          rtrue;
        }
    ];


Object decorado_exterior3 exterior3
  class DecoradoAmpliado
  with
    desc_lodo "Una superficie de polvo y arena forma el barro que se acumula en las
               suelas de tus botas. Y sobre dicha superficie, montones de tuercas y
               tornillos abandonados a su suerte.",
    desc_charcos "Agua de lluvia estancada aquí y allá, difícil de filtrar en este terreno
                  tan duro.",
    describir
      'lodo'    [; return self.desc_lodo; ] 0
      'barro'   [; return self.desc_lodo; ] 0
      'terreno' [; return self.desc_lodo; ] 0
      'charcos' [; return self.desc_charcos; ] G_PLURAL
      'charco'  [; return self.desc_charcos; ] 0
      'lluvia'  "Aquí es ligeramente más débil por la cercanía con el edificio del
                 complejo." G_FEMENINO
      'sombra'  "¿Qué fuente de luz la provocará?" G_FEMENINO;


Object "piezas metálicas" exterior3
  with
    nombre_m 'tornillo' 'artefacto',
    nombre_f  'pieza' 'tuerca',
    nombre_mp 'tornillos' 'artefactos',
    nombre_fp 'piezas' 'metalicas' 'tuercas',
    genero G_FEMENINO + G_PLURAL,
    descripcion "Tuercas, tornillos, dispositivos electrónicos y todo tipo de artefactos
                 metálicos; algunos oxidados, otros casi nuevos.",
    antes [;
      Coger:
        "No merece la pena... No son más que piezas inservibles.";
    ],
  has
    escenario;

    
Object ascensor_arriba "ascensor de emergencia"
  with
    nombre 'ascensor' 'elevador' 'cubiculo' 'emergencia',
    descripcion "Es una especie de cabina rectangular de cristal muy resistente,
                 reforzado por cientos de remaches metálicos. Su tamaño aproximado
                 es de unos cinco por cinco metros. Sobresale del suelo, con la puerta
                 abierta y cubierto de barro. Sin duda, ha cumplido perfectamente su
                 misión.",
    inicial "Sobresaliendo del suelo como una planta carnívora, permanece a tu lado el
             ascensor que os ha sacado a Jason y a ti del complejo colonial.",
    antes [;
      "Olvídate ahora de él. ¡Corre!";
    ],
  has
    estatico;


Object junto_puerta "Junto a la entrada de acceso al complejo"
  class Lugar
  with
    descripcion "Te encuentras frente a la puerta que da acceso al complejo colonial.
                 Parece que desde aquí es la única entrada. El edificio es relativamente
                 bajo, pero muy extenso. Todo tiene aspecto de haber sido innecesariamente
                 maltrado. Más allá sólo continúa la superficie de la colonia, que apenas
                 alcanzas a ver debido a la neblina.",
    sgw_img Junto_Puerta_jpg,
    sgw_mus Lluvia_ogg,
    al_o exterior3,
    adentro puerta_rara;


Object decorado_junto_puerta junto_puerta
  class DecoradoAmpliado
  with
    desc_edificio "Desde aquí te parece enorme, frío y estéril. Nadie diría que es una
                   estructura diseñada para albergar vida.",
    desc_niebla "Es un efecto provocado por la intensa lluvia, que te impide ver con
                 claridad los límites exteriores de la colonia.",
    describir
      'edificio' [; return self.desc_edificio; ] 0
      'complejo' [; return self.desc_edificio; ] 0
      'colonia'  [; return self.desc_edificio; ] G_FEMENINO
      'neblina'  [; return self.desc_niebla; ] G_FEMENINO
      'niebla'   [; return self.desc_niebla; ] G_FEMENINO;


Class Laberinto
  class Lugar
  with
    nombre_corto "Túneles",
    descripcion [ o k ns;
      print "Estás en los pasillos interiores del complejo. Metros y metros de estructura
             metálica recorren estos túneles, que parecen no acabar nunca. Las filtraciones
             en las paredes y el techo forman irregulares charcos de agua aquí y allá, y
             dotan al ambiente de una humedad malsana y asfixiante.";
      ns = 0;
!      nb = 0;
      objectloop (o in brujula) {
        if (~~(self provides o.direcc_puerta)) {
          ! No hay salida en esa dirección
          continue;
        }
        if (o == obj_adentro or obj_afuera) {
          continue;
        }
        k = self.(o.direcc_puerta);
        if (ZRegion(k) == 2) {
          ! Rutina que devuelve
          k = k();
        }
        if (ZRegion(k) ~= 1) {
          ! No se puede pasar
          continue;
        }
        if (k) {
          ns++;
          if (k ofclass Laberinto) {
            print " El túnel se alarga hacia ", (el) o;
            if (k.(sibling(o).direcc_puerta) ofclass Laberinto) {
              print " y continúa por ", (el) sibling(o), ".";
              o = sibling(o);
              ns++;
            } else {
              print ".";
            }
          } else {
            switch (o) {
              obj_n: print " ", (_Al) o, " ves ";
              obj_s: print " Por ", (el) o, " se llega hasta ";
              obj_e: print " Hacia ", (el) o, ", el túnel desemboca en ";
              obj_o: print " ", (_Al) o, " se alcanza ";
              obj_arriba: print " Sobre tu cabeza divisas ";
              obj_abajo: print " Bajando ves ";
              default: print " Hacia ", (el) o, " se observa ";
            }
            if (k has puerta) {
              ! Es una puerta
              print "una puerta.";
            } else {
              if (~~(k ofclass Balcon)) {
                if (k hasnt femenino && k hasnt nombreplural) {
                  print "un";
                } else if (k has femenino && k hasnt nombreplural) {
                  print "una";
                } else if (k hasnt femenino && k has nombreplural) {
                  print "unos";
                } else {
                  print "unas";
                }
                print " ", (string) k.nombre_localidad, ".";
              } else {
                print "el balcón que da al ", (_nombre_) k.posicion, " de la veta.";
              }
!              if (k ofclass Balcon && nb == 0) {
!                nb++;
!              }
            }
          }
        }
      }
      if (ns == 1) {
        print " Y parece la única salida posible, ya que el túnel termina aquí como
               un callejón sin salida.";
      }
      "";
    ],
    sgw_img Laberinto_jpg,
    sgw_mus Pasillo_ogg,
    antes [;
      Examinar:
        if (uno == obj_abajo) {
          ImprimirOEjecutar(decorado_laberinto, desc_charcos);
          rtrue;
        }
    ],
  has
    ~luz;


Object decorado_laberinto
  class DecoradoAmpliado
  with
    esta_en [;
      return LugarReal() ofclass Laberinto;
    ],
    desc_paredes "Por las curvadas paredes de los túneles discurren enormes tuberías.
                  Pero observando más de cerca puedes apreciar restos de sangre reseca
                  manchando la estructura.",
    desc_charcos "El agua se filtra por los enormes tubos de las paredes y se encharca
                  en algunos puntos del suelo.",
    desc_tubos "Son largos y sinuosos cilindros de varios tamaños que recorren las paredes
                transportando quién sabe qué.",
    describir
      'tuberias' [; return self.desc_tubos; ] G_FEMENINO + G_PLURAL
      'tuberia'  [; return self.desc_tubos; ] G_FEMENINO
      'tubos'    [; return self.desc_tubos; ] G_PLURAL
      'tubo'     [; return self.desc_tubos; ] 0
      'charcos'  [; return self.desc_charcos; ] G_PLURAL
      'charco'   [; return self.desc_charcos; ] 0
      'agua'     [; return self.desc_charcos; ] 0
      'paredes'  [; return self.desc_paredes; ] G_FEMENINO + G_PLURAL
      'pared'    [; return self.desc_paredes; ] G_FEMENINO
      'pared'    [; return self.desc_paredes; ] G_FEMENINO
      'sangre'   "Sólo eso: sangre salpicada (o proyectada) contra la pared. No hay ninguna
                  pista sobre a qué ser (persona o animal) pertenecía." G_FEMENINO;


Class Balcon
  class Lugar
  with
    nombre_corto [;
      print "Balcón ", (_nombre_) self.posicion, " a la veta";
      rtrue;
    ],
    nombre_localidad "balcón a la veta",
    descripcion [;
      print "Estás en la plataforma ", (_nombre_) self.posicion, " del nivel ", self.nivel,
            " de acceso a los túneles. Esta superficie rodea la veta que han construido los
             colonos para la extracción del mineral. Desde aquí, puedes ver la veta si miras
             al ", (_nombre_) IntercambiarDireccion(self.posicion), ".";
      if (puerta_emergencia in self) {
        print " En la pared sur hay una puerta con una ranura en un lateral.";
      }
      if (montacargas in self) {
        print " ";
        ImprimirOEjecutar(montacargas, inicial);
      } else {
        "";
      }
    ],
    posicion 0,
    nivel 0,
    sgw_img Balcon_jpg,
    sgw_mus Balcon_ogg,
    antes [;
      Ir, Examinar:
        if (uno == IntercambiarDireccion(self.posicion)) {
          "Te acercas a asomarte a la veta de mineral. Es enorme...";
        } else {
          rfalse;
        }
    ],
  has
    ~luz;


Object decorado_balcon
  class DecoradoAmpliado
  with
    desc_mineral "El Vibrantium es un mineral muy valioso y escaso. Esta veta es
                  particularmente rica; no hay más que ver el brillo tan puro que emite.",
    desc_balcon "Es la plataforma que rodea la veta.",
    esta_en [;
      return LugarReal() ofclass Balcon;
    ],
    antes [;
      Tocar:
        "No alcanzas a tocarl", (o) self.cantidad, " desde el balcón.";
    ],
    describir
      'veta'       [; return self.desc_mineral; ] G_FEMENINO
      'mineral'    [; return self.desc_mineral; ] 0
      'vibrantium' [; return self.desc_mineral; ] 0
      'plataforma' [; return self.desc_balcon; ] G_FEMENINO
      'balcon'     [; return self.desc_balcon; ] 0;


Object montacargas "montacargas"
  with
    descripcion "Es una plataforma situada al borde del balcón y que permite el
                 transporte vertical de objetos.",
    nombre 'montacargas' 'ascensor' 'elevador',
    esta_en balcon4 balcon8 balcon12,
    nivel 0,
    viene_jason false,
    inicial [ p;
      p = parent(self);
      print "Además, observas un montacargas que te permite ";
      if (p.arriba ~= nothing) {
        print "subir al nivel ", p.nivel + 1;
        if (p.abajo ~= nothing) {
          print ", o bien ";
        }
      }
      if (p.abajo ~= nothing) {
        print "bajar al nivel ", p.nivel - 1;
      }
      ".";
    ],
    reaccionar_antes [;
      self.viene_jason = jason in parent(self);
    ],
    reaccionar_antesPNJ [;
      if ((self in balcon4  && otro == obj_abajo) ||
          (self in balcon8  && otro == obj_arriba or abajo) ||
          (self in balcon12 && otro == obj_arriba)) {
        if (parent(self) ~= LugarReal()) {
          HablaJason("Mejor vayamos juntos.");
          rtrue;
        }
      }
    ],
    antes [;
      Meterse:
        switch (parent(self)) {
          balcon4:  <<Ir obj_abajo>>;
          balcon12: <<Ir obj_arriba>>;
          balcon8:  "¿Qué quieres hacer: subir o bajar?";
        }
    ],
    reaccionar_despues [;
      Ir:
        if (uno == obj_arriba or obj_abajo) {
          self.nivel = parent(self).nivel;
        }
        if (uno == obj_arriba) {
          if (self.viene_jason) {
            self.viene_jason = false;
            print "Subís";
          } else {
            print "Subes";
          }
        } else if (uno == obj_abajo && LugarReal() has visitado) {
          if (self.viene_jason) {
            self.viene_jason = false;
            print "Bajais";
          } else {
            print "Bajas";
          }
        } else {
          rfalse;
        }
        print " por el montacargas hasta el nivel ", self.nivel, "...^";
    ],
  has
    escenario entrable;    


! La propiedad general indica que no es la primera vez que entramos aquí con la puerta
! viscosa

Object entrada "Entrada al complejo"
  class Lugar
  with
    descripcion "Estás en la entrada que da acceso al complejo de la colonia minera.
                 Nada de decoración lujosa ni elementos ornamentales, sino más bien un
                 sencillo y amplio pasillo, semejante a un recibidor, totalmente
                 vacío y sin vida.",
    sgw_img Entrada_jpg,
    afuera puerta_rara,
    al_s balcon1,
    cada_turno [;
      if (self hasnt general && puerta_viscosa in self && jason in self) {
        give self general;
        print "^Jason observa la puerta sellada con esa sustancia, y exclama:^";
        jason.hablar = false;
        HablaJason("¡Maldita sea! Tendremos que buscar otra forma de salir de aquí.
                      Y de paso... ¡mandar todo esto al infierno!");
      }
    ];


Object puerta_rara "puerta del complejo" entrada
  class PNJPuerta
  class EscenarioAbrible
  with
    ! La librería PnjPuertas ya incluye la palabra 'puerta' como nombre
    nombre 'compuerta' 'agujero',
    adjetivos 'complejo',
    descripcion [;
      if (self hasnt abierta) {
        "La puerta es metálica y de aspecto recio e intimidante.";
      } else {
        "Restos de ácido gotean por los dentados bordes de la grieta abierta.";
      }
    ],
    ! "La puerta, de aspecto infranqueable, se alza formidable en la entrada al complejo.",
!    si_cerrada "La puerta tiene un aspecto muy extraño y parece imposible de abrir.", 
    si_abierta "Un enorme agujero de forma irregular abre el paso a través de la puerta.",
    afuera junto_puerta,
    adentro entrada,
    interior entrada,
    exterior junto_puerta,
    antes [;
      Abrir, QuitarCerrojo:
        if (self has abierta) {
          "No es necesario abrirla, ya que puedes pasar perfectamente a través del
           enorme agujero que se ha formado.";
        } else {
          "No ves ningún cuadro de mandos ni cerradura que pudieras usar para abrirla.";
!          "Es imposible que un ser humano pueda abrir manualmente esta puerta, y no ves
!           ningún cuadro de mandos ni cerradura que pudieras usar para abrirla.";
        }
      Empujar, EmpujarDir, Tirar:
        "Es imposible que un ser humano pueda abrirla manualmente.";
      Cerrar:
        "La puerta ya está cerrada, aunque puedes pasar por el agujero.";
      ! Esto es para que la librería no diga que está abierta o cerrada:
      Examinar:
        ImprimirOEjecutar(self, descripcion);
        rtrue;
    ],
  has
    femenino;


Object puerta_viscosa "puerta del complejo"
  class PNJPuerta
  with
    ! La librería PnjPuertas ya incluye la palabra 'puerta' como nombre
    nombre 'compuerta' 'enorme' 'grande' 'enorme' 'agujero' 'viscosa' 'viscosidad',
    adjetivos 'complejo',
    descripcion "El monstruo ha sellado el agujero de la puerta, haciéndola de nuevo
                 infranqueable.",
    si_cerrada "El agujero de la puerta ha sido sellado por una sustancia viscosa muy
                dura y resistente. Parece imposible de romper.",
    ! Inicialmente no está conectada a ninguna habitación. Más tarde, cuando cambiemos
    ! la puerta_rara por ésta, actualizaremos sus conexiones:
    adentro [; rfalse; ],
    afuera [; rfalse; ],
    interior entrada,
    exterior junto_puerta,
    antes [;
      Atacar:
        <<Abrir self>>;
      Abrir:
        "Intentas romper la sustancia viscosa que tapona el agujero, pero en seguida te
         das cuenta de que resulta imposible. Tendrás que buscar una salida por otra
         parte.";
      Cerrar:
        "La puerta ya está suficientemente taponada.";
      ! Esto es para que la librería no diga que está abierta o cerrada:
      Examinar:
        ImprimirOEjecutar(self, descripcion);
        rtrue;
    ],
  has
    femenino abrible;


Object oficina "Oficina"
  class Lugar
  with
    descripcion "Has llegado a lo que parece ser una oficina. Todas los puestos están
                 perfectamente delimitados e impolutamente ordenados, equipados cada uno
                 con una moderna consola de trabajo. Al fondo, una mesa que da la
                 impresión de ser la de algún pez gordo. La iluminación en esta sala es
                 excelente, así como la decoración, sorprendentemente lujosa para un
                 complejo como este. Una serie de archivadores se alinean perfectamente
                 bajo decenas de cuadros que prácticamente ocultan por completo la pared
                 con escenas marciales y bélicas. En una esquina, cerca del techo,
                 una cámara de seguridad lo observa todo con su único ojo.",
    sgw_img Oficina_jpg,
    al_e balcon2,
    antes [;
      Examinar:
        if (uno == obj_arriba) {
          "Hay una cámara girando lentamente cerca del techo de la sala.";
        }
    ];


Object decorado_oficina oficina
  class DecoradoAmpliado
  with
    desc_cuadros "Todos muy oscuros y, al mismo tiempo, alegóricos.",
    desc_puestos "Típicos escritorios de despacho, salvo la mesa del fondo.",
    desc_papeles "Cientos y cientos de documentos con información que no te resultan para
                  nada interesantes.",
    desc_archivadores "Son inmensos muebles metálicos firmemente sujetos al suelo y a la
                       pared sobre la que descansan. Todos están cerrados, y no parece
                       que haya una forma fácil de abrirlos.",
    desc_iluminacion "En contraste con el resto del complejo, parece que el sol mismo
                      se filtra por las paredes de esta habitación. La iluminación
                      proviene directamente del techo.",
    desc_paredes "Las paredes están casi ocultas por una enorme cantidad de cuadros. En
                  una esquina, pegada casi al techo, hay una cámara de seguridad.",
    antes [;
      Coger, Empujar, Tirar, Tocar:
        if (self.cantidad == 'cuadro' or 'cuadros') {
          print "¿Cuál tengo que ";
          IdiomaVerbo(palabra_verbo);
          "? ¡Hay decenas!";
        } else if (self.cantidad == 'papeles' or 'papel' or 'folios' or 'folio' or
                                    'documentos' or 'documento' &&
                   accion == ##Coger or ##Tocar) {
          "No merece la pena... Básicamente, son listados de cuentas y gastos.";
        } else if (self.cantidad == 'iluminacion' or 'luz' or 'luces') {
          print "¿Lo dices en serio? ¿Quieres ";
          IdiomaVerbo(palabra_verbo);
          " la luz?";
        }
      Abrir, Tirar, Empujar:
        "Tiras y tiras, pero es imposible... No hay forma de abrir esos archivadores.";
      Examinar:
        rfalse;
    ],
    describir
      'oficina'      [; oficina.descripcion(); return ""; ] G_FEMENINO
      'archivadores' [; return self.desc_archivadores; ] G_PLURAL
      'archivador'   [; return self.desc_archivadores; ] 0
      'cuadros'      [; return self.desc_cuadros; ] G_PLURAL
      'cuadro'       [; return self.desc_cuadros; ] 0
      'puestos'      [; return self.desc_puestos; ] G_PLURAL
      'puesto'       [; return self.desc_puestos; ] 0
      'mesas'        [; return self.desc_puestos; ] G_FEMENINO + G_PLURAL
      'escritorios'  [; return self.desc_puestos; ] G_FEMENINO + G_PLURAL
      'papeles'      [; return self.desc_papeles; ] G_PLURAL
      'papel'        [; return self.desc_papeles; ] 0
      'folios'       [; return self.desc_papeles; ] G_PLURAL
      'folio'        [; return self.desc_papeles; ] 0
      'documentos'   [; return self.desc_papeles; ] G_PLURAL
      'documento'    [; return self.desc_papeles; ] 0
      'iluminacion'  [; return self.desc_iluminacion; ] G_FEMENINO
      'luces'        [; return self.desc_iluminacion; ] G_PLURAL
      'luz'          [; return self.desc_iluminacion; ] 0
      'paredes'      [; return self.desc_paredes; ] G_FEMENINO + G_PLURAL
      'pared'        [; return self.desc_paredes; ] G_FEMENINO;


Object "ordenadores de la oficina" oficina
  with
    nombre_m 'ordenador' 'computador' 'equipo',
    nombre_f 'computadora' 'consola',
    nombre_mp 'ordenadores' 'computadores' 'equipos',
    nombre_fp 'computadoras' 'consolas',
    adjetivos 'oficina',
    genero G_MASCULINO + G_PLURAL,
    descripcion "Estos equipos tan potentes no son fáciles de encontrar... Todos
                 están apagados.",
    antes [;
      Encender, Quemar:
        "Pulsas todos los botones de encendido, sin éxito.";
      default:
        "Olvida los equipos informáticos... Está claro que no podrás usarlos, y además
         no hay ningún motivo para ello.";
    ],
  has
    escenario;
    
    
Object "mesa" oficina
  with
    nombre 'mesa' 'escritorio',
    adjetivos 'fondo',
    descripcion "Esto sí que es un escritorio amplio y con clase. Te recuerda el estilo
                 clásico de las antiguas mesas de abogado propias del S. XX (salvo por               
                 la falta de madera, naturalmente). Más allá de su recargado estilo
                 decorativo, tan sólo destacar las pilas de documentos garabateados y
                 mecanoimpresos que descansan sobre su superficie.",
    antes [;
      Coger, Empujar, Tirar:
        "Debe pesar una barbaridad... No se ha desplazado ni un centímetro.";
      Abrir, Cerrar:
        print "No hay nada que puedas ";
        IdiomaVerbo(palabra_verbo);
        " en esa mesa.";
    ],
  has
    escenario;
                 
                 
! El cuadro se crear cuando has visto las grabaciones de la cámara de seguridad

Object cuadro "cuadro"
  with
    nombre 'cuadro',
    descripcion [;
      print "Un patriótico cuadro exaltador de la vida al servicio de tu país. ";
      if (caja_fuerte notin oficina) {
        "No parece muy sujeto a la pared...";
      } else {
        "Descansa en un suelo, apoyado con desgana en la pared.";
      }
    ],
    antes [;
      Coger, Empujar, Tirar, Sacar:
        if (caja_fuerte notin oficina) {
          move caja_fuerte to oficina;
          "Deslizas lateralmente el cuadro para comprobar que escondía una caja fuerte.";
        } else {
          "Ya has movido el cuadro y has encontrado la caja fuerte... No esperes mucho
           más de él.";
        }
    ],
  has
    escenario;


! El atributo general indica si se ha introducido correctamente la combinación

Object caja_fuerte "caja fuerte"
  class Contenedor
  with
    nombre 'caja' 'fuerte' 'teclado' 'numerico',
    descripcion_real "Es una caja fuerte sencilla, sin ningún tipo de adorno exterior,
                      pero que tiene todo el aspecto de ser muy resistente. Lleva
                      acoplado un teclado numérico en la puerta, sin duda para poder
                      introducir algún tipo de combinación y poder abrirla.",
    si_abierta "Hay una caja fuerte abierta incrustada en la pared.",
    si_cerrada "Ves una caja fuerte cerrada incrustada en la pared.",
    antes [ t;
      Abrir:
        if (self hasnt general) {
          "No puedes abrirla hasta que introduzcas correctamente la combinación.";
        }
      Teclear:
        t = DireccionDePalabra(2);
        ! 111582
        if (LongitudDePalabra(2) < 6 || t->0 ~= '1' || t ->1 ~= '1' || t->2 ~= '1' ||
            t->3 ~= '5' || t->4 ~= '8' || t->5 ~= '2' || LongitudDePalabra(2) > 6) {
          "No pasa nada. Parece ser que la combinación no es la correcta...";
        } else {
          give self general;
          "Se oye un ligero chasquido y unos motores internos accionan el desbloqueo
           de la caja fuerte.";
        }
    ],
  has
    estatico femenino abrible;


Object holocubo "holocubo" caja_fuerte
  with
    nombre 'cubo' 'holografico' 'holocubo',
    descripcion "Un cubo holográfico es un soporte de almacenamiento de datos usado
                 principalmente para contener imágenes.";


Object tarjeta "tarjeta" caja_fuerte
  with
    nombre 'tarjeta' 'llave',
    descripcion "Es una tarjeta fabricada con un material mezcla de plástico y metal,
                 con forma rectangular, de unos cinco por siete centímetros. Tiene una
                 enorme letra ~E~ grabada en un dorso.",
  has
    femenino;


Object enfermeria "Enfermería"
  class Lugar
  with
    nombre_localidad "enfermería",
    descripcion "Te encuentras en la enfermería del complejo. Por lo que puedes observar,
                 está perfectamente equipada. Su magnífica iluminación baña toda la sala
                 con una lechosa luz blanca. La estancia se encuentra rodeada por una gran
                 fila de armarios. Un charco de sangre roja y brillante reposa en el suelo
                 junto a varias camillas desperdigadas.",
    sgw_img Enfermeria_jpg,
    al_s laberinto16,
    al_o balcon7,
  has
    femenino;


Object decorado_enfermeria enfermeria
  class DecoradoAmpliado
  with
    desc_armarios "Los armarios tienen puertas en su parte superior y cajones en la
                   inferior, y están salpicados de sangre.",
    desc_camillas "Todas las camillas están tiradas y volcadas por el suelo. Rastros de
                   sangre contrastan de forma impactante con el blanco inmaculado de las
                   sábanas.",
    desc_iluminacion "Una pulcra luz blanca, propia de los entornos sanitarios.",
    desc_cajones "Multitud de cajones situados en la parte inferior de los armarios. De
                  entre todos ellos, sólo uno parece poder abrirse.",
    desc_puertas "Amplias puertas que llegan casi hasta el techo de la sala, y que impiden
                  el acceso al interior de los armarios.",
    desc_sabanas "Sábanas blancas manchadas de sangre.",
    antes [;
      if (self.cantidad == 'iluminacion' or 'luz' or 'luces' &&
          accion == ##Coger or ##Tocar or ##Tirar or ##Empujar) {
        print "¿Lo dices en serio? ¿Quieres ";
        IdiomaVerbo(palabra_verbo);
        " la luz?";
      }
      Tocar:
        if (self.cantidad == 'sabanas' or 'sabana') {
          "No quieres mancharte las manos de sangre.";
        }
      Abrir, Empujar, Tirar:
        if (self.cantidad == 'armarios' or 'armario' or 'puertas' or 'puerta') {
          "Imposible... No ves ningún pomo o cerradura que puedas usar para poder
           abrir ninguna de las puertas.";
        } else if (self.cantidad == 'cajones') {
          "Hay muchos, pero sólo uno tiene pinta de poder abrirse.";
        }
      Empujar, Tirar, EmpujarDir:
        if (self.cantidad == 'camillas' or 'camilla') {
          "No merece la pena.";
        }
      Cerrar:
        if (self.cantidad == 'armarios' or 'armario' or 'puertas' or 'puerta') {
          "Los armarios ya están bastante cerrados. ¿No lo ves?";
        }
    ],
    describir
      'armarios'    [; return self.desc_armarios; ] G_PLURAL
      'armario'     [; return self.desc_armarios; ] 0
      'puertas'     [; return self.desc_puertas; ] G_FEMENINO + G_PLURAL
      'puerta'      [; return self.desc_puertas; ] G_FEMENINO
      'cajones'     [; return self.desc_cajones; ] G_PLURAL
      'camillas'    [; return self.desc_camillas; ] G_FEMENINO + G_PLURAL
      'camilla'     [; return self.desc_camillas; ] G_FEMENINO
      'sabanas'     [; return self.desc_sabanas; ] G_FEMENINO + G_PLURAL
      'sabana'      [; return self.desc_sabanas; ] G_FEMENINO
      'iluminacion' [; return self.desc_iluminacion; ] G_FEMENINO
      'luces'       [; return self.desc_iluminacion; ] G_PLURAL
      'luz'         [; return self.desc_iluminacion; ] 0;


Object cajon "cajón" enfermeria
  class Contenedor
  class EscenarioAbrible
  with
    nombre 'cajon',
    descripcion_real "Es un cajón situado en la parte inferior del armario más grande.",
    si_abierto "Hay un cajón abierto en uno de los armarios.",
    antes [;
      Tirar:
        <<Abrir self>>;
      Empujar:
        <<Cerrar self>>;
    ];

    
Object "sangre" enfermeria
  with
    nombre_f 'sangre',
    nombre_m 'charco',
    genero G_FEMENINO,
    descripcion "Es un generoso charco de sangre brillante y muy roja. Parece relativamente
                 reciente.",
    antes [;
      Tocar:
        "Mejor no tocar eso... Podrías pillar una infección...";
      Examinar:
        rfalse;
      default:
        "Mejor no... Ese charco de sangre en el suelo te recuerda que no deberías perder
         el tiempo.";
    ],
  has
    escenario;                 


! El atributo general indica si se ha vaciado

Object jeringa "jeringa" cajon
  with
    nombre_f 'jeringuilla' 'inyeccion' 'jeringa' 'aguja',
    nombre_m 'inyectable',
    adjetivo 'hipodermica',
    genero G_FEMENINO,
    descripcion [;
      print "Es una gruesa jeringa hipodérmica de 50 cc. y con una enorme aguja de unos
             15 centímetros.";
      if (self hasnt general) {
        " Según indica la etiqueta pegada a la misma, contiene 30 cc. de adrenalina.";
      } else {
        " Está vacía.";
      }
    ],
    antes [;
      Pinchar:
        if (self has general) {
          "La jeringa ya está vacía. No puede inyectar nada.";
        }
        if (otro == jason) {
          HablaJason("¿De qué va? ¡Ni se le ocurra!");
          rtrue;
        } else if (otro == jason_inerte) {
          remove jason_inerte;
          give jason_inerte ausente;
          give jason ~ausente;
          move jason to LugarReal();
          give self general;
          print "Clavas la aguja justo en el corazón de Jason, y le inyectas toda la
                 adrenalina que había en la jeringa. El Mayor abre los ojos y lanza un
                 grito desgarrador... Retiras la aguja rápidamente de su pecho, y
                 comienza a toser fuertemente mientras se aprieta el tórax con las
                 manos... ¡Está vivo y despierto! ¡Ha funcionado!^";
          print "Jason mira la interminable aguja de la jeringa, y exclama:^";
          HablaJason("Dios mío... ¿Ha tenido que ensartarme con ESO?"); PreguntaSiNo = 1;
          rtrue;
        }
    ];


Object laboratorio "Laboratorio"
  class Lugar
  with
    nombre_localidad "laboratorio",
    descripcion "El laboratorio de la colonia es bastante grande, y se encuentra dividido
                 en dos áreas claramente delimitadas. En la zona sur, multitud de tubos
                 de ensayo, probetas y cosas similares yacen rotos por el suelo. Las
                 estanterías están prácticamente vacías y medio descolgadas y, sobre algunas
                 de las mesas, descansan lo que parecen ser embriones de seres
                 irreconocibles, embutidos en grandes botes transparentes. La zona este
                 tiene el aspecto de haber sido un área de contención, pero sea lo que sea
                 lo que hubiera estado confinado dentro, ya no lo está. Manchas y charcos
                 de sangre cubren ciertas partes de la estancia, y una cámara situada
                 cerca del techo barre la sala de lado a lado.",
    sgw_img Laboratorio_jpg,
    al_n laberinto19,
    antes [;
      Examinar:
        if (uno == obj_arriba) {
          "Hay una cámara girando lentamente cerca del techo de la sala.";
        }
    ];


Object decorado_laboratorio laboratorio
  class DecoradoAmpliado
  with
    desc_tubos "La mayor variedad de probetas y tubos de ensayo que hayas visto nunca
                en una misma sala. No queda ninguno en pie... Todos están rodando por
                el suelo, algunos en buen estado, pero la mayoría completamente
                destrozados.",
    desc_mesas "Los botes transparentes, situados aquí y alla sobre la superficie de las
                mesas, contienen restos orgánicos semejantes a fetos o embriones de una
                especie que te resulta imposible identificar.",
    desc_paredes "No hay mucho más que destacar. Tan sólo la cámara que se desplaza
                  suavemente en lo alto de una de las paredes.",
    desc_estanterias [;
      print "La mayoría de las estanterías están sueltas, tiradas o directamente
             destrozadas";
      if (bisturi has oculto && bisturi hasnt movido) {
        give bisturi ~oculto;
        return ", pero buscando bien en una de ellas, encuentras una especie
                de objeto afilado... Al mirarlo de cerca, compruebas que se trata de
                un bisturí láser.";
      } else {
        return ".";
      }
    ],
    desc_sangre 
    antes [;
      Tocar, Probar:
        switch (self.cantidad) {
          'tubos', 'tubo',
          'probetas', 'probeta',
          'botes', 'bote',
          'fetos', 'feto',
          'embriones', 'embrion': "Mejor no... No quieres contagiarte de una posible
                                   infección.";
      }
    ],
    describir
      'paredes'     [; return self.desc_paredes; ] G_FEMENINO + G_PLURAL
      'pared'       [; return self.desc_paredes; ] G_FEMENINO
      'tubos'       [; return self.desc_tubos; ] G_PLURAL
      'tubo'        [; return self.desc_tubos; ] 0
      'probetas'    [; return self.desc_tubos; ] G_FEMENINO + G_PLURAL
      'probeta'     [; return self.desc_tubos; ] G_FEMENINO
      'botes'       [; return self.desc_mesas; ] G_PLURAL
      'bote'        [; return self.desc_mesas; ] 0
      'mesas'       [; return self.desc_mesas; ] G_FEMENINO + G_PLURAL
      'mesa'        [; return self.desc_mesas; ] G_FEMENINO
      'fetos'       [; return self.desc_mesas; ] G_PLURAL
      'feto'        [; return self.desc_mesas; ] 0
      'embriones'   [; return self.desc_mesas; ] G_PLURAL
      'embrion'     [; return self.desc_mesas; ] 0
      'estanterias' [; return self.desc_estanterias(); ] G_FEMENINO + G_PLURAL
      'estanteria'  [; return self.desc_estanterias(); ] G_FEMENINO
      'sangre'      [; return self.desc_sangre; ] G_FEMENINO;


Object "sangre" laboratorio
  with
    nombre_f 'sangre' 'mancha',
    nombre_fp 'manchas',
    nombre_m 'charco',
    nombre_mp 'charcos',
    genero G_FEMENINO,
    descripcion "Multitud de charcos de sangre cubren el suelo y la mayor parte del
                 mobiliario.",
    antes [;
      Tocar, Probar:
        "La sangre no parece reciente. De hecho, en algunas zonas está reseca. La humedad
         del ambiente ha debido conservarla mejor de lo esperado.";
    ],
  has
    escenario;


Object area_contencion "área de contención" laboratorio
  with
    nombre 'area' 'contencion' 'jaula' 'cabina',
    descripcion "Es una especie de jaula construida a base de cristal y metal, y tiene
                 un tamaño enorme, pues ocupa toda la zona este del laboratorio. La
                 puerta, que ocupa toda una cara de la jaula, tiene dos hojas: una está
                 completamente abierta, mientras que la otra está, más que rota, disuelta
                 o corroída.",
    antes [;
      Coger, Tocar, Probar:
        "Intuyes que el daño de la puerta pudo haberse debido a la fuga de algún tipo de
         ácido.";
    ],
  has
    escenario;

        
Object bisturi "bisturí láser" laboratorio
  with
    nombre_m 'bisturi' 'laser',
    nombre_f 'cuchilla',
    adjetivos 'cirujano',
    inicial "Sobre una de las pocas estanterías intactas reposa lo que parece ser un
             bisturí de cirujano.",
    descripcion "Es un bisturí láser de gran potencia de corte. Sólo actúa directamente
                 sobre la superficie a cortar.",
    despues [;
      Coger:
        if (self has oculto) {
          give self ~oculto;
        }
    ],
  has
    oculto;


Object camara_seguridad "cámara de seguridad"
  with
    nombre 'camara' 'seguridad',
    descripcion [;
      print "Es una cámara de seguridad que se mueve lentamente a izquierda y derecha
             y que emite una parpadeante luz roja junto al objetivo. Por detrás le sale
             un cable de color ";
      if (LugarReal() == oficina) {
        print "blanco ";
      } else {
        print "negro ";
      }
      "que se pierde fuera de la habitación, fijado a la parte superior de la pared.";
    ],
    esta_en oficina laboratorio,
    visto_en_oficina false,
    visto_en_laboratorio false,
    despues [;
      Examinar:
        if (LugarReal() == oficina && ~~(self.visto_en_oficina) &&
            ~~(decorado_seguridad.visto_en_seguridad)) {
          self.visto_en_oficina = true;
          PonerCableBlanco();
          move rastro_cable_blanco to oficina;
          rfalse;
        }
        if (LugarReal() == laboratorio && ~~(self.visto_en_laboratorio) &&
            ~~(decorado_seguridad.visto_en_seguridad)) {
          self.visto_en_laboratorio = true;
          PonerCableNegro();
          move rastro_cable_negro to laboratorio;
          rfalse;
        }
    ],
  has
    femenino escenario;


Class ZonaNorte
  with
    contador_muerte 0,
    ya_has_oido false,
    donde_estoy [;
      switch (self) {
        comedor1:
          if (despensa has visitado) {
            return "la despensa";
          } else {
            return "la sala situada al este";
          }
        despensa:
          if (despensa has visitado) {
            return "esta habitación";
          }
        cocina:
          if (despensa has visitado) {
            return "la despensa";
          } else {
            return "la sala que está al oeste";
          }
      }
    ],
    despues [;
      Ir:
        if (uno == obj_n && self.ya_has_oido) {
          print "Sería mejor evitar esta zona: no parece muy segura...^";
        }
    ],
    antes [;
      Escuchar:
        if (self.ZonaNorte::contador_muerte >= 10) {
          "Oyes un ruido que procede de alguna parte de la despensa. Es un sonido
           agudo, como de animal.";
        } else {
          if (tu_traje in jugador && tu_traje has puesto) {
            "El casco te impide oir nada con claridad.";
          }
        }
 
      Ir:
        if (uno == obj_s) {
          if (self.ZonaNorte::contador_muerte >= 10) {
            print "Uff... Parece que lo que sea que emitía ese sonido se ha calmado...
                   No cabe duda de que esa zona resulta muy peligrosa. Lo mejor será
                   evitarla siempre que se pueda.^";
          }
          self.ZonaNorte::contador_muerte = 0;
        }
    ],    
    cada_turno [;
      if (self.ZonaNorte::contador_muerte >= 15 && self == despensa) {
        print "^Y entonces la ves. ";
        if (hueco_techo has visitado) {
          print "Una araña idéntica a la que habías visto muerta en la sala de
                 ventilación, ";
        } else {
          print "Una especie de araña, provista de unas patas anormalmente largas y delgadas
                 y de una poderosa y serpenteante cola, ";
        }
        print "sale rápidamente de entre los expositores y salta como una exhalación hacia
               ti. Te tapa completamente la cara, no puedes respirar, sus patas se cierran
               alrededor de tu cabeza en una suerte de abrazo terrible, y su larga cola se
               enrosca rodeando tu cuello. La hinchazón que provoca en tu laringe te hace
               sentir con dolorosa precisión que algo desciende por tu esófago, justo antes
               de perder el conocimiento...^";
        EsperarTecla();
        Bandera_Fin(1);
        "^Cuando despiertas, notas un regusto amargo en la boca, y la garganta te duele
         horrores. Pero lo peor no es eso... Algo se mueve dentro de ti... Algo que
         lucha por salir, que te presiona el pecho, que te deja casi sin respiración...
         No puedes soportar el dolor y gritas... gritas como nunca antes lo habías hecho,
         y tu grito coincide con el momento en que tus costillas se rompen hacia afuera y
         tu pecho estalla en sangre, carne y vísceras... Pero vives lo suficiente para
         ver esa pequeña y redonda cabeza llena de dientes saliendo de tu cuerpo...^";
      } else {
        if (self.ZonaNorte::contador_muerte < 17) {
          self.ZonaNorte::contador_muerte++;
        }
      }

      if ((~~(self.ya_has_oido) && self.ZonaNorte::contador_muerte >= 10)) {
        self.ya_has_oido = true;
      }

      switch (self.ZonaNorte::contador_muerte) {
        10,11: "^Oyes ruidos procedentes de alguna parte de ",
           (string) self.donde_estoy(), ".";
        12,13: "^Los ruidos se hacen más fuertes, y siguen viniendo de ",
           (string) self.donde_estoy(), "... Esto no te gusta nada...";
        14,15: "^Es un chillido... Claramente es un chillido... ¡Hay algo en la despensa!
            ¡Corre!";
        16: "^¡Corre! ¡Sal de aquí! ¡Pero no pases por la despensa!";
      }
    ];


Object despensa "Despensa"
  class Lugar
  class ZonaNorte
  with
    descripcion "La despensa. En la zona frigorífica, frente a tí, es donde guardan los
                 elementos perecederos. Desde aquí alcanzas a ver trozos de carne
                 (probablemente cerdo) y frutas. A tu alrededor, cientos de expositores
                 soportan los distintos tipos de comida prefabricada o enlatada de la que
                 disponen los colonos. Algunos de los expositores están rotos y con la
                 comida desperdigada por los alrededores.",
    sgw_img Despensa_jpg,
    al_o comedor1,
    al_e cocina;


Object decorado_despensa despensa
  class DecoradoAmpliado
  with
    desc_frigorifico "Los alimentos se congelan en esa especie de gigantesco armario
                      frigorífico. Está cerrado, y no parece abrirse.",
    desc_carne "Enormes trozos de carne sin procesar. Incomestibles, desde luego.",
    desc_frutas "Alcanzas a distinguir manzanas y algunas naranjas. El resto no lo habías
                 visto en tu vida.",
    desc_m_p_m "Todo parece bastante jugoso. Aún así, no te fías...",
    desc_expositores "Muestran al posible consumidor la oferta de alimentos disponibles.
                      Algunos están rotos, exponiendo la comida al aire.",
    desc_comida "Nada por lo que perderías el apetito.",
    antes [;
      Coger, Tocar, Probar, Comer:
        if (self.cantidad == 'carne' or 'trozos' or 'cerdo' or 'frutas' or 'fruta' or
            'manzanas' or 'manzana' or 'naranjas' or 'naranja' or
            'comidas' or 'comida' or 'alimentos' or 'alimento') {
          "Será mejor que no pruebes nada. Ni toque siquiera. Nunca se sabe...";
        }
      Abrir, Tirar, Empujar:
        if (self.cantidad == 'frigorifico' or 'armario' or 'congelador') {
          ImprimirOEjecutar(self, no_puedes_abrir);
          rtrue;
        }
    ],
    no_puedes_abrir "Lo intentas con todas tus fuerzas, pero te resulta imposible: está
                     herméticamente cerrado.",
    describir
      'frigorifico' [; return self.desc_frigorifico; ] 0
      'armario'     [; return self.desc_frigorifico; ] 0
      'congelador'  [; return self.desc_frigorifico; ] 0
      'carne'       [; return self.desc_carne; ] 0
      'trozos'      [; return self.desc_carne; ] G_PLURAL
      'cerdo'       [; return self.desc_carne; ] 0
      'frutas'      [; return self.desc_frutas; ] G_FEMENINO + G_PLURAL
      'fruta'       [; return self.desc_frutas; ] G_FEMENINO
      'manzanas'    [; return self.desc_m_p_m; ] G_FEMENINO + G_PLURAL
      'manzana'     [; return self.desc_m_p_m; ] G_FEMENINO
      'peras'       [; return self.desc_m_p_m; ] G_FEMENINO + G_PLURAL
      'pera'        [; return self.desc_m_p_m; ] G_FEMENINO      
      'melocotones' [; return self.desc_m_p_m; ] G_PLURAL
      'melocoton'   [; return self.desc_m_p_m; ] 0
      'expositores' [; return self.desc_expositores; ] G_PLURAL
      'expositor'   [; return self.desc_expositores; ] 0
      'comidas'     [; return self.desc_comida; ] G_FEMENINO + G_PLURAL
      'comida'      [; return self.desc_comida; ] G_FEMENINO
      'alimentos'   [; return self.desc_comida; ] G_PLURAL
      'alimento'    [; return self.desc_comida; ] 0;


Object "zona frigorífica" despensa
  with
    nombre 'zona' 'frigorifica',
    descripcion [;
      ImprimirOEjecutar(decorado_despensa, desc_frigorifico);
    ],
    antes [;
      Abrir, Tirar, Empujar:
        ImprimirOEjecutar(decorado_despensa, no_puedes_abrir);
        rtrue;
    ],
  has
    escenario femenino;
        
    
Object comedor1 "Comedor"
  class Lugar
  class ZonaNorte
  with
    nombre_localidad "comedor",
    descripcion "Estás en el comedor de la colonia. Docenas de mesas se extienden a derecha
                 e izquierda, de este a oeste, dejando únicamente un gran pasillo central por
                 donde pasar cómodamente. Un sencillo cálculo mental tras un rápido vistazo
                 arrojan una capacidad aproximada de cien personas. Una gran barra, por donde
                 pasar las bandejas y servir la comida, se extiende hacia la habitación
                 contigua hasta penetrar en ella. En una mesa cercana hay algunas bandejas
                 de comida sin consumir.",
    sgw_img Comedor_jpg,
    al_e despensa,
    al_s balcon6;


Object decorado_comedor comedor1
  class DecoradoAmpliado
  with
    desc_mesas "Las mesas se alinean ordenadamente, fijas como están al suelo y, salvo una,
                todas se encuentran bastante limpias.",
    desc_bandejas "Algunas bandejas se alinean en la barra como soldados en formación,
                   mientras que otras reposan toscamente en una de las mesas.",
    desc_comida "Es sólo comida putrefacta.", 
    antes [;
      Comer, Coger:
        if (self.cantidad == 'comida') {
          "Es evidente que no está en buen estado.";
        }
      Coger:
        if (self.cantidad == 'bandejas' or 'bandeja') {
          "No hay nada que merezca la pena.";
        }
    ],
    describir
      'mesas'     [; return self.desc_mesas; ] G_FEMENINO + G_PLURAL
      'mesa'      [; return self.desc_comida; ] G_FEMENINO
      'barra'     "Es una superficie rectangular, lisa y alargada, que tiene como finalidad
                   servir de soporte a las bandejas de comida." G_FEMENINO
      'bandejas'  [; return self.desc_bandejas; ] G_FEMENINO + G_PLURAL
      'bandeja'   [; return self.desc_comida; ] G_FEMENINO
      'comida'    [; return self.desc_comida; ] G_FEMENINO
      'alimentos' [; return self.desc_comida; ] G_PLURAL
      'alimento'  [; return self.desc_comida; ] 0;


Object cocina "Cocina"
  class Lugar
  class ZonaNorte
  with
    descripcion "Esta enorme cocina está unida al comedor por medio de
                 las barras de autoservicio. Una gran cantidad de cacharros y pucheros
                 penden de un techo bajo y claustrofóbico. El único mobiliario visible
                 lo forman las mesas para preparar la comida. Diez enormes campanas de
                 extracción adornan la parte alta de sendos fogones. Por lo demás, resulta
                 sorprendende la ausencia de todo rastro de comida.",
    nombre_localidad "cocina",
    sgw_img Cocina_jpg,
    al_s balcon7,
    al_o despensa,
  has
    femenino;


Object decorado_cocina cocina
  class DecoradoAmpliado
  with
    desc_barras "Es una superficie rectangular, lisa y alargada, que tiene como finalidad
                 la de servir de soporte a las bandejas de comida. Se pierde por el
                 oeste hacia el comedor situado junto a la cocina.",
    desc_cacharros "Ollas, cacerolas y todo tipo de utensilios de cocina, colgando del
                    techo de forma laxa.",
    desc_mesas "Amplias e impolutas. Esta cocina parece no haberse usado en siglos.",
    desc_campanas "Potentes extractores de aire que expulsan el humo y el vapor. Ahora
                   mismo se encuentran apagados, y no ves forma de hacerlos funcionar.",
    desc_fogones "Están compuestos de un material cerámico que se activa automáticamente y
                  desprende calor al poner algo encima, aunque estos de aquí no parecen
                  funcionar.",
    describir
      'barras'      [; return self.desc_barras; ] G_FEMENINO + G_PLURAL
      'barra'       [; return self.desc_barras; ] G_FEMENINO
      'cacharros'   [; return self.desc_cacharros; ] G_PLURAL
      'cacharro'    [; return self.desc_cacharros; ] 0
      'pucheros'    [; return self.desc_cacharros; ] G_PLURAL
      'puchero'     [; return self.desc_cacharros; ] 0
      'mesas'       [; return self.desc_mesas; ] G_FEMENINO + G_PLURAL
      'mesa'        [; return self.desc_mesas; ] G_FEMENINO
      'campanas'    [; return self.desc_campanas; ] G_FEMENINO + G_PLURAL
      'campana'     [; return self.desc_campanas; ] G_FEMENINO
      'extractores' [; return self.desc_campanas; ] G_PLURAL
      'extractor'   [; return self.desc_campanas; ] 0
      'fogones'     [; return self.desc_fogones; ] G_PLURAL
      'fogon'       [; return self.desc_fogones; ] 0;


Object balcon1
  class Balcon,
  with
    nivel 0,
    posicion obj_n,
    al_n entrada,
    al_e laberinto3,
    al_o laberinto1,
    despues [;
      Ir:
        if (self hasnt visitado) {
          print "Llegas a una especie de balcón desde donde se puede observar claramente
                 la forma y la estructura del complejo. Todo está construido alrededor de
                 la veta de mineral (situado en el centro del edificio) y hay balcones
                 como éste al norte, sur, este y oeste de la veta. Además, es evidente
                 que éste no es el único nivel del complejo, pues éste sigue hacia abajo
                 siempre rodeando el mineral. Las distancias son difíciles de calcular
                 desde esta perspectiva, pero la sensación que transmite es que te
                 encuentras ante algo grande.^";
        }
    ];


Object balcon2
  class Balcon,
  with
    nivel 0,
    posicion obj_o,
    al_n laberinto1,
    al_s laberinto11,
    al_o oficina;

    
Object balcon3
  class Balcon,
  with
    nivel 0,
    posicion obj_e,
    al_n laberinto3,
    al_s laberinto6,
    al_e laberinto7;


Object balcon4
  class Balcon,
  with
    nivel 0,
    posicion obj_s,
    al_e laberinto6,
    al_o laberinto11,
    abajo balcon8,
    antes [;
      Ir:
        if (uno == obj_abajo && balcon8 hasnt visitado) {
          print "Te subes al montacargas, y comienzas a bajar lentamente hacia el nivel
                 -1...^";
          EsperarTecla();
          rfalse;
        }
    ];


!Object balcon5
!  class Lugar
!  class Balcon,
!  with
!    nivel -1,
!    posicion obj_n,
!    al_e laberinto14,
!    al_o laberinto13;


Object balcon6
  class Balcon,
  with
    nivel -1,
    posicion obj_o,
    al_n comedor1,
    al_s laberinto18,
    ! Al oeste hay un hábitat
    antes [;
      Ir:
        if (uno == obj_o) {
          ImprimirOEjecutar(habitat, direccion);
          rtrue;
        }
    ],
    salidas [ d;
      switch (d) {
        obj_o: return 3;
        default: rfalse;
      }
    ];


Object balcon7
  class Balcon,
  with
    nivel -1,
    posicion obj_e,
    al_n cocina,
    al_s laberinto19,
    al_e enfermeria;


Object balcon8
  class Balcon,
  with
    nivel -1,
    posicion obj_s,
    adentro puerta_emergencia,
    al_e laberinto19,
    al_o laberinto18,
    arriba balcon4,
    abajo balcon12,
    antes [;
      Ir:
        if (uno == obj_abajo && balcon12 hasnt visitado) {
          print "Lentamente, el montacargas va descendiendo aún más hacia las profundidades
                 del complejo. Es evidente que estás entrando en un nivel aún inacabado
                 (y el último construido hasta ahora, según puedes comprobar), diseñado para
                 dar soporte y mantenimiento a los niveles superiores, y continuar
                 descubriendo la veta aún más hacia el interior del planeta. La sensación
                 de agobio se hace mayor por momentos, y las paredes y el techo se acercan
                 entre sí como en una tenebrosa emboscada...^";
          EsperarTecla();
          rfalse;
        }
    ];


!Object balcon9
!  class Lugar
!  class Balcon,
!  with
!    nivel -2,
!    posicion obj_n,
!    al_e laberinto23,
!    al_o laberinto21;


!Object balcon10
!  class Lugar
!  class Balcon,
!  with
!    nivel -2,
!    posicion obj_o,
!    al_n laberinto21,
!    al_s laberinto28;


!Object balcon11
!  class Lugar
!  class Balcon,
!  with
!    nivel -2,
!    posicion obj_e,
!    al_n laberinto23,
!    al_s laberinto30;


Object balcon12
  class Balcon,
  with
    nivel -2,
    posicion obj_s,
    no_puedes_ir "Este nivel aún está sin terminar. No hay más salidas.",    
    al_e laberinto36,
    al_o laberinto28,
    arriba balcon8;


Class SalaEmergencia
  class Lugar
  with
    descripcion [;
      "Te encuentras en la sala ", (string) self.situacion, " del área de emergencia.
       Está absolutamente vacía, a excepción de un gran botón pulsador situado en el
       centro de la pared opuesta a la puerta. Todo, desde sus pequeñas dimensiones a
       la total falta de decoración, favorece la sensación de total asfixia.";
    ];


Class ZonaEmergencia;


Object emergencia_este "Sala este de emergencia"
  class SalaEmergencia
  class ZonaEmergencia
  with
    sgw_img Emergencia_Este_jpg,
    situacion "este",
    al_o emergencia_pasillo;


Object emergencia_pasillo "Pasillo de emergencia"
  class Lugar
  class ZonaEmergencia
  with
    descripcion "Estás en el pasillo central del área de emergencia. El pasillo discurre
                 de este a oeste, hacia sendas salas separadas entre sí unos cincuenta
                 metros. La luz aquí es de un amarillo muy tenue, y la enorme letra ~E~
                 dibujada en la pared te recuerda que te encuentras en un lugar
                 especialmente restringido.",
    afuera puerta_emergencia,
    al_e emergencia_este,
    al_o emergencia_oeste,
    sgw_img Emergencia_Pasillo_jpg,
    despues [;
      Ir:
        if (self hasnt visitado) {
          print "Al cruzar la puerta te das cuenta de que has accedido... ¡al área de
                 emergencia!";
          EsperarTecla();
          print "^";
        }
    ];


Object decorado_pasillo emergencia_pasillo
  class DecoradoAmpliado
  with
    desc_paredes "Las paredes están absolutamente desnudas, salvo por un diseño rayado
                  cerca del suelo y una letra ~E~ tan alta como la pared misma,
                  recordándote que estás en la zona de emergencia.",
    describir
      'paredes' [; return self.desc_paredes; ] G_FEMENINO + G_PLURAL
      'pared'   [; return self.desc_paredes; ] G_FEMENINO
      'pasillo' [; return emergencia_pasillo.descripcion; ] 0;


Object "letra ~E~" emergencia_pasillo
  with
    nombre 'letra' 'e//',
    descripcion "Es una enorme letra ~E~ que ocupa casi toda la altura de una pared
                 del pasillo. Te informa de que estás en la zona de emergencia.",
  has
    escenario;
      

Object "diseño rayado" emergencia_pasillo
  with
    nombre 'diseno' 'rayado' 'rayas',
    descripcion "Es un motivo decorativo formado por una sucesión de rayas amarillas
                 y negras.",
  has
    escenario;


! El atributo general indica que no es la primera vez que se abre

Object puerta_emergencia "puerta" emergencia_pasillo
  class PNJPuerta
  class EscenarioAbrible
  with
    ! La librería PnjPuertas ya incluye la palabra 'puerta' como nombre
    nombre 'compuerta' 'emergencia',
    descripcion [;
      print "Es una puerta sin ningún tipo de identificación. Tan sólo cabe destacar una
             ranura en un panel situado junto a ella.";
      if (self has abierta) {
        " Ahora mismo está abierta.";
      } else {
        "";
      }
    ],
    si_cerrada "La puerta de emergencia está cerrada.", 
    si_abierta "La puerta de emergencia está abierta.",
    adentro emergencia_pasillo,
    afuera balcon8,
    interior emergencia_pasillo,
    exterior balcon8,
    con_llave tarjeta,
    lado_cierre balcon8,
    tras_abrir NO_PUEDE_ABRIR,
    antes [;
      Abrir:
        if (self has cerrojoechado) {
          "La puerta está bloqueada. Necesitas desbloquearla primero.";
        }

        if (LugarReal() == emergencia_pasillo &&
            parent(jason) ofclass ZonaEmergencia &&
            alien.comportamiento == 3 && alien in balcon8) {
          if (jason in LugarReal()) {
            HablaJason("Pero... ¿qué hace? ¡No abra la puerta con ese monstruo ahí!");
            jason.hablar = false;
            rtrue;
          } else {
            "¡Sería una locura abrir la puerta con ese monstruo al otro lado!";
          }
        }
        
      QuitarCerrojo:
        if (self hasnt cerrojoechado) {
          print "(La puerta ya no está bloqueada, y no necesitas de nuevo la tarjeta para
                 abrirla.)^^";
          <<Abrir self>>;
        }

        if (otro ~= nothing && otro ~= tarjeta) {
          print (_El) otro, " no parece entrar en la ranura.^";
          rtrue;
        }

      EcharCerrojo:
        "Lo más que puedes hacer es cerrarla, pero no bloquearla.";

      Cerrar:
        if (parent(alien) ofclass ZonaEmergencia) {
          "¡No cierres la puerta con el monstruo dentro!";
        }

      ! Esto es para que la librería no diga que está abierta o cerrada:
      Examinar:
        ImprimirOEjecutar(self, descripcion);
        rtrue;
    ],
    despues [;
      Abrir:
        self.abrir();
        if (self hasnt general) {
          give self general;
          "Insertas la tarjeta en la ranura... y tras un leve zumbido, el cierre
           hidráulico de la puerta se desbloquea. Rápidamente abres la puerta y descubres
           un estrecho y largo pasillo.";
        }

      QuitarCerrojo:
        <<Abrir self>>;

      Cerrar:
        if (alien.comportamiento == 3 && parent(jason) ofclass ZonaEmergencia) {
          print "Cierras la puerta.^";
          HablaJason("¡Uff! Menos mal. Aquí estaremos a salvo... por un tiempo.");
          rtrue;
        }
    ],
  has
    femenino cerrojo cerrojoechado;


Object ranura "ranura" balcon8
  with
    nombre 'ranura' 'raja' 'panel',
    descripcion "En el panel metálico que hay junto a la puerta sólo destaca una fina
                 ranura horizontal, de unos seis centímetros de largo por dos o tres
                 milímetros de ancho. Se encuentra casi a la altura de los ojos.",
    antes [;
      Recibir:
        if (uno ~= tarjeta) {
          print (_El) uno, " no parece entrar en la ranura.^";
          rtrue;
        }
        
        <<QuitarCerrojo puerta_emergencia uno>>;
    ],
  has
    femenino escenario recipiente;
    
    
Object emergencia_oeste "Sala oeste de emergencia"
  class SalaEmergencia
  class ZonaEmergencia
  with
    sgw_img Emergencia_Oeste_jpg,
    situacion "oeste",
    al_e emergencia_pasillo;


Class BotonEmergencia
  with
    nombre 'boton' 'pulsador',
    color,
    descripcion [;
      print "Es un enorme botón de color ", (string) self.color, " que sobresale de
             la pared a la altura de los ojos.";
      if (self has general) {
        print " Ahora mismo está pulsado y se oye un tic tac que sale de él.";
      }
      "";
    ],
    tiempo_restante,
    antes [;
      Empujar:
        if (puerta_emergencia has abierta) {
          "Los botones no funcionarán hasta que se cierre la puerta.";
        }
        self.pulsar();
        rtrue;
    ],
    alguien_pulsa [;
      switch (actor) {
        objjugador:
          "Al pulsar el botón, este se queda hundido y se oye un tic tac...";
        jason:
          HablaJason("¡He pulsado el botón, se ha quedado dentro y se oye algo que
                      no sé qué es!");
          rtrue;
      }
    ],
    liberar_boton [;
      give self ~general;
      if (LugarReal() == parent(self)) {
        "^El botón salta a su posición inicial y el tic tac se detiene.";
      } else {
        "^Desde lejos oyes que el botón de la otra sala salta a su posición inicial.";
      }
        
      if (jason in parent(self)) {
        HablaJason("El botón ha saltado y ahora está como al principio.");
        rtrue;
      }
    ],
  has
    escenario;


! El primero de los botones que hay que pulsar.
! El atributo general indica que está pulsado

Object boton_este "botón rojo" emergencia_este
  class BotonEmergencia
  with
    color "rojo",
    adjetivos 'rojo',
    tiempo_agotado [;
      if (boton_oeste hasnt general) {
        self.liberar_boton();
      }
    ],
    pulsar [;
      if (self hasnt general) {
        ArrancarReloj(self, 2);
        give self general;
        self.alguien_pulsa();
      } else {
        switch (actor) {
          objjugador: "Ya está pulsado.";
          jason: HablaJason("¡Ya está pulsado!");
        }
      }
    ];


! El segundo de los botones que hay que pulsar.
! El atributo general indica que está pulsado

Object boton_oeste "botón azul" emergencia_oeste
  class BotonEmergencia
  with
    color "azul",
    adjetivos 'azul',
    tiempo_agotado [;
      self.liberar_boton();
    ],
    pulsar [;
      if (boton_este hasnt general) {
        ArrancarReloj(self, 2);
        give self general;
        self.alguien_pulsa();
      } else {
        PararReloj(self);
        PararReloj(boton_este);
        give self general;

        switch (actor) {
          objjugador:
            print "¡El botón se queda hundido!^";
          jason:
            HablaJason("!Esto parece que funciona!");
        }
        
        ! Se activa la autodestrucción:
        remove alien;
        give alien ausente;
        alien.comportamiento = 4;
        give ascensor abierto;
        control_sonido_detector.activar(Sirena_ogg, SONIDO_BUCLE_INFINITO);
        ArrancarDaemon(autodestruccion);
        style bold;
        print "^¡HAS ACTIVADO EL PROTOCOLO DE AUTODESTRUCCIÓN!^";
        style roman;
      }
    ];


Object aula "Aula"
  class Lugar
  with
    nombre_localidad "aula",
    descripcion "Te encuentras en un aula de enseñanza. Los distintos puestos para los
                 alumnos están perfectamente alineados a tu izquierda. A tu derecha, la
                 mesa del docente, pulcramente ordenada. En el centro de la sala hay un
                 proyector orientado hacia una enorme pantalla blanca, situada al
                 fondo.",
    sgw_img Aula_jpg,
    al_o laberinto3,
    al_s laberinto7;


Object decorado_aula aula
  class DecoradoAmpliado
  with
    desc_patas "Son, sencillamente, las patas que sujetan la mesa del docente.",
    describir
      'patas' [; return self.desc_patas; ] G_FEMENINO + G_PLURAL
      'pata'  [; return self.desc_patas; ] G_FEMENINO;


Object "mesas de los alumnos" aula
  with
    nombre 'mesa' 'mesas//p' 'pupitre' 'pupitres//p' 'puesto' 'puestos//p',
    adjetivos 'alumno' 'alumnos//p',
    descripcion "Cada puesto es una especie de pupitre pequeño y rectangular frente a una
                  cómoda silla acolchada. La mesa del profesor es algo más grande, y la
                  silla parece más cómoda.",
  has
    femenino nombreplural escenario;

Object "sillas de los alumnos" aula
  with
    nombre 'silla' 'sillas//p',
    adjetivs 'alumno' 'alumnos//p',
    descripcion "Son pequeñas y mullidas sillas de color gris. Parecen cómodas.",
  has
    femenino nombreplural escenario;


Object "mesa del profesor" aula
  with
    nombre 'mesa',
    adjetivos 'profesor' 'docente',
    descripcion "Una mesa algo de un tamaño más grande que el resto de los puestos.
                 Es tan sencilla, que sólo se compone de una superficie sujeta por
                 cuatro patas metálicas.",
  has
    femenino escenario;


Object "silla del profesor" aula
  with
    nombre 'silla' 'sillon',
    adjetivos 'profesor' 'docente',
    descripcion "Es un sillón amplio, de color negro, con reposabrazos.",
  has
    femenino escenario;


Object proyector "proyector" aula
  with
    nombre 'proyector',
    descripcion "Un proyector.",
    describir [;
      if (self has encendido) {
        "^El proyector está emitiendo imágenes hacia la pantalla.";
      } else {
        rtrue;
      }
    ],
    reaccionar_antes [;
      Escuchar:
        if (self has encendido) {
          "Oyes la voz de la grabación, describiendo minuciosamente y con todo detalle la
           fisionomía de un ser extraterrestre. Por sus palabras y lo visto en la pantalla
           de proyección, no te cabe duda de que está hablando del mismo ser que os atacó
           antes a Jason y a ti.";
        } else {
          rfalse;
        }
    ],
    antes [;
      Examinar:
        print "Es un dispositivo destinado a proyectar el contenido audiovisual
               almacenado en un holocubo";
        if (holocubo notin self) {
          ", pero no contiene ninguno dentro para proyectar.";
        }
        if (self has abierto) {
          ", y contiene uno, pero no se podrá poner en funcionamiento hasta que el
           proyector se cierre.";
        }
        if (self hasnt encendido) {
          ". Está apagado.";
        }
        ", y está emitiendo imágenes hacia una pantalla situada en frente.";
      Quemar: <<Encender self>>;
      Encender:
        if (holocubo notin self) {
          "No hay nada que proyectar.";
        }
        if (self has abierta) {
          "Tienes que cerrar el proyector primero.";
        }
    ],
    despues [;
      Encender:
        "Enciendes el proyector, y sobre la pantalla empiezan a desfilar imágenes.";
    ],
    listarse [;
      if (etapa_inventario == 2) {
        if (self has encendida) {
          print " (encendido)";
        } else {
          print " (apagado)";
        }
        rtrue;
      }
    ]
  has
    conmutable abrible abierto recipiente estatico;


! El atributo general indica si es la primera vez que examinamos la pantalla con el
! proyector encendido:

Object pantalla "pantalla" aula
  with
    nombre 'pantalla',
    descripcion "Una superficie blanca y rectangular anclada en el techo y ocupando la
                 mayor parte de la pared norte.",
    antes [;
      Examinar:
        if (proyector has encendido) {
          if (self hasnt general) {
            if (jason.sacado()) {
              give self general;
              sacador_de_aliens.meter_alien();
              alien.comportamiento = 3;
              PNJ_Ruta(alien, MOVIMIENTO_PERSEGUIR, jugador);
              print "Al mirar a la pantalla, ves el contenido del holocubo. Y lo que ves
                     te da la solución a tu problema:^^";
            } else {
              print "Al mirar a la pantalla, ves el contenido del holocubo:^^";
            }
          }
          style bold;
          print "*** HOLOCUBO DE SEGURIDAD - PROTOCOLO DE EMERGENCIA: ***^^";
          style roman;
          print "En caso de emergencia, proceder a la autodestrucción del complejo
                 siguiendo el siguiente procedimiento:^^";
          print "1. Penetrar en la zona de emergencia (nivel -1, zona sur).^";
          print "2. Cerrar su única entrada para aislarla del exterior.^";
          print "3. Pulsar simultáneamente los botones rojo y azul, en ese orden, antes
                 de que vuelvan a su posición inicial.^^";
          style bold;
          print "*** NOTAS: ***^^";
          style roman;
          print "a. Tras la activación del protocolo, se abrirá el ascensor de emergencia
                 (nivel -2, zona norte).^";
          print "b. El ascensor será la única vía de salida mientras dure la emergencia.^";
          print "c. Se dispondrá de un máximo de diez minutos para evacuar el complejo.^";
          print "d. El protocolo no puede abortarse en ningún momento.^^";
          print "[La clave para acceder al documento Holocubo.pdf es ";
          style bold;
          print "APOGEO";
          style roman;
          print " (en mayúsculas).]^";
          
          if (jason in LugarReal()) {
            HablaJason("¡Vamos a volar todo esto en pedazos!");
            jason.hablar = false;
            PNJ_Ruta(jason, MOVIMIENTO_PERSEGUIR, jugador);
          }

          rtrue;
        } else {
          "La pantalla permanece en blanco.";
        }
    ],
  has
    femenino escenario;


Object seguridad "Sala de seguridad"
  class Lugar
  with
    nombre_localidad "sala",
    descripcion "Esta es la sala de seguridad. Tan fría y estéril como el restro del
                 complejo, siendo lo más destacable la consola de control situada al
                 fondo. Un cable blanco y otro negro entran en la sala por medio de una
                 hendidura en la pared, para acabar conectados a la parte posterior de la
                 consola. Aquí la luz es más apagada y por las paredes fluyen tubos de
                 distinto grosor.",
    sgw_img Seguridad_jpg,
    al_n laberinto18,
  has
    femenino;


Object decorado_seguridad seguridad
  class DecoradoAmpliado
  with
    visto_en_seguridad,
    desc_cables [;
      if (~~(camara_seguridad.visto_en_oficina)) {
        decorado_seguridad.visto_en_seguridad = true;
        PonerCableBlanco();
        move rastro_cable_blanco to seguridad;
      }
      if (~~(camara_seguridad.visto_en_laboratorio)) {      
        decorado_seguridad.visto_en_seguridad = true;
        PonerCableNegro();
        move rastro_cable_negro to seguridad;
      }
      return "Son dos cables (uno blanco y otro negro) que provienen del exterior
              de la sala y que están conectados a la consola.";
    ],
    desc_tubos "Algunos anchos, otros delgados... Todos metálicos, reptando por la pared,
                entrando o saliendo de ella.",
    desc_paredes "Lo único digno de destacar de las paredes son los tubos que las cruzan
                  y los dos cables que salen de una de ellas por un agujero.",
    desc_agujero "Es un agujero situado en la parte superior de la pared del fondo, por el
                  que salen dos cables de distinto color.",
    antes [;
      Coger, Tocar:
        if (self.cantidad == 'tubos' or 'tubo') {
          "Los tubos están muy sujetos a las paredes, y su tacto es frío y seco.";
        } else if (self.cantidad == 'cables' or 'cable') {
          "Mejor no tocarlos demasiado. Podrías llevarte una buena descarga...";
        } else if (self.cantidad == 'hendidura' or 'agujero' or 'orificio') {
          "El agujero está muy alto y no alcanzas. Pero está claro que tampoco es demasiado
           importante...";
        }
    ],
    describir
      'hendidura' [; return self.desc_agujero; ] G_FEMENINO
      'agujero'   [; return self.desc_agujero; ] 0
      'orificio'  [; return self.desc_agujero; ] 0
      'paredes'   [; return self.desc_paredes; ] G_FEMENINO + G_PLURAL
      'pared'     [; return self.desc_paredes; ] G_FEMENINO
      'tubos'     [; return self.desc_tubos; ] G_PLURAL
      'tubo'      [; return self.desc_tubos; ] 0
      'cables'    [; return self.desc_cables(); ] G_PLURAL
      'cable'     [; return self.desc_cables(); ] 0;


! posicion_actual:
! = 0: Situación de partida
! = 1: Se ha visto la grabación del alien escapando
! = 2: Se ha visto la grabación de la caja fuerte

Object consola "consola de control" seguridad
  with
    nombre 'consola' 'seguridad' 'control',
    descripcion "Es una consola que sirve para poder ver las grabaciones de las cámaras
                 de seguridad. Está formada por un monitor de televisión y un
                 pequeño teclado en el que destaca un botón ~Reproducir~.",
!                 Tiene un botón ~Reproducir~, otro botón ~Retroceder~ y
!                 otro más para seleccionar la grabación a reproducir, etiquetado con
!                 la palabra ~Seleccionar~.",
    posicion_actual 0,
  has
    femenino transparente escenario;

!Object boton_retroceder "botón Retroceder" consola
!  with
!    nombre 'boton',
!    adjetivos 'retroceder',
!    descripcion "Es un botón que sirve para poder volver a visualizar la grabación desde
!                 el principio.";

Object monitor "monitor de televisión" consola
  with
    nombre 'monitor' 'pantalla' 'aparato' 'television',
    descripcion [;
      print "Es el aparato que muestra las imágenes grabadas por las cámaras de
             seguridad. Ahora mismo ";
    ],
    despues [;
      Examinar:
        switch (consola.posicion_actual) {
          0: "sólo muestra nieve...";
          1: "muestra la última imagen del alien escapando.";
          2: "muestra la caja fuerte abierta.";
          3: "indica que no hay más imágenes grabadas.";
        }
    ];

Object boton_reproducir "botón Reproducir" consola
  with
    nombre 'boton' 'reproducir',
!    adjetivos 'reproducir',
    descripcion "Es un botón que sirve para poder visualizar la grabación desde su
                 posición actual.",
    grabacion_laboratorio [;
      print "Al pulsar el botón de reproducción, comienzan a aparecer imágenes en el
             monitor de la consola. Y lo que ves parece ser la grabación de la cámara
             de seguridad de un laboratorio";
      if (laboratorio has visitado) {
        print " (evidentemente, el mismo laboratorio que ya conoces)";
      }
      print ", donde varias personas vestidas con batas blancas caminan aquí y allá
             llevando materiales y anotando datos.^";
      EsperarTecla();
      print "^¡Un momento! ¡En una especie de jaula de contención se vislumbra claramente
             la figura del monstruo! ¡Lo tienen encerrado para estudiarlo! Ahora lo
             entiendes todo... Ese monstruo es un arma demasiado poderosa para ser
             ignorada, y ese laboratorio no es más que el lugar donde pretendían sacar
             partido de su potencial destructivo. El ansia de poder de estos burócratas
             es aún mayor de lo que esperabas...^";
      EsperarTecla();
      print "^¿Qué...? ¡El monstruo ha roto la cabina de contención y se escapa! ¡Dios
             santo... qué carnicería! No puedes seguir manteniendo la mirada en la
             pantalla, pero casi puedes oir la sangre derramándose al salir de sus
             destrozados cuerpos. ¡Malditos sean mil veces los responsables de esta
             masacre! Por su culpa, todo este complejo ha pasado a convertirse en un
             enorme cementerio.^";
      EsperarTecla();
      print "^Cuando por fin reúnes el valor suficiente para seguir mirando, ves que la
             grabación se ha detenido, y que la consola indica que hay otra grabación
             pendiente de ser visionada.^";
      rtrue;
    ],
    grabacion_oficina [;
      move cuadro to oficina;
      print "En esta grabación ves una oficina";
      if (oficina has visitado) {
        print "(la oficina que ya conoces)";
      }
      print ", en la que hay varias personas trabajando, con sus importantísimos
             documentos oficiales y sus potentísimos equipos informáticos. En un momento
             dado, el que parece de mayor graduación dice algo y los demás abandonan la
             habitación.^";
      EsperarTecla();
      print "^Una vez que se ve por fin solo, se levanta y se dirige hacia uno de los
             muchos cuadros que tiene en la pared del fondo. Lo retira y... ¡ves que
             detrás hay una caja fuerte! El pez gordo introduce ahora la combinación de
             la caja, y a continuación la abre. El monitor lo ha mostrado bien claro: el
             número de la combinación es el 111582.^";
      EsperarTecla();
      print "^El hombre saca algo de su bolsillo... Es un objeto no muy grande, de forma
             rectangular... Finalmente, lo mete en la caja fuerte y la cierra, volviendo
             a colocar el cuadro en su posición original.^";
      rtrue;
    ],
    antes [;
      Empujar:
        if (consola.posicion_actual < 3) {
          consola.posicion_actual++;
          switch (consola.posicion_actual) {
            0: "El monitor sólo muestra nieve...";
            1: self.grabacion_laboratorio(); rtrue;
            2: self.grabacion_oficina(); rtrue;
            3: "No hay más imágenes grabadas.";
          }
        } else {
          "No ocurre nada. Parece que se han agotado las grabaciones.";
        }
    ];

!Object boton_seleccionar "botón Seleccionar" consola
!  with
!    nombre 'boton',
!    adjetivos 'seleccionar',
!    descripcion "Es un botón que sirve para poder seleccionar qué grabación se desdea
!                 reproducir.";


Object laberinto1
  class Laberinto,
  with
    al_s balcon2,
    al_e balcon1,
    ! Al oeste hay un hábitat
    antes [;
      Ir:
        if (uno == obj_o) {
          ImprimirOEjecutar(habitat, direccion);
          rtrue;
        }
    ],
    salidas [ d;
      switch (d) {
        obj_o: return 3;
        default: rfalse;
      }
    ];


Object habitat "hábitat"
  with
    nombre 'puerta' 'vivienda',
    adjetivos 'habitat',
    esta_en laberinto1 laberinto11 balcon6 laberinto18,
    descripcion "Es la puerta de acceso a un hábitat. Está cerrada y no ves manera de
                 abrirla.",
    direccion "En esa dirección hay una puerta cerrada que da acceso al hábitat de algún
               colono o familia.",
    antes [;
      Abrir, QuitarCerrojo:
        "No hay forma de abrir esa puerta. En realidad, no deberías tenerla en cuenta.";
      Cerrar:
        "Ya está bastante cerrada.";
    ],
  has
    escenario;

   
Object laberinto3
  class Laberinto,
  with
    al_s balcon3,
    al_e aula,
    al_o balcon1;

      
Object laberinto6
  class Laberinto,
  with
    al_n balcon3,
    al_o balcon4;

    
Object laberinto7
  class Laberinto,
  with
    al_n aula,
    al_o balcon3;


Object laberinto11
  class Laberinto,
  with
    al_n balcon2,
    al_e balcon4,
    ! Al oeste hay un hábitat
    antes [;
      Ir:
        if (uno == obj_o) {
          ImprimirOEjecutar(habitat, direccion);
          rtrue;
        }
    ],
    salidas [ d;
      switch (d) {
        obj_o: return 3;
        default: rfalse;
      }
    ];


Object laberinto16
  class Laberinto,
  with
    al_n enfermeria,
    al_o laberinto19;


Object laberinto18
  class Laberinto,
  with
    al_n balcon6,
    al_e balcon8,
    al_s seguridad,
    ! Al oeste hay un hábitat
    antes [;
      Ir:
        if (uno == obj_o) {
          ImprimirOEjecutar(habitat, direccion);
          rtrue;
        }
    ],
    salidas [ d;
      switch (d) {
        obj_o: return 3;
        default: rfalse;
      }
    ];
    

Object laberinto19
  class Laberinto,
  with
    al_n balcon7,
    al_o balcon8,
    al_e laberinto16,
    al_s laboratorio;


Object laberinto28
  class Laberinto,
  with
    sgw_img Ascensor_Emergencia_jpg,
    al_e balcon12,
    adentro laberinto28, ! Esto es para que funcione "jason, vete adentro"
    no_puedes_ir "Este nivel aún está sin terminar. No hay más salidas.",
    antes [ t;
      Ir:
        if (uno == obj_adentro) {
          if (ascensor has abierto) {
            if (jason in self && jason.tipo_de_movimiento == MOVIMIENTO_PERSEGUIR &&
                jason.perseguido == jugador) {
              if (hayGraficos) {
                t = true;
                ApagarGraficosSub(true);
              }
              clearMainWindow();
              tt_escape1.visualiza();
              Damusix.TocarV(Rugido_ogg);
              tt_escape2.visualiza();
              control_sonido_detector.cambiar_volumen($3000);
              move ascensor_arriba to exterior3;
              move jason to exterior3;
              JugadorA(exterior3);
              if (t) {
                EncenderGraficosSub(true);
              }
              <<Mirar>>;
!              rtrue;
            } else {
              "¡No dejes a Jason aquí!";
            }
          } else {
            "No podrás entrar en el ascensor hasta que esté abierto.";
          }
        }
    ],
    antesPNJ [;
      Ir:
        if (otro == obj_adentro) {
          if (ascensor has abierto) {
            HablaJason("¡Entre usted primero! ¡Vamos!");
          } else {
            HablaJason("No puedo entrar ahí... Está cerrado.");
          }
        }
        rtrue;
    ];


Object laberinto30
  class Laberinto,
  with
    al_o balcon12,
    al_e laberinto36;


Object laberinto36
  class Laberinto,
  with
    descripcion "Estás en unos pasillos interiores del complejo. Metros y metros de
                 estructura metálica recorren estos pasillos, que parecen no acabar nunca.
                 Puedes apreciar que las paredes en esta sección son más curvadas, y los
                 techos bastante más bajos; tanto que, en esta sala, casi hay que pasar
                 agachado. El túnel continúa por el oeste, y hacia arriba hay acceso a
                 través de una trampilla en el techo.",
    arriba ventilacion1,
    al_o balcon12,
    no_puedes_ir "Este nivel aún está sin terminar. No hay más salidas.",
    antes [;
      Examinar:
        if (uno == obj_arriba) {
          "Hay una trampilla a la que tienes un fácil acceso, debido a la poca altura
           a la que se encuentra el techo.";
        }
    ];


Object trampilla "trampilla" laberinto36
  with
    nombre 'trampilla' 'techo' 'orificio',
    descripcion "Es una trampilla abierta en el techo.",
    antes [;
      Meterse:
        <<Ir obj_arriba>>;
      Examinar:
        rfalse;
      default:
        "Es un simple agujero de acceso a una zona superior. No hay mucho más que añadir.";
    ],
  has
    escenario;


Object ascensor "ascensor de emergencia" laberinto28
  with
    nombre 'ascensor' 'elevador' 'cubiculo' 'emergencia',
    descripcion "Es una especie de cabina rectangular de cristal muy resistente,
                 reforzado por cientos de remaches metálicos. Su tamaño aproximado
                 es de unos cinco por cinco metros. Próximo a él, junto a la pared,
                 hay un cartel que dice: ~ASCENSOR DE EMERGENCIA: Apertura
                 automática~.",
    si_cerrado "En un rincón del túnel hay un cubículo en forma de ascensor.",
    si_abierto "El ascensor de emergencia está abierto. ¡Yo que tú me andaría con prisas!",
    antes [;
      Abrir, Atacar:
        if (self hasnt abierto) {
          "Al parecer, el ascensor sólo se abrirá en caso de emergencia. No hay cerradura
           ni panel de control con el que accionarlo.";
        } else {
          "El ascensor ya está abierto. ¡Aprovecha la ocasión!";
        }
      Cerrar:
        if (self hasnt abierto) {
          "No está abierto, así que no puedes cerrarlo.";
        } else {
          "No es posible cerrarlo, y no creo que sea el momento ahora de pensar en ello.";
        }
      Meterse:
        <<Ir obj_adentro>>;
    ],
  has
    estatico recipiente abrible; ! Hay que poner recipiente con si_abierto y si_cerrado


Object ventilacion1
  class Lugar,
  with
    nombre_corto "Sala oeste de ventilación",
    nombre_localidad "sala destinada a la ventilación",
    descripcion "Estás en una de las salas de ventilación en los pasillos interiores del
                 complejo. En el techo anormalmente bajo de la estancia puedes ver varios
                 de los muchos ventiladores que se encargan de suministrar aire por toda
                 la subterránea del complejo. El zumbido que producen es casi hipnótico.",
    sgw_mus Ventilacion_ogg,
    sgw_img Ventilacion1_jpg,
    abajo laberinto36,
    al_e hueco_techo,
    antes [;
      Escuchar:
        "El sonido de las corrientes de aire provocadas por los ventiladores es
         constante y rítmico. Es de esos sonidos que no te dejan pensar.";
      Examinar:
        if (uno == obj_abajo) {
          "Desde aquí puedes bajar al túnel inferior.";
        } else if (uno == obj_arriba) {
          "Los ventiladores rugen fuertemente allá arriba, inyectando aire hacia el
           interior.";
        }
    ],
  has
    femenino;


Object decorado_ventilacion1 ventilacion1
  class DecoradoAmpliado
  with
    desc_ventiladores "Pequeños pero potentes ingenios que giran veloces absorbiendo aire
                       del exterior y expulsándolo al interior del complejo.",
    desc_paredes "Las paredes son metálicas y lisas, sin ningún adorno.",
    describir
      'paredes'      [; return self.desc_paredes; ] G_PLURAL
      'pared'        [; return self.desc_paredes; ] 0
      'ventiladores' [; return self.desc_ventiladores; ] G_PLURAL
      'ventilador'   [; return self.desc_ventiladores; ] 0
      'aspas'        [; return self.desc_ventiladores; ] G_FEMENINO + G_PLURAL
      'aspa'         [; return self.desc_ventiladores; ] G_FEMENINO;


Object hueco_techo "Sala este de ventilación"
  class Lugar
  with
    descripcion "Estás en una sala de ventilación en los pasillos interiores del
                 complejo. Todos los ventiladores que deberían estar girando en
                 el techo se encuentran esparcidos por el suelo con sus aspas rotas e
                 inútiles. Una enfermiza luz de color verde pálido lo ilumina todo sin
                 que sea posible apreciar su fuente. Pero lo que más llama tu atención
                 es la sustancia mucosa y repugnante que cubre casi la totalidad de las
                 paredes y el techo. Un techo tan bajo que casi te impide permanecer
                 totalmente de pie, y en el que, además, se aprecia un enorme
                 hueco, cubierto también por la misma desagradable viscosidad.",
    sgw_mus Vaina_ogg,
    sgw_img Hueco_Techo_jpg,
    al_o ventilacion1,
    antes [;
      Examinar:
        if (uno == obj_abajo) {
          "En el suelo sólo cabe destacar las aspas rotas de los ventiladores y la
           asquerosa sustancia que lo empapa todo y que casi te impide caminar.";
        } else if (uno == obj_arriba) {
          <<Examinar techo>>;
        }
    ],
    despues [;
      Ir:
        if (self hasnt visitado) {
          print "Avanzas un poco más, dejando atrás los sonoros ventiladores... y por
                 un momento tu mente te hace creer que lo que tienes ante ti no es más
                 que una alucinación producto del hipnotizante ritmo del aire... Pero no:
                 lo que ves es muy real, así que algo no marcha bien... Nada bien...^";
          EsperarTecla();
          style underline;
          print "^Dios santo... Pero... ¿qué...? ¿Qué es esto...?^";
          EsperarTecla();
          print "^Oh... Dios... mío...^";
          style bold;
          EsperarTecla();
        }
    ];


Object techo "techo" hueco_techo
  with
    nombre 'hueco' 'techo',
    descripcion "Incrustado dentro del hueco en el techo ves una vaina viscosa y
                 verde de aspecto repulsivo y ligeramente fosforescente.",
    despues [;
      Examinar:
        give vaina ~escenario;
        give vaina estatico;    
    ],
  has
    escenario;


Object abrazacaras "araña" hueco_techo
  with
    nombre_f 'arana',
    nombre_m 'animal' 'aracnido' 'abrazacaras',
    genero G_FEMENINO,
    descripcion [;
      print "Es un animal muerto, parecido a una araña, tumbado boca arriba en el
             suelo. Es de color crema, con ocho delgadas y articuladas patas, y una
             cola larga y con forma de serpiente. En lo que parece ser el ~cuerpo~,
             observas órganos internos y una especie de trompa que parece hacer las
             veces de ~boca~. Verdaderamente, es un ser asqueroso.";
      if (self.cortada) {
        " En una de sus patas hay una pequeña incisión.";
      } else {
        "";
      }
    ],
    inicial "Una especie de araña nauseabunda yace muerta en el suelo, junto a un enorme
             huevo.",
    cortada false,
    antes [;
      Examinar:
        rfalse;
      Cortar:
        if (otro == nothing) {
          "Necesitarás usar algún instrumento cortante.";
        }
        if (otro == bisturi) {
          if (vaina has abierta) {
            "El bisturí ya no tiene energía para cortar nada.";
          }
          if (~~(self.cortada)) {
            self.cortada = true;
            hueco_techo.marcaRastro(alien, rastro_acido, obj_abajo, nivelClaro);
            move rastro_acido to hueco_techo;
            "Acercas el bisturí a una de las patas de la ~criatura~ y, al activarlo,
             un humo denso y tóxico comienza a surgir del punto de corte. Al mismo
             tiempo, una gota de ácido brota de la herida y cae al suelo, disolviendo
             todo a su paso...";
          } else {
            "Mejor no... No conseguiste nada la primera vez.";
          }
        } else {
          "Eso no sirve para cortar nada.";
        }
      default:
        "No te atreves ni a acercarte, aunque sabes que está muerta.";
    ],
  has
    estatico;


Object decorado_hueco hueco_techo
  class DecoradoAmpliado
  with
    desc_paredes "Las paredes están prácticamente cubiertas por esa sustancia viscosa tan
                  desagradable.",
    desc_ventiladores "Las enormes aspas de los ventiladores yacen por el suelo, rotas en
                       pedazos.",
    antes [;
      Cortar:
        if (self.cantidad == 'huevo') {
          "No es necesario cortarlo: ya está abierto por arriba.";
        }
      Tocar:
        switch (self.cantidad) {
          'sustancia': <<Examinar self>>;
          'huevo': "Es algo membranoso al tacto... aunque muy duro.";
          'cola': "Tiene el tacto y la dureza de una serpiente.";
          'luz': "¿Tocar algo inmaterial? ¿Te encuentras bien?";
        }
      Coger:
        switch (self.cantidad) {
          'ventiladores', 'ventilador',
          'aspas', 'aspa': "No serviría de nada. Las aspas están rotas y son inservibles.";
          'cola': "La tomas en tus manos, pero un miedo irracional te obliga a soltarla
                   y alejarte del animal.";
          'huevo': "Parece pegado al suelo. De todas formas, no hay razón para
                    llevárselo.";
        }
    ],
    describir
      'ventiladores' [; return self.desc_ventiladores; ] G_PLURAL
      'ventilador'   [; return self.desc_ventiladores; ] 0
      'aspas'        [; return self.desc_ventiladores; ] G_FEMENINO + G_PLURAL
      'aspa'         [; return self.desc_ventiladores; ] G_FEMENINO
      'paredes'      [; return self.desc_paredes; ] G_FEMENINO + G_PLURAL
      'pared'        [; return self.desc_paredes; ] G_FEMENINO
      'cola'         "Es un apéndice muy largo y robusto que sobresale de la parte posterior
                      del animal." G_FEMENINO
      'sustancia'    "Es una sustancia muy pegajosa, unas veces muy blanda y otras muy
                      sólida y rígida." G_FEMENINO
      'luz'          "Es una luminiscencia débil de un color verde muy apagado, única hasta ahora
                      en todo el complejo." G_FEMENINO
      'huevo'        "El huevo está abierto por la parte superior, merced a cuatro membranas que
                      se pliegan hacia fuera mostrando el interior. Al acercarte, observas que
                      está vacío, y que las paredes están compuestas de algún tipo de proteína
                      traslúcida que deja pasar algo de luz. Por lo demás, está completamente
                      cubierto (por dentro y por fuera) de la sustancia viscosa que inunda la
                      sala." 0;


Object "patas" hueco_techo
  with
    nombre 'patas' 'pata',
    descripcion [;
      print "Ocho patas grotescamente largas y delgadas, con varias articulaciones.";
      if (abrazacaras.cortada()) {
        " Una de ellas tiene una pequeña incisión.";
      } else {
        "";
      }
    ],
    antes [;
      Cortar: <<Cortar abrazacaras otro>>;
      Coger: <<Coger abrazacaras>>;
      Empujar: <<Empujar abrazacaras>>;
      Tirar: <<Tirar abrazacaras>>;
      default: "Deja las patas en paz... Te dan malas vibraciones.";
    ],
  has
    escenario;


Object "incisión"
  with
    nombre_m 'corte',
    nombre_f 'incisión',
    genero G_FEMENINO,
    descripcion "Es un pequeño corte por el que salió antes una gota de ácido.",
  has
    escenario;


! El atributo general indica que ya sabemos que Jason está dentro de la vaina

Object vaina "vaina mucosa" hueco_techo
  with
    nombre 'vaina' 'mucosa' 'gelatinosa' 'mucosidad' 'viscosidad' 'viscosa'
           'gelatina' 'capullo' 'mocos',
    descripcion [;
      print "Es una especie de capullo mucoso y gelatinoso, pero que, al mismo tiempo,
             tiene el aspecto de ser terríblemente duro. Calculas que tendrá más de dos
             metros de alto por casi un metro de ancho. Está incrustado dentro del hueco
             del techo, firmemente pegado al mismo por grandes y pequeños filamentos
             pegajosos de la misma sustancia que lo forma. ";
      if (self has abierta) {
        print "Muestra un enorme corte, lo bastante grande como para dejar pasar un cuerpo
         humano adulto. ";
      }
      if (jason in self) {
        if (self has general) {
          print "Jason sigue dentro. ¡Sácalo de ahí!";
        } else {
          give self general;  ! general indica que ya sabemos que Jason está dentro
          print "Espera... Un momento... No puedes creer lo que ves... Te acercas todo
                 lo que puedes, pegando casi tu cara contra el nauseabundo capullo...
                 y entonces lo ves...";
          EsperarTecla();
          style bold;
          print "^^¡VES A JASON DENTRO DE LA VAINA!";
          style roman;
        }
      }
      "";
    ],
    si_cerrada [;
      print "Hay una vaina gelatinosa y rígida pegada en un hueco en el techo. ";
      if (jason in self && self has general) {
        "Jason sigue dentro de ella.";
      } else {
        "";
      }
    ],
    si_abierta [;
      print "La vaina mucosa del techo muestra un enorme corte, lo bastante grande como
             para dejar pasar un cuerpo humano adulto. ";
      if (jason in self && self has general) {
        "Jason sigue dentro de ella.";
      } else {
        "";
      }
    ],
    con_llave bisturi,  ! El bisturi nos permite abrir la vaina
    antes [;
      Abrir, Atacar:
        ! Si no hay aire fuera, no se deja:
        if (self.comprobar_aire()) {
          rtrue;
        }
        ! Si intentas abrir con algo que no sea el bisturí
        if (self has cerrojoechado && otro ~= bisturi) {
          "Necesitarás algún objeto cortante para poder abrir esa sustancia tan rígida.";
        }
      Cortar:
        ! Si no hay aire fuera, no se deja:
        if (self.comprobar_aire()) {
          rtrue;
        }
        ! Si intentas cortar con algo que no sea el cortador
        if (self has cerrojoechado && otro ~= bisturi) {
          "Necesitarás algún objeto cortante para poder cortar esa sustancia tan rígida.";
        }
        <<QuitarCerrojo self otro>>;
      Recibir:
        ! Intentas meter algo en la vaina
        "No, no lo hagas.";
      DejarSalir:
        ! Intentas sacar algo de la vaina. Si está cerrada, se dice, y retorna true.
        ! Si está abierta, se retorna false, y se permite.
        ImprimirOEjecutar(self, dejar_salir);
      QuitarCerrojo:
        ! Si no hay aire fuera, no se deja:
        if (self.comprobar_aire()) {
          rtrue;
        }
        ! Se intenta abrir la vaina con algo
        if (self has cerrojoechado && otro ~= bisturi) {
          "Necesitarás algún objeto cortante para poder abrir esa sustancia tan rígida.";
        }        
        if (self hasnt cerrojoechado) {
          "La vaina ya está suficientemente abierta.";
        }
      Cerrar:
        ! Se intenta cerrar la vaina
        "Imposible, e ilógico.";
    ],
    comprobar_aire [;
      ! Se intenta abrir la vaina antes de arreglar el procesador atmosférico
      if (circuito hasnt general) {
        "No deberías intentar sacar a Jason de esa vaina antes de disponer de aire
         respirable. Su traje parece roto desde aquí, y probablemente esté vivo gracias
         a que el aire del traje no ha podido escapar a través de esa vaina
         herméticamente sellada. Debes conseguir que Jason tenga aire respirable fuera
         de esa vaina antes de sacarlo.";
      } else {
        rfalse;
      }
    ],
    ! Esta rutina se llama desde vaina.antes y desde jason.antes
    dejar_salir [;
      if (self hasnt abierta) {
        "La vaina es demasiado dura para poder sacarlo.";
      }
    ],
    despues [;
      Examinar:
        give self ~escenario;
        give self estatico;
      QuitarCerrojo:
        Damusix.TocarV(Bisturi_ogg);
        print "Desgarras cuidadosamente la vaina usando el bisturí, procurando no dañar
               a Jason. Cuando terminas, la vaina presenta un amplio corte longitudinal.
               Justo a tiempo, ya que el bisturí ha dejado de funcionar por agotamiento
               de su energía interna.^";
        tate_callao = 1;  ! Para que no diga que abres la vaina a continuación
        <Abrir self>;     ! Abres la vaina (pero el intérprete no dice nada)
        tate_callao = 0;  ! Vuelves a la situación normal
        rtrue;
    ],
  has
    escenario femenino transparente recipiente cerrojo cerrojoechado abrible;   



! ====================================================================
!
! PERSONAJES
!
! ====================================================================



Object jason "Jason" nave1
  class Movil
  with
    nombre 'jason' 'mayor' 'soldado' 'marine' 'companero',
    descripcion [;
      if (self in vaina) {
        "A través de la vaina semi-transparente puedes ver que tiene los ojos cerrados, y
         que no parece muy consciente de tu presencia. Realmente tiene mal aspecto. Su
         traje tiene un corte bastante grande en un costado y, por lo que puedes intuir,
         el oxígeno de su traje llena ahora por completo la vaina.";
      }
      if (self.sacado()) {
        ! Lo hemos sacado de la vaina:
        print "No tiene muy buen aspecto, aunque se esfuerza en aparentar lo contrario. ";
      } else {
        print "Jason, tu compañero. Es bastante más grande y corpulento que tú, y su mirada
               refleja sarcasmo y frialdad a partes iguales. Es, en definitiva, todo un
               ejemplo de duro y capacitado soldado, siempre dispuesto para el combate. ";
      }
      if (children(self) ~= 0) {
        print "Lleva ";
        EscribirListaDesde(child(self), INFOTOTAL_BIT + ESPANOL_BIT + RECURSIVO_BIT);
        ".";
      } else {
        "";
      }
    ],
    inicial "^Jason, tu compañero de expedición, está junto a ti aguardando tus órdenes.",
    hablar true,
    ! Propiedad que indica si ya hemos sacado a Jason de la vaina:
    sacado [;
      return procesador.arreglado() && jason notin vaina;
    ],
    describir [;
      ! La propiedad general sirve para saber si es la primera vez que se describe
      if (self hasnt general) {
        give self general;
        ImprimirOEjecutar(self, inicial);
        rtrue;
      } else {
        print "^";
        if (~~(self.sacado())) {
          switch (random(6)) {
            1: "Jason se encuentra a tu lado.";
            2: "Jason te mira expectante.";
            3: "Jason mira alrededor con atención.";
            4: "Jason, tu compañero, está a tu lado esperando órdenes.";
            5: "Jason te mira de reojo con cierto recelo.";
            6: "Junto a ti se encuentra Jason, el marine.";
          }
        } else {
          switch (random(3)) {
            1: "Jason camina con cierta lentitud.";
            2: print "Jason se encuentra a tu lado, respirando con cierta dificultad.";
               if (~~EsExterior(parent(self))) {
                 " De vez en cuando, se apoya en una pared.";
               } else {
                 "";
               }
            3: "Jason te mira con cara de estar pasándolo realmente mal.";
          }
        }
      }
    ],
    gramatica [ s1 s2 a o f;
      a = null;

      switch (palabra_verbo) {
        'coge', 'toma', 'recoge': a = ##Coger;
        'saca':                   a = ##Sacar;
        'deja', 'suelta':         a = ##Dejar;
        'ponte':                  a = ##Vestir;
        'quitate', 'sacate':      a = ##Desvestir;
        'da':                     a = ##Dar;
        'dispara':                a = ##Disparar;
        'enciende', 'activa':     a = ##Encender;
        'apaga', 'desactiva':     a = ##Apagar;
        'pulsa', 'empuja':        a = ##Empujar;
        'entra':                  a = ##Entrar;
        'metete':                 a = ##Meterse;
        'sal':                    a = ##Salir;
        'salte':                  a = ##Salirse;
        'no':                     if (SiguientePalabra() == 'te' &&
                                      SiguientePalabra() == 'muevas' or 'vayas') {
                                    accion = ##Quedar;
                                    rtrue;
                                  }
        default: rfalse;
      }
        
      s1 = SiguientePalabra();
      f = 0;

      ! Si se dice: "JASON, COGE TRAJE" sin especificar cuál, se da prioridad
      ! al de Jason (si lo ve; si no, le da prioridad al que ve):
      if (s1 == 'traje') {
        if (PruebaDeAlcance(traje_jason, jason)) {
          uno = traje_jason;
        } else {
          uno = tu_traje;
        }
        accion = a;
        rtrue;
      }

      if (s1 == '-me') {
        f = 1;
        s1 = SiguientePalabra();
      }
      
      s2 = SiguientePalabra();

      if (s2 == 'traje') {
        palabra_verbonum++;
        
        switch(s1) {
          'tu': o = traje_jason;
          'mi': o = tu_traje;
          'el': if (PruebaDeAlcance(traje_jason, jason)) {
                  o = traje_jason;
                } else {
                  o = tu_traje;
                }
          default:
            switch (a) {
              ##Coger:     return -'coge';
              ##Sacar:     return -'saca';
              ##Dejar:     return -'deja';
              ##Vestir:    return -'ponte';
              ##Desvestir: return -'quitate';
              ##Dar:       return -'da';
              ##Disparar:  return -'dispara';
              ##Encender:  return -'enciende';
              ##Apagar:    return -'apaga';
              ##Empujar:   return -'empuja';
              ##Entrar:    return -'entra';
              ##Meterse:   return -'metete';
              ##Salir:     return -'sal';
              ##Salirse:   return -'salte';
              null:        rfalse;
            }
        }

        accion = a;
        
        if (f == 1) {
          uno = o;
          otro = objjugador;
        } else {
          uno = o;
        }
        rtrue;
      } else {
        rfalse;
      }
    ],
    antes [;        
      Coger:
        if (self in vaina) {
          <<Sacar self vaina>>;
        }

      Sacar:
        if (otro == nothing) {
          otro = vaina;
        }
        if (otro ~= vaina) {
          "¿De dónde? Pero si Jason no está ahí...";
        }
        if (jason notin vaina) {
          "No hay que sacar a Jason de ningún sitio.";
        }
        if (vaina hasnt general && vaina hasnt abierta) {
          print "Te aproximas a observar la vaina más de cerca...^";
          <<Examinar vaina>>;
        }
        ! Sacar a Jason de la vaina
        if (vaina hasnt abierta) {                        ! Si la vaina no está abierta
          ImprimirOEjecutar(vaina, dejar_salir);          ! te dice que está demasiado dura
          rtrue;                                          ! y sale.
        } else {                                          ! Si la vaina está abierta:
          move jason to hueco_techo;                    ! lo sacas
          PNJ_Ruta(jason, MOVIMIENTO_PERSEGUIR, jugador);
          move traje_inutil to hueco_techo;
          max_longitud_camino = 30;
          alien.pnj_ha_llegado = alien.pnj_ha_llegado_puerta;
          sacador_de_aliens.meter_alien();
          PNJ_Ruta(alien, MOVIMIENTO_POR_META, entrada, CAMINO_SIN_PUERTAS);
          alien.comportamiento = 2;
          self.hablar = false;
          ArrancarReloj(self, 7);
          print "Sacas a Jason de la vaina. Parece inconsciente, y su dañado traje ha
                 perdido prácticamente toda su reserva de oxígeno. Inmediatamente le
                 retiras su casco y el resto del traje atmosférico, y tras unos
                 instantes de aturdimiento, se despierta algo mareado.^";
          HablaJason("Me duele la garganta... Pero estoy bien...
                      me recuperaré... Gracias por venir a rescatarme...");
          if (detector_movimiento hasnt encendido) {
            give detector_movimiento encendido;
            print "^Inmediatamente a continuación, echa un vistazo a su rifle y
                   exclama:^";
            HablaJason("¿Ha llegado hasta aquí con el detector de movimiento
                        desactivado? Dios santo...¡Activelo!");
            "^No es necesario pensar mucho para ver que tiene razón, así que le haces
             caso.";
          }
          rtrue;
        }

      Atacar, Disparar, Empujar, EmpujarDir, Tirar:
        if (accion == ##Disparar && rifle notin jugador) {
          rfalse;
        }
        HablaJason("Conozco ciento cincuenta formas de matar a un hombre. Sólo necesito
                    la mitad de una de ellas para acabar con usted.");
        rtrue;
    ],
!    precamino RUTA_NINGUNA,    ! En realidad no sé si hace falta
    tras_abrir NO_PUEDE_ABRIR,   ! Comportamiento después de pasar por una puerta
    vida [ tema;
      Preguntar:
        self.hablar = false;

        ! Si está en la vaina, permanece en semi-inconsciencia:
        if (self in vaina) {
          "Sólo oyes un débil sonido de respiración...";
        }

        tema = AveriguarTema(TemasJason);
        switch (tema) {
          tema_jason_donde_estas:
            if (parent(self) == LugarReal()) {
              HablaJason("Con usted, amigo... Qué remedio...");
            } else if (EsExterior(parent(self))) {
              HablaJason("Bajo una lluvia de mil demonios.");
            } else {
              switch(random(2)) {
                1: HablaJason("No tengo ni idea, pero le aseguro que no es un lugar
                               agradable.");
                2: HablaJason("En cualquier sitio menos donde quisiera estar.");
              }
            }
          tema_jason_alien:
            if (alien.primera_vez) {
              HablaJason("¿A qué se refiere? No sé de qué me está hablando...");
            } else {
              HablaJason("Me desmayé casi inmediatamente, nada más sentir el tacto de
                          ese bicho monstruoso... Pero no dejo de recordar que le vacié
                          el cargador entero de mi rifle, y aún así siguió avanzando...
                          No sé mucho más que usted. Pero sí sé que es el hijo de puta
                          más peligroso que he visto en mi vida.");
            }
          tema_jason_colonia:
            HablaJason("Los colonos deben vivir en condiciones infrahumanas para
                        satisfacer las ~necesidades~ de la Compañía. Dígame usted
                        si estar encerrado aquí es ~vivir~...");
          tema_jason_comunicador:
            HablaJason("Me permite no perder el contacto con usted... pero que eso sea o
                        no una ventaja, aún no lo tengo muy claro.");
          tema_jason_detector:
            HablaJason("Es un detector de movimiento, y sirve para saber si tenemos
                        compañía.");
          tema_jason_gente:
            HablaJason("No hay nadie... Nadie... Por ninguna parte... ¿Cómo es posible?");
          tema_jason_jason:
            HablaJason("Prefiero no hablar mucho de mí. Cuando intimas con alguien,
                        luego es más duro saber que ha muerto.");
          tema_jason_madre:
            HablaJason("Tanta inteligencia artificial y tantos buenos modales me pone
                        de los nervios.");
          tema_jason_mision:
            HablaJason("Tan sólo me limito a obedecer ordenes. Pero si quiere saber mi
                        opinión, hoy me he puesto mis calzoncillos de la suerte.");
          tema_jason_planeta:
            HablaJason("No es donde vendría a pasar mis vacaciones, desde luego...");
          tema_jason_rifle:
            if (rifle in jason) {
              HablaJason("Ésta es sin duda mi mejor amiga... ¡Jajajaja...!");
            } else {
              HablaJason("Ya no la quiero... No me ha servido de nada...");
            }
          tema_jason_robot:
            if (panel_control.codigo_viejo == 0) {
              HablaJason("Nunca me han gustado esos aparatos andantes. No son de fiar.
                          Nadie lo es... Especialmente ellos.");
            } else {
              HablaJason("Para lo único que ha servido hasta ahora es para darnos un
                          código falso.");
            }
          tema_jason_vaina:
            if (jason.sacado()) {
              HablaJason("No quiero hablar de ello... Estar allí dentro ha sido la
                          experiencia más desagradable de mi vida... Estaba como ido,
                          pero a veces me daba cuenta de dónde me encontraba... allí
                          metido, sin poder moverme, casi sin recordar quién era ni
                          qué hacía allí... Y esa oscuridad...");
            } else {
              HablaJason("¿De qué demonios me está hablando?");
            }
          default:
            HablaJason("No entiendo qué quiere decirme.");
        }
        rtrue;

      Dar:
        if (uno in jugador) {
          move uno to self;
          print "Le das ", (el) uno, " a Jason.^";
          HablaJason("Muy amable...");
          rtrue;
        }

      default:
        switch (random(2)) {
          1: HablaJason("No entiendo qué quiere decirme.");
          2: HablaJason("¿Perdone?");
        }
        rtrue;
    ],
    intentar_coger [ o;
      if (o == 0) {
        o = uno;
      }
      
      if (~~(PuedeVer(self, o))) {
        HablaJason("No puedo ver eso que dice.");
        rfalse;
      }

      if (~~(PNJCoger(self, o))) {
        switch (RazonErrorPNJ) {
          PNJ_INTENTA_COGERSE_A_SI_MISMO:
            HablaJason("Yo ya me tengo a mí mismo, gracias...");
          PNJ_OBJETO_NO_PRESENTE:
           HablaJason("No puedo ver ", o, " por aquí.");
          PNJ_EN_RECIPIENTE_CERRADO:
            HablaJason("Puedo ver ", o, "pero no puedo cogerl", o,
                        " porque estoy aquí metido.");
          PNJ_INTENTA_COGER_AL_JUGADOR:
            HablaJason("No es usted mi tipo...");
          PNJ_INTENTA_COGER_CRIATURA:
            if (uno in jugador) {
              move uno to self;
              print "Le das ", (el) o, " a Jason.^";
            }
          PNJ_OBJETO_ESCENARIO_O_ESTATICO:
            HablaJason("No puedo coger eso.");
          PNJ_INTENTA_COGER_SUBOBJETO:
            print "Jason intenta coger ", (el) o, " pero no puede porque forma parte",
                  (del) parent(o), ".";
          PNJ_OBJETO_INACCESIBLE:
            HablaJason("No puedo coger ", o, ". No está a mi alcance.");
          PNJ_OBJETO_EN_RECIPIENTE_CERRADO:
            HablaJason("No puedo coger ", o, ". Está metido dentro ", 0, 0,
                        parent(o), ".");
        }
        rfalse;
      }
      rtrue;
    ],
!    antesPNJ [;
!      Ir:
!        HablaJason("¡No quiero moverme, jo!");
!        rtrue;
!      Coger:
!        HablaJason("¡No quiero coger nada, jo!");
!        rtrue;
!    ],
    orden_entrar [;
      if (self in exterior1) {
        if (puerta_nave has abierta) {
          move self to nave1;
          puerta_nave.cerrar();
          if (jugador in nave1 or nave2) {
            PNJ_Ruta(jason, MOVIMIENTO_PERSEGUIR, jugador);
          } else {
            PNJ_Ruta(self, MOVIMIENTO_NINGUNO);
          }
          "Jason entra en la nave. A continuación, Madre cierra la compuerta exterior.";
        } else {
          HablaJason("La compuerta de la nave está cerrada.");
          rfalse;
        }
      }

      self.orden_ir(obj_adentro);
      rtrue;
    ],
    orden_salir [;
      if (self in nave1 or nave2) {
        if (puerta_nave hasnt abierta) {
          HablaJason("La compuerta de la nave está cerrada.");
          rfalse;
        }
        if (~~(procesador.arreglado())) {
          if (traje_jason notin jason) {
            if (PuedeVer(jason, traje_jason)) {
              HablaJason("De acuerdo, pero antes cogeré el traje atmosférico.");
              if (~~(self.intentar_coger(traje_jason))) {
                rfalse;
              }
              give traje_jason puesto;
              print "^";
            } else {
              HablaJason("No puedo salir de la nave. No llevo puesto mi traje, y
                          no lo veo por aquí.");
              rfalse;
            }
          } else {
            if (traje_jason hasnt puesto) {
              give traje_jason puesto;
              HablaJason("De acuerdo, pero antes me pondré el traje atmosférico.");
              print "^";
            }
          }          
        }
        move self to exterior1;
        puerta_nave.cerrar();
        PNJ_Ruta(self, MOVIMIENTO_NINGUNO);
        "Jason sale al exterior protegido por su traje espacial. A continuación,
         Madre cierra la compuerta de la nave.";
      }
       
      self.orden_ir(obj_afuera);
      rtrue;
    ],
    orden_ir [ d;
      if (~~(PNJIr(self, d))) {
        switch (RazonErrorPNJ) {
          PNJ_SIN_SALIDA:
            HablaJason("No puedo ir por ahí.");
          PNJ_PUERTA_CERRADA:
            HablaJason("La puerta está cerrada.");
          PNJ_PUERTA_BLOQUEADA:
            HablaJason("No puedo pasar por la puerta.");
        }
      } else {
        HablaJason("Ya estoy.");
        if (self.tipo_de_movimiento == MOVIMIENTO_PERSEGUIR) {
          if (self.perseguido == jugador) {
            PNJ_Ruta(self, MOVIMIENTO_NINGUNO);
            HablaJason("A partir de ahora dejaré de seguirle.");
          } else {
            HablaJason("Ahora mismo estoy siguiendo a ", self.perseguido, ".");
            rtrue;
          }
        }
      }
      rtrue;    
    ],
    ordenes [ tmp;
      self.hablar = false;

      ! Si está en la vaina, permanece en semi-inconsciencia:
      if (self in vaina) {
        "Sólo oyes un débil sonido de respiración...";
      }

      Si:
        HablaJason("¿Sí qué?");
        rtrue;

      No:
        HablaJason("Supongo que los jefes están para decir que no...");
        rtrue;

      Ir:        
        if (uno == obj_adentro) {
          self.orden_entrar();
          rtrue;
        }
        
        if (uno == obj_afuera) {
          self.orden_salir();
          rtrue;
        }
        
        if (self.tipo_de_movimiento == MOVIMIENTO_PERSEGUIR && 
            self.perseguido ~= jugador) {
          HablaJason("Ahora mismo estoy siguiendo a ", self.perseguido, ".");
          rtrue;
        }

        if (alien.comportamiento == 3 ||
            (alien.comportamiento == 1 &&
             alien.tipo_de_movimiento == MOVIMIENTO_PERSEGUIR &&
             alien.perseguido == jugador)) {
          if (jason in LugarReal()) {
            HablaJason("¡Ahora no es un buen momento! ¡Ese bicho nos acecha!");
          } else {
            HablaJason("¿Para que me persiga a mí también? ¡Ni hablar!");
          }
          rtrue;
        }
        
        self.orden_ir(uno);
        rtrue;

      Entrar, Meterse:
        if (uno == montacargas && self in balcon4 or balcon12) {
          if (parent(jason) ~= LugarReal()) {
            HablaJason("Será mejor que lo hagamos juntos.");
            rtrue;
          } else {
            <<Meterse montacargas>>;
          }
        }

        self.orden_entrar();
        rtrue;

      Salir, Salirse:
        self.orden_salir();
        rtrue;                

      Subir:
        if (montacargas in parent(self)) {
          if (parent(jason) ~= LugarReal()) {
            HablaJason("Será mejor que lo hagamos juntos.");
            rtrue;
          } else {
            <<Ir obj_arriba>>;
          }
        }

        self.orden_ir(obj_arriba);
        rtrue;

      Bajar:
        if (montacargas in parent(self)) {
          if (parent(jason) ~= LugarReal()) {
            HablaJason("Será mejor que lo hagamos juntos.");
            rtrue;
          } else {
            <<Ir obj_abajo>>;
          }
        }

        self.orden_ir(obj_abajo);
        rtrue;
                
      Tacos, Soso:
        print "Jason te mira con cara de pocos amigos, y más que decir, escupe:^";
        HablaJason("Está jugando con fuego...");
        rtrue;

      Coger, Sacar:        
        if (uno in self) {
          HablaJason("Ya tenía ", uno, ".");
          rtrue;
        }
        if (uno == comunicador) {
          "Mejor no. Si le das el comunicador, perderías el contacto con él y con
           la nave.";
        }

        self.intentar_coger();
        rtrue;
        
      Dejar:
        if (uno == rifle && uno in self) {
          HablaJason("Ni hablar. No pienso despegarme de mi arma.");
          rtrue;
        } else if (uno == detector_movimiento && rifle in self) {
          HablaJason("Para eso tendría que dejar mi rifle, y no pienso hacerlo.");
          rtrue;
        } else if (uno notin self) {
          HablaJason("No llevo eso encima.");
          rtrue;
        }
        
        PNJDejar(self, uno);
        rtrue;
        
      Vestir:
        if (~~(uno ofclass TrajeAtmosferico)) {
          HablaJason("No puedo ponerme eso.");
          rtrue;
        }

        if (uno ~= traje_jason) {
          HablaJason("Lo siento; no es de mi talla.");
          rtrue;
        }    

        if (uno notin self) {
          if (~~(PuedeVer(uno, self))) {
            HablaJason("No veo mi traje por aquí.");
            rtrue;
          }
          if (uno in jugador) {
            print "(Primero se lo das)^";
            move uno to jason;
          } else {
            print "(Primero intenta cogerlo)^";
            self.no_mostrar_despues = true;
            tmp = self.intentar_coger();
            self.no_mostrar_despues = false;
            if (~~tmp) {
              rtrue;
            }
          }
        } else {
          if (uno has puesto) {
            "Jason ya lleva puesto su traje.";
          }
        }
        give uno puesto;
        HablaJason("Ya está. Me he puesto ", uno, ".");
        rtrue;        

      Desvestir:
        if (uno in self && uno has puesto) {
          if (uno == traje_jason or tu_traje && jason notin nave1 or nave2 &&
              circuito hasnt general) {
            HablaJason("No quiero morir envenenado, señor...");
            rtrue;
          }
          give uno ~puesto;
          HablaJason("Ya está. Me he quitado ", uno, ".");
          rtrue;
        } else {
          HablaJason("No puedo quitarme eso. No lo llevo puesto.");
          rtrue;
        }
        
      Dar:
        if (uno == nothing) {
          HablaJason("No entiendo qué quiere que le dé.");
          rtrue;
        }

        if (uno == rifle && uno in self) {
          HablaJason("Ni hablar. No pienso despegarme de mi arma.");
          rtrue;
        } else if (uno == detector_movimiento && rifle in self) {
          HablaJason("Para eso tendría que darle mi rifle, y no pienso hacerlo.");
          rtrue;
        }

        ! Si le pides que te dé algo, te lo da
        if (uno in self) {
          if (uno has puesto) {
            give uno ~puesto;      ! Se lo quita, por si acaso lo tenía puesto
          }
          move uno to jugador;
          if (uno == tu_traje) {
            HablaJason("Tome, aquí tiene su traje.");
          } else {
            HablaJason("Tome, aquí tiene ", uno, ".");
          }
          rtrue;
        } else {
          HablaJason("No tengo eso.");
          rtrue;
        }
        
      Encender:
        if (uno in self || (uno == detector_movimiento && parent(uno) in self)) {
          if (uno hasnt encendido) {
            if (uno == linterna) {
              linterna.encender();
            } else {
              give uno encendido;
            }
            HablaJason("Ya está. He encendido ", uno, ".");
            rtrue;
          } else {
            HablaJason("Me temo que ", uno, " ya está encendid", uno, ".");
            rtrue;
          }
        } else {
          HablaJason("No llevo ", uno, " encima para poder encenderl", uno, ".");
          rtrue;
        }
        
      Apagar:
        if (uno in self || (uno == detector_movimiento && parent(uno) in self)) {
          if (uno has encendido) {
            give uno ~encendido;
            HablaJason("Ya está. He apagado ", uno, ".");
            rtrue;
          } else {
            HablaJason("Me temo que ", uno, " ya está apagado.");
            rtrue;
          }
        } else {
          HablaJason("No llevo ", uno, " encima para poder apagarlo.");
          rtrue;
        }
        
      Inv:
        ! Si le pides inventario de lo que lleva encima
        if (children(self) == 0) {
          HablaJason("No llevo nada encima.");
          rtrue;
        } else {
          style bold;
          print "^JASON: ";
          style roman;
          print "~Llevo ";
          EscribirListaDesde(child(self), INFOTOTAL_BIT + ESPANOL_BIT + RECURSIVO_BIT);
          "~.";
        }
              
      Venir, Seguir:
        if (uno == nothing) {
          uno = jugador;
        }
        if (uno ~= jugador) {
          HablaJason("No entiendo a quién quiere que siga.");
          rtrue;
        }
        if (jugador in nave1 or nave2 && self in exterior1 && puerta_nave hasnt abierta) {
          HablaJason("La compuerta de la nave está cerrada.");
          rtrue;
        }
        if (self.tipo_de_movimiento == MOVIMIENTO_PERSEGUIR &&
            self.perseguido == jugador) {
          if (nave2 hasnt visitado && exterior1 hasnt visitado) {
            ! Si no nos hemos movido de la primera localidad, Jason
            ! dice otra cosa:
            HablaJason("Voy pisándole los talones.");
          } else {
            switch (random(2)) {
              1: HablaJason("¡Pero si voy detrás suya todo el rato!");
              2: HablaJason("Voy pisándole los talones.");
            }
          }
          rtrue;
        }

        if (jason in nave1 or nave2 && LugarReal() ~= nave1 && LugarReal() ~= nave2) {
!          print "^";
          if (~~(self.orden_salir())) {
            rtrue;
          }
        }

        HablaJason("De acuerdo. Le seguiré.");        
        PNJ_Ruta(jason, MOVIMIENTO_PERSEGUIR, jugador);
        rtrue;
        
      Quedar, Esperar:
        if (alien.comportamiento == 4 ||
            alien.comportamiento == 3 && (puerta_emergencia has abierta ||
            (puerta_emergencia hasnt abierta && (parent(jason) ofclass ZonaEmergencia &&
            parent(alien) ofclass ZonaEmergencia) ||
            (~~(parent(jason) ofclass ZonaEmergencia) &&
            (~~(parent(alien) ofclass ZonaEmergencia)))))) {
          HablaJason("¡No pienso quedarme quieto con esa cosa acechando!");
          rtrue;
        }
        
        if (self.tipo_de_movimiento == MOVIMIENTO_NINGUNO) {
          HablaJason("Ya estoy más quieto que un muerto.");
        } else {
          PNJ_Ruta(jason, MOVIMIENTO_NINGUNO);
          HablaJason("De acuerdo. Me quedaré aquí.");
        }
        rtrue;
              
      Saludar:
        HablaJason("Será mejor no perder el tiempo en protocolos y centrarse en la
                    misión.");
        rtrue;
      
      Disparar:
        if (rifle in self) {
          if (uno ~= nothing) {
            "Jason dispara su arma contra ", (el) uno, ".";
          } else {
            "Jason dispara su arma a un punto cualquiera.";
          }
        } else {
          HablaJason("¡No tengo nada con lo que disparar!");
          rtrue;
        }
      
      Encender:
        if (uno == detector_movimiento) {
          if (rifle in jason) {
            if (detector_movimiento hasnt encendido) {
              give detector_movimiento encendido;
              HablaJason("Ya está. He activado el detector de movimiento.");
              rtrue;
            } else {
              HablaJason("Ya llevo encendido el detector de movimiento.");
              rtrue;
            }
          } else {
            HablaJason("No puedo activarlo, porque no llevo mi rifle encima.");
            rtrue;
          }
        } else {
          HablaJason("Mejor enciéndalo usted. Usted es el cerebrito, ¿no?");
          rtrue;
        }

      Apagar:
        if (uno == detector_movimiento) {
          if (rifle in jason) {
            if (detector_movimiento has encendido) {
              give detector_movimiento ~encendido;
              HablaJason("Ya está. He desactivado el detector de movimiento.");
              rtrue;
            } else {
              HablaJason("Ya llevo desactivado el detector de movimiento.");
              rtrue;
            }
          } else {
            HablaJason("No puedo desactivarlo, porque no llevo mi rifle encima.");
            rtrue;
          }
        } else {
          HablaJason("Yo no sé hacerlo. Hágalo usted mismo.");
          rtrue;
        }
      
      Empujar:
        if (uno == boton_este or boton_oeste && PruebaDeAlcance(uno, self)) {
          if (LugarReal() ~= parent(uno)) {
            if (puerta_emergencia has abierta) {
              HablaJason("Lo he pulsado, pero parece que no hace nada...");
              rtrue;
            } else {
              uno.pulsar();
              rtrue;
            }
          } else {
            HablaJason("¿Por qué no lo hace usted mismo, ya que está aquí?");
            rtrue;
          }
        } else {
          HablaJason("¿Para qué se supone que tengo que hacer eso?");
          rtrue;
        }

      Ayudar:
        if (alien.comportamiento == 0) {
          ! Estamos en la primera parte del juego
          if (LugarReal() == nave1 or nave2) {
            HablaJason("Deberíamos salir de la nave.");
          } else {
            HablaJason("Tenemos que entrar en el complejo de la colonia minera.");
          }
        } else {
          ! El alien ya se ha llevado a Jason y Has sacado a Jason de la vaina
          if (entrada hasnt general) {
            ! Aún no sabes que la puerta está sellada
            HablaJason("Tenemos que salir de aquí cagando leches.");
          } else {
            ! Sabes que la puerta está sellada
            HablaJason("Tenemos que buscar otra salida.");
          }
        }
        rtrue;
        
      NoComprendido, Hablar:
        <<Preguntar self>>;
!        accion = ##Preguntar;
!        rfalse;

      default:
        HablaJason("Mejor hágalo usted mismo...");
        rtrue;
    ],
    no_mostrar_despues false,
    despuesPNJ [;
      Coger:
        if (~~(self.no_mostrar_despues)) {
          if (uno == tu_traje) {
            HablaJason("Ya está. He recogido su traje.");
          } else {
            HablaJason("Ya está. He recogido ", uno, ".");
          }
        }
        rtrue;

      Dejar:
        if (uno == tu_traje) {
          HablaJason("Ya está. He dejado su traje.");
        } else {
          HablaJason("Ya está. He dejado ", uno, ".");
        }
        rtrue;
    ],
    cada_turno [;
      
      if (banderafin ~= 0) {
        rtrue;
      }
      
      if (~~(self.hablar)) {
        self.hablar = true;
        rtrue;
      }
      
      ! El alien va corriendo hacia nosotros:
      if ((alien.comportamiento == 3 &&
           ~~(puerta_emergencia hasnt abierta &&
              LugarReal() ofclass ZonaEmergencia &&
              parent(jason) ofclass ZonaEmergencia)) ||
          (alien.comportamiento == 1 &&
           alien.tipo_de_movimiento == MOVIMIENTO_PERSEGUIR &&
           alien.perseguido == jugador && jason in LugarReal())) {
        if (random(100) < 50) {
          switch (random(3)) {
            1: if (alien notin LugarReal()) {
                 HablaJason("¡Se está acercando! ¡Se está acercando!");
               } else {
                 HablaJason("¡Está aquí! ¡Está aquí! ¡Huyamos!");
               }
            2: HablaJason("¡Corra, Dios mío! ¡Viene a por nosotros!");
            3: HablaJason("¡Huyamos!");
          }
        }
        rtrue;
      }

      if (alien.comportamiento == 4) {
        if (random(100) < 60) {
          switch (random(2)) {
            1: if (EsExterior(LugarReal())) {
                 HablaJason("¡Vamos a la nave, deprisa!");
               } else {
                 HablaJason("¡Vamos a la salida de emergencia!");
               }
            2: HablaJason("¡Rápido, salgamos de aquí!");
          }
        }
        rtrue;
      }
      
      ! Situaciones "normales":
      if (alien.comportamiento == 0 or 1 or 2) {     
        if (self notin vaina) {
          if (random(100) < 20) {
            if (self in LugarReal()) {
              if (~~(self.sacado())) {
                if (SeVen(robot, jason)) {
                  switch (random(3)) {
                    1: HablaJason("Me parece que este robot no anda muy bien de
                                   la sesera.");
                       rtrue;
                    2: HablaJason("A este robot le falta un tornillo, y nunca mejor
                                   dicho.");
                       rtrue;
                    3: switch (random(2)) {
                         1: print "^Jason da una sonora patada al cuerpo del robot y
                                   dice:^";
                         2: print "^Jason mira con evidente antipatía al robot y dice:^";
                       }
                       if (robot.ha_hablado) {
                         HablaJason("¡Este trasto de lata está como una cabra! ¡Sólo
                                     dice tonterías sin sentido!");
                       } else {
                         HablaRobot("Menudo montón de chatarra...");
                       }
                       rtrue;
                  }
                }
                switch (random(9)) {
                  1: HablaJason("¡Estoy deseando entrar en acción!");
                  2: HablaJason("Este lugar me da escalofríos...");
                  3: HablaJason("En el espacio sólo puedes fiarte de ti mismo.");
                  4: switch (random(2)) {
                       1: if (traje_jason in jason && traje_jason has puesto) {
                            HablaJason("Todo esto es muy extraño... Muy, muy extraño...");
                          } else {
                            print "^Con una rapidez inusitada, Jason produce un gorgojeante
                                   sonido en su boca y garganta, y dispara un enorme
                                   escupitajo al suelo.^";
                            switch (random(2)) {
                              1: HablaJason("¿Por qué me mira de ese modo? ¿Me va a
                                             regañar?"); PreguntaSiNo = 1;
                              2: HablaJason("Tengo mucha saliva, ¿vale?"); PreguntaSiNo = 1;
                            }
                          }
                       2: HablaJason("No le podrá la tensión, ¿verdad, señor?");
                          PreguntaSiNo = 1;
                     }
                  5: if (EsExterior(LugarReal())) {
                       HablaJason("Aquí sólo hay barro y desolación... ¿Qué coño hacemos
                                   en este lugar?");
                     } else {
                       HablaJason("¿Qué hacemos aquí todavía? ¡La acción está ahí
                                   fuera!");
                     }
                  6: switch (random(2)) {
                       1: print "^Jason acaricia su arma con una expresión casi
                                 obscena.^";
                       2: print "^Jason sopesa su arma y apunta con ella al azar.^";
                     }
                     switch (random(2)) {
                       1: HablaJason("Hay que cuidarla, ¿sabe? Y así ella también cuidará
                                      de uno.");
                       2: HablaJason("No encontrará un aliado mejor en momentos como
                                      este.");
                     }
                  7: HablaJason("Acabemos de una vez con esto y larguémonos de aquí.");
                  8: if (jason notin nave1 && jason notin nave2) {
                       print "^Jason se lleva la mano al oído y dice:^";
                     }
                     HablaJason("¿Sabes, nena? Tienes una voz muy dulce... No tendrás
                                 por casualidad una hermana soltera, ¿verdad?");
                     HablaMadre("Mi pariente más cercano es un procesador de cámaras
                                 frigoríficas. No creo que sea su tipo, Mayor.");
                     "^Jason se echa a reir a carcajadas.";
                  9: if (LugarReal() == nave1 or nave2) {
                       print "^Jason toca una pared de la nave y, dirigiendo la mirada a
                              ningún sitio en particular, dice:^";
                       HablaJason("Madre: ¿nunca sientes necesidad de...? Venga, ya
                                   sabes...");
                       HablaMadre("No, lo lamento.");
                       "^Jason se encoge de hombros.";
                     } else {
                       print "^Jason se lleva la mano al oído y dice:^";
                       HablaJason("Sigues ahí, ¿verdad, Madre? No te vayas sin nosotros,
                                   ¿eh?");
                       HablaMadre("No se preocupe, Mayor. Mis directrices me lo impiden
                                   por más que lo deseara.");
                       "^Se ve que se llevan maravillosamente.";
                     }
                }
              } else {
                ! Ya lo hemos sacado de la vaina:
                switch (random(4)) {
                  1: HablaJason("No me encuentro muy bien...");
                  2: HablaJason("Vámonos ya de aquí, por favor...");
                  3: HablaJason("Por favor, ayúdeme a llegar a la nave...");
                  4: HablaJason("Santo Cielo... ¿A qué nos enfrentamos?");
                }
              }
            } else {
              ! Jason no está contigo:
              switch (random(5)) {
                1: HablaJason("¡No me deje aquí!");
                2: HablaJason("¿Qué hago yo mientras tanto?");
                3: HablaJason("¿Qué se supone que tengo que hacer aquí?");
                4: HablaJason("¿Por qué me deja aquí?");
                5: HablaJason("¿Hola? ¿Está ahí?"); PreguntaSiNo = 1;
              }
            }
          }
        } else {
          ! Jason está dentro de la vaina, y habla poco y menos:
          if (random(100) < 10) {
            switch (random(4)) {
              1: HablaJason("Aaaaaaahhhh...");
              2: HablaJason("Ayúdemeeeeeeee....");
              3: HablaJason("(lamento ahogado)");
              4: HablaJason("(respiración entrecortada)");
            }
            if (~~(self.habla_en_vaina)) {
              self.habla_en_vaina = true;
              style bold;
              print "^¡ES JASON! ¡ESTÁ VIVO!^";
              style roman;
              rtrue;
            }
          }
        }
      }
    ],
    habla_en_vaina false,
    tiempo_restante,
    tiempo_agotado [ o p sig;
      p = parent(self);
      ! Se le cae todo antes de quedar inconsciente:
      o = child(self);
      while (o ~= Nothing) {
        sig = sibling(o);
        move o to p;
        o = sig;
      }
      ! objectloop (o) {
      !   if (o in jason) move o to p;
      ! }
      remove self;
      give self ausente;
      move jason_inerte to p;
      if (p == LugarReal()) {
        "^De repente, ves que Jason cae al suelo inconsciente. Rápidamente, te agachas
         a ver cómo se encuentra... y le notas pulso. Al menos está vivo, pero por
         mucho que lo zarandeas, no hay manera de despertarlo. Tendrás que buscar algo
         que te ayude a sacarlo de su estado de inconsciencia.";
      } else {
        "^Oyes por el comunicador que algo grande cae al suelo con un golpe seco.";
      }
    ],
  has
    animado propio transparente;  ! Hay que comprobar si funciona bien sin el transparente


Object jason_inerte "Jason"
  with
    nombre 'jason' 'mayor',
    descripcion "Está inconsciente en el suelo.",
    describir "^Jason yace en el suelo, inconsciente.",
    antes [;
      Hablar, Responder, Decir, Preguntar, Saludar:
        "No va a responderte. Está inconsciente.";
      Coger, Empujar, Tirar:
        "Imposible. Pesa demasiado.";
      DespertarOtro:
        "No reacciona.";
      default:
        "Está inconsciente. Tienes que intentar reanimarlo.";
    ],
  has
    animado propio;


Object computadora "Madre"
  with
    nombre 'madre',
    adjetivos 'nave' 'computadora' 'computador' 'ordenador',
    descripcion [;
      print "Madre es la computadora de la nave. Es lo último y más sofisticado en
             inteligencia artificial, aunque Jason prefiere el término ~estupidez
             artificial~. Gobierna el funcionamiento interno de todos los sistemas
             como lo haría el cerebro de un gigantesco organismo. ";
      if (LugarReal() == nave1 or nave2) {
        "No se encuentra en un lugar específico; en realidad, puedes sentir
         su presencia en todas las estancias del vehículo.";
      } else {
        "Se encuentra en la nave... o mejor dicho, ES la nave.";
      }
    ],
!    esta_en nave1 nave2,
    esta_en [; rtrue; ],                   ! Está en todas partes
    genero G_FEMENINO,
    hablar true,
    gramatica [;
      return -'madre,';
    ],
    ! codigo_nuevo == 0: indica que Madre todavía no ha calculado el nuevo código
    ! codigo_nuevo == 1: sí lo ha calculado
    codigo_nuevo 0,
    vida [ tema;
      Preguntar:
        self.hablar = false;
        tema = AveriguarTema(TemasComputadora);
        switch (tema) {
          tema_madre_alien:
            if (procesador.arreglado()) {
              HablaMadre("No puedo darle información. No tenía constancia de la
                          existencia de un ser de esas características.");
            } else {
              HablaMadre("No sé a qué clase de criatura se refiere, señor.");
            }
          tema_madre_arreglar:
            if (procesador.arreglado()) {
              HablaMadre("El procesador atmosférico ya está completamente arreglado.");
            } else {
              HablaMadre("Detectado un fallo en uno de los circuitos principales del
                          procesador atmosférico. Probabilidad de conexión rota por
                          sobrecalentamiento: 97%. Su reparación requiere soldar el
                          circuito.");
            }
          tema_madre_ascensor:
            HablaMadre("El ascensor de emergencia se encuentra situado en el nivel -2
                        del complejo. Sólo se abre de forma automática en caso de
                        activarse el protocolo de emergencia.");
          tema_madre_atmosfera:
            if (circuito hasnt general) {
              HablaMadre("La atmósfera natural del planeta es irrespirable, debido
                          a una alta concentración de dióxido de azufre en
                          suspensión. Se requiere la transformación artificial del
                          aire por medio de un procesador atmosférico.");
            } else {
              HablaMadre("La atmósfera del planeta está siendo transformada
                          artificialmente por el procesador atmosférico para hacerla
                          respirable. Es posible salir al exterior sin equipo
                          de protección.");
            }
          tema_madre_ayuda:
            HablaMadre("Simplemente indíqueme qué información necesita o qué
                        órdenes debo cumplir.");
          tema_madre_cajafuerte:
            HablaMadre("No puedo acceder al computador del complejo para consultar la
                        combinación de la caja fuerte.");
          tema_madre_cargador:
            HablaMadre("Para recargar las baterías puede usarse el cargador situado en
                        el panel de instrumentos de la nave.");
          tema_madre_circuito:
            if (PruebaDeAlcance(circuito, jugador)) {
              if (circuito hasnt general) {
                HablaMadre("La única forma de arreglar una conexión en un circuito
                            fotónico es soldar las conexiones que tenga abiertas, y
                            para ello se requiere un soldador industrial controlado
                            por un autómata.");
              } else {
                HablaMadre("El circuito fotónico ya ha sido soldado y funciona dentro
                            de sus parámetros operativos.");
              }
            } else {
              HablaMadre("No sé a qué circuito o conexión se refiere, señor.");
            }
          tema_madre_codigo:
            if (panel_control.codigo_viejo == 0) {
              ! El jugador todavía no ha intentado meter el código viejo
              HablaMadre("El código de acceso de la puerta al procesador atmosférico,
                          según indican los informes que me fueron grabados antes de
                          partir, es el 759812.");
            } else {
              ! El jugador ha intentado meter el código viejo, y no le funciona
              if (self.codigo_nuevo == 0) {
                HablaMadre("Al parecer, el control de la puerta se ha reiniciado,
                            haciendo que el código de acceso sea otro distinto. Hay
                            que calcular el nuevo código a partir de su semilla.");
              } else {
                ! Ya se ha calculado el nuevo código a partir de la semilla:
                HablaMadre("El nuevo código de acceso al procesador atmosférico es el
                            268741.");
              }
            }
          tema_madre_complejo:
            HablaMadre("El complejo minero rodea la veta de Vibrantium, y dispone de todo
                        lo necesario para hacer factible la vida en el planeta.");
          tema_madre_compuerta:
            HablaMadre("La compuerta de la nave está bloqueada por motivos de seguridad.
                        Si desea que la abra, sólo tiene que indicármelo.");
          tema_madre_energia:
            if (circuito hasnt general) {
              HablaMadre("El suministro de energía ha sido interrumpido debido a un
                          fallo en el sistema de control del procesador atmosférico.");
            } else {
              HablaMadre("La energía del complejo se halla restablecida al 100%.");
            }
          tema_madre_habitats:
            HablaMadre("La zona oeste del complejo está dedicada a los hábitats de los
                        colonos. Son los recintos destinados a dormitorio y aseo. Sólo
                        un colono puede acceder a ellos. En todo caso, con el computador
                        central dañado será imposible abrir las puertas.");
          tema_madre_holocubo:
            HablaMadre("El holocubo es un soporte de datos muy utilizado por su alta
                        durabilidad y gran capacidad de almacenamiento. Para
                        reproducir su contenido se require de un dispositivo
                        proyector.");
          tema_madre_informe:
            HablaMadre("Todos los sistemas de la nave se encuentran operativos.");
          tema_madre_jason:
            HablaMadre("El Mayor Jason es un marine condecorado con varias medallas al
                        valor en el campo de batalla. El número de misiones en las que
                        ha participado es incalculable. Por otra parte, es el ser
                        humano más maleducado y desagradable con quien he tenido el
                        privilegio de trabajar.");
            if (jason notin vaina && (~~(procesador.arreglado()))) {
              jason.hablar = false;
              switch (random(3)) {
                1: HablaJason("Me pitan los oídos. ¿Habláis de mi?");
                2: HablaJason("Yo también te quiero, saco de circuitos.");
                3: HablaJason("Tú tampoco eres mi tipo, ¿sabes?");
              }
            }
          tema_madre_localizarle:
            if (jason in nave1 or nave2) {
              HablaMadre("El Mayor Jason está dentro del vehículo, señor.");
            } else {
              HablaMadre("Lo lamento, pero no puedo precisar la localización concreta
                          en la que se encuentra el Mayor.");
            }
          tema_madre_localizarme:
            if (LugarReal() == nave1 or nave2) {
              HablaMadre("Se encuentra dentro del vehículo, señor.");
            } else if (LugarReal() == procesador) {
              HablaMadre("Se encuentra dentro del procesador atmosférico.");
            } else if (EsExterior(LugarReal())) {
              HablaMadre("Se encuentra en el exterior, directamente sobre la
                          superficie del planeta.");
            } else {
              switch (montacargas.nivel) {
                0: HablaMadre("Sólo puedo indicarle que se encuentra en el nivel 0
                               del complejo, y que hay dos niveles más hacia abajo.");
                -1: HablaMadre("Se encuentra en el nivel -1 del complejo, y arriba y
                                abajo puede encontrar los niveles 0 y -2,
                                respectivamente. Lamento no poder precisar más.");
                -2: HablaMadre("Se encuentra en el nivel -2, el más profundo del
                                complejo, y hacia arriba se encuentran los niveles 0
                                y -1. No puedo darle más información.");
              }
            }
          tema_madre_madre:
            HablaMadre("Soy la computadora de la nave. Una inteligencia artificial
                        diseñada para llevar a cabo tareas sencillas y razonar de
                        pensamientos profundos.");
          tema_madre_mision:
            HablaMadre("El objetivo de la misión es elaborar un informe completo de la
                        situación producida en la colonia minera, reestablecer las
                        comunicaciones y determinar las causas de lo ocurrido.");
          tema_madre_ordenadores:
            if (entrada has visitado) {
              HablaMadre("El sistema lógico del complejo ha sido dañado y no puedo
                          comunicarme con el computador central del complejo.");
            } else {
              HablaMadre("Yo misma gobierno la nave sin necesidad de computadores
                          periféricos.");
            }
          tema_madre_planeta:
            HablaMadre("Objeto 2003 UB313. Nombre alternativo: Eris. Cuerpo celeste
                        clase V. El mayor plutoide y objeto transneptuniano. Satélite
                        natural: Disnomia. Diámetro: 2.400 km. Órbita: 557 años
                        terrestres. Clasificado como «cuerpo del disco disperso del
                        Cinturón de Kuiper». Ausencia de vida autóctona.");
          tema_madre_procesador:
            if (circuito hasnt general) {
              HablaMadre("El procesador atmosférico es el mecanismo utilizado para
                          transformar artificialmente el aire y hacerlo respirable
                          para el ser humano. Toda instalación colonial enclavada en
                          una atmósfera venenosa requiere de al menos uno. El estado
                          actual del procesador atmosférico es: INOPERANTE.
                          Atmósfera irrespirable.");
            } else {
              HablaMadre("El procesador atmosférico es el mecanismo utilizado para
                          transformar artificialmente el aire y hacerlo respirable
                          para el ser humano. El estado actual del procesador
                          atmosférico es: OPERATIVO. Atmósfera respirable.");
            }
          tema_madre_semilla:
            switch (self.codigo_nuevo) {
              0: HablaMadre("El código de acceso se calcula a partir de la semilla del
                             generador pseudoaleatorio. Indíqueme el valor de la nueva
                             semilla que aparece en el display, para que pueda calcular
                             el código correcto."); new_line;
                 print "[Prueba usando ~"; style bold; print "MADRE, LA SEMILLA ES n";
                 style roman; print "~, sustituyendo ~"; style bold; print "n";
                 style roman; print "~ por el valor de la semilla.]^";
              1: HablaMadre("El código de acceso se calcula a partir de la semilla del
                             generador pseudoaleatorio. A partir de la semilla que me
                             ha indicado, he calculado que el nuevo código de acceso es
                             268741.");
            } 
          tema_madre_salir:
            if (LugarReal() == nave1 or nave2) {
              HablaMadre("Para salir sólo tiene que pedirme que abra la puerta.");
              rtrue;
            }
            if (EsExterior(LugarReal())) {
              HablaMadre("Ya se encuentra en terreno abierto, señor...");
              rtrue;
            }
            if (jugador in procesador) {
              HablaMadre("La puerta ya está abierta, señor. Nada le impide salir al
                          exterior.");
            }
            HablaMadre("La única salida conocida del complejo es la entrada situada
                        al norte del nivel 0. Existe una salida de emergencia que
                        siempre está cerrada salvo en casos excepcionales.");
          tema_madre_trajes:
            if (compartimento_trajes has abierto) {
              HablaMadre("El compartimento de los trajes ya está abierto.");
            } else {
              HablaMadre("Los trajes atmosféricos de protección se encuentran
                          almacenados en un compartimento acoplado a una de las
                          paredes de la cabina.");
            }            
          default:
            HablaMadre("Lo siento. No he podido identificar el sentido de sus palabras.");
            rtrue;
        }
        rtrue;
    ],
    ordenes [;
      self.hablar = false;

      Si:
        HablaMadre("Dígame cómo puedo ayudarle...");
        rtrue;

      No:
        HablaMadre("Como desee...");
        rtrue;

      Semilla:
        if (numero_interpretado == 9957) {
          self.codigo_nuevo = 1;
          HablaMadre("Parece una semilla correcta. El valor generado es 268741. Ese
                      debería ser el nuevo código.");
        } else {
          HablaMadre("Ese número no corresponde a un valor de semilla correcto.");
        }
        rtrue;

      Despegar:
        HablaMadre("Mis parámetros me impiden abandonar el planeta en esta etapa tan
                    temprana de la misión.");
        rtrue;

      Abrir:
        switch (uno) {
          puerta_nave, nave:
            if (uno == nave) {
              uno = puerta_nave;
            }
            if (uno hasnt abierta) {
              uno.abrir();
              move descompresion to nave1;
              HablaMadre("Abriendo compuerta de la nave.");
              if (LugarReal() == nave1 or nave2 or exterior1) {
                "^Se oye el rugido de los servomecanismos de la compuerta de la nave
                 mientras se desplaza suavemente hacia arriba. Desde tu posición puedes
                 observar el habitáculo de descompresión.";
              }
              rtrue;
            } else {
              HablaMadre("La compuerta de la nave ya ha sido abierta. Proceso
                          interrumpido.");
              rtrue;
            }
          compartimento_trajes:
            if (uno hasnt abierto) {
              uno.abrir();
              HablaMadre("Abriendo compartimento de trajes atmosféricos.");
              if (LugarReal() == nave1 or exterior1) {
                "^Un sonido hidráulico en una pared lateral llama tu atención hacia la
                 puerta transparente del compartimento, mientras ésta se desplaza
                 lateralmente para dejar acceso a su interior.";
              }
              rtrue;
            } else {
              HablaMadre("El compartimento de trajes ya ha sido abierto. Proceso
                          interrumpido.");
              rtrue;
            }
          armario:
            HablaMadre("El armario es un dispositivo de apertura manual. Puede abrirlo
                        usted mismo.");
            rtrue;
          cargador:
            Hablamadre("No es necesario abrir el cargador para operar con él.");
            rtrue;
          puerta_procesador:
            if (uno hasnt abierta) {
              if (panel_control has general) {
                HablaMadre("No puedo abrir la puerta desde aquí. El sistema parece haberse
                            reinicializado, y el código de acceso que tengo registrado
                            ya no sirve. Hay que calcular el nuevo código a partir de
                            la semilla que se muestra.");
              } else {
                HablaMadre("No tengo acceso al sistema de control de la puerta.");
              }
            } else {
              HablaMadre("La puerta de acceso al procesador atmosférico ya se encuentra
                          abierta.");
            }
            rtrue;
          default:
            if (uno has puerta) {
              HablaMadre("El computador interno del complejo está dañado, y no me permite
                          accionar ninguna puerta.");
              rtrue;
            }
            if (uno has abrible) {
              HablaMadre("No puedo abrir nada a distancia, señor.");
              rtrue;
            }
            HablaMadre("No se ha podido determinar qué se desea abrir.");
            rtrue;
        }

      Cerrar:
        switch (uno) {
          puerta_nave, nave:
            if (uno == nave) {
              uno = puerta_nave;
            }
            if (uno has abierta) {
              uno.cerrar();
              remove descompresion;
              HablaMadre("Cerrando compuerta de la nave.");
              rtrue;
            } else {
              HablaMadre("La compuerta de la nave ya ha sido cerrada. Proceso
                          interrumpido.");
              rtrue;
            }
          compartimento_trajes:
            if (uno has abierto) {
              uno.cerrar();
              HablaMadre("Cerrando compartimento de trajes atmosféricos.");
              rtrue;
            } else {
              HablaMadre("El compartimento de trajes ya ha sido cerrado. Proceso
                          interrumpido.");
              rtrue;
            }
          puerta_procesador:
            HablaMadre("El computador interno del complejo está dañado, y no me permite
                        accionar ninguna puerta.");
            rtrue;          
          default:
            if (uno has puerta) {
              HablaMadre("El computador interno del complejo está dañado, y no me permite
                          accionar ninguna puerta.");
              rtrue;
            }
            if (uno has abrible) {
              HablaMadre("No puedo cerrar nada a distancia, señor.");
            } else {
              HablaMadre("No se ha podido determinar qué se desea cerrar.");
            }
            rtrue;
        }

      Saludar:
        HablaMadre("Siempre dispuesta a cumplir sus órdenes.");
        rtrue;

      NoComprendido, Hablar:
        <<Preguntar self>>;

      Ayudar:
        if (alien.comportamiento == 0) {
          ! Estamos en la primera parte del juego
          if (LugarReal() == nave1 or nave2) {
            HablaMadre("Su misión principal consiste en investigar la situación. Debe
                        entrar en el complejo y descubrir qué está pasando.");
          } else {
            HablaMadre("Debería arreglar el procesador atmosférico para restablecer el
                        suministro de oxígeno.");
          }
        } else {
          ! El alien ya se ha llevado a Jason
          if (jason in vaina) {
            ! Jason sigue en la vaina
            HablaMadre("Sus órdenes siguen siendo comprobar la situación actual y
                        restablecer las comunicaciones. Rescatar a un herido en combate
                        no forma parte de la misión. Aunque... claro está... yo no soy
                        humana...");
          } else {
            ! Has sacado a Jason de la vaina
            if (entrada hasnt general) {
              ! Aún no sabes que la puerta está sellada
              HablaMadre("Deberían escapar lo antes posible de ese lugar.");
            } else {
              ! Sabes que la puerta está sellada
              HablaMadre("La única salida alternativa que aparece en mis planos es
                          el ascensor de emergencia, pero normalmente permanece
                          inaccesible.");
            }
          }
        }
        rtrue;

      default:
        ! Cuando es una orden distinta a las de arriba, dice "La computadora tiene mejores
        ! cosas que hacer". Lo suyo es que diga otra cosa:
        HablaMadre("Lamento decirle que mi programación no es tan sofisticada.");
        rtrue;
    ],
    mensaje_aleatorio [;
      switch (random(4)) {
        1: HablaMadre("¿Desea alguna cosa?"); PreguntaSiNo = 1;
        2: HablaMadre("¿Puedo ayudarle en algo?"); PreguntaSiNo = 1;
        3: HablaMadre("¿En qué puedo serle de utilidad?");
        4: HablaMadre("¿Puedo hacer algo por usted?"); PreguntaSiNo = 1;
      }
      self.hablar = false;
    ],
    cada_turno [;
      if (self.hablar && banderafin == 0) {
        if (random(100) < 5) {
          self.mensaje_aleatorio();
        }
      } else {
        self.hablar = true;
      }
    ],
  has
    escenario hablable propio;


! comportamiento =
! 0: Está en el procesador atmosférico esperando a que lleguemos
! 1: Está en el laberinto moviéndose al azar (después de haberse llevado a Jason)
!    (OJO: Jason puede estar o no en la vaina)
! 2: Va camino de la puerta para taparla (de aquí se pasa de nuevo al 1)
! 3: Va hacia ti corriendo (cuando vas a activar la autodestrucción)
! 4: "Desaparece"

Object alien procesador
  class Movil
  class ObjetoRastreable
  with
    nombre_corto "monstruo",
    nombre_m 'alien' 'alienigena' 'bicho' 'monstruo' 'ser' 'xenomorfo' 'xenoforme',
    nombre_f 'criatura',
    genero G_MASCULINO,
    descripcion [; 
      print "Puedes apreciar que tiene una cabeza enorme, lisa, alargada de forma obscena
             hacia atrás, sin ojos, con una boca rebosante de afilados dientes, sobre
             una estructura semihumana de largos y delgados brazos y piernas, acabando
             todos ellos en manos y pies con seis dedos y largas uñas. También posee
             una larga cola, que mueve con una velocidad y precisión asombrosas.";
      if (alien.comportamiento > 0) {
        print " Sin duda, es la misma criatura que os atacó en el procesador atmosférico.";
      }
      "";
    ],
    llega [ dir;
      if (self.tipo_de_movimiento == MOVIMIENTO_PERSEGUIR &&
          self.perseguido == jugador && self.tiempo_restante > 0) {
        print "^¡", (_El) self, " te sigue desde ", (el) dir, "! ¡Corre!^";
      } else {
        print "^", (_El) self, " aparece";
        if (dir ~= null) {
          print " por ", (el) dir;
          print ".^";
        }
      }
    ],
    pnj_ha_llegado_default [;
      if (self.tipo_de_movimiento ~= MOVIMIENTO_PERSEGUIR) {
        PNJ_Ruta(self, MOVIMIENTO_ALEATORIO);
      }
    ],
    pnj_ha_llegado pnj_ha_llegado_default,
    pnj_ha_llegado_puerta [;
      if (self.tipo_de_movimiento == MOVIMIENTO_POR_META && self in entrada) {
        AlienBloqueaPuerta();
        if (LugarReal() == entrada) {
          "¡Ves al monstruo tapando la entrada con una sustancia viscosa que se vuelve
           inmediatamente rígida!";
        }
      }
    ],
!    precamino RUTA_NINGUNA,
    tras_abrir NO_PUEDE_ABRIR,   ! Comportamiento después de pasar por una puerta
    capricho 50,    ! 50% de probabilidades de moverse en un turno (defecto = 20%)
    distancia 0,    ! En cada turno, contiene la longitud de la ruta hasta el jugador
    oportunidad 0,  ! Se le da una oportunidad al jugador para escapar del alien cuando
                    ! los dos se encuentren en la misma localidad
    comportamiento 0,
    ! ¿Es la primera vez que lo vemos?
    primera_vez true,
    muerte_final [;
      Bandera_Fin(1);
      viewImageCenter(Alien_Mata_jpg);
      "El monstruo salta sobre ti. Sientes sus afiladas garras hundirse en tu
       carne como si fuese mantequilla, y su poderosa boca se abre completamente
       alrededor de tu cabeza, que en pocos segundos deja de formar parte de tu
       cuerpo.^";
    ],
    describir [;
      if (self.primera_vez) {
        self.primera_vez = false;
        viewImageCenter(procesador.sgw_img);
        print "^De pronto, observas aterrado que un ser de aspecto grotescamente
               terrorífico comienza a sobresalir entre un conglomerado de tuberías.
               Conforme se va incorporando, lo tienes más fácil para comprobar su enorme
               altura y su terrible fisionomía: una cabeza enorme, lisa, alargada de
               forma obscena hacia atrás, sin ojos, con una boca rebosante de afilados
               dientes, coronando una estructura antropomorfa de largos y delgados brazos
               y piernas, culminando todos ellos en apéndices con seis dedos y largas y
               amenazadoras uñas. Al separarse completamente de la pared en la que se
               encontraba escondido, puedes ver su larga cola, que mueve con una
               velocidad y precisión asombrosas.^";
        EsperarTecla();
        rtrue;
      } else {
        ! A partir de ahora, la rutina cada_turno se encargará de describir la presencia
        ! del alien, y no la rutina describir:
        rtrue;
      }
    ],
    tiempo_restante,
    tiempo_agotado [;
      if (alien.comportamiento == 1) {
        self.tiempo_restante = -1;
        PNJ_Ruta(alien, MOVIMIENTO_ALEATORIO);
        "^Vaya... Parece que ha dejado de seguirte... Aún no te ha visto...";
      }
    ],
    ! Se usa por si el alien surge de repente de la pared. En ese caso, no se debe decir
    ! que está aquí, porque ya lo ha dicho el objeto sacador_de_aliens:
    callado false,
!    accion_antes [;
!      if (self.comportamiento == 1 or 3 && self in LugarReal()) {
!        self.capricho = 0;
!      } else {
!        self.capricho = 50;
!      }
!    ],
    antes [;
      Disparar:
        if (rifle notin jugador) {
          rfalse;
        }
        Bandera_Fin(1);
        viewImageCenter(Alien_Mata_jpg);
        "Descargas una ráfaga de munición directamente en el ~corazón~ del monstruoso ser,
         provocando una lluvia verde de sangre ácida que cae directamente sobre tu cuerpo.
         Y mientras éste se disuelve, casi desintegrándose a marchas forzadas, pides por
         favor que el monstruo no se demore mucho en arrancarte la cabeza y librarte del
         inenarrable dolor que sientes...";
       Examinar:
         rfalse;
       default:
         self.muerte_final();
         rtrue;
    ],
    cada_turno [;
      ! Cambia aleatoriamente el nombre del alien:
      switch (random(3)) {
        1: self.nombre_corto = "criatura"; self.genero = G_FEMENINO;
        2: self.nombre_corto = "monstruo"; self.genero = G_MASCULINO;
        3: self.nombre_corto = "ser"; self.genero = G_MASCULINO;
      }
      
      if (self.comportamiento == 1 or 3 && self in LugarReal()) {
        self.capricho = 0;
      } else {
        self.capricho = 50;
      }

      if (self in LugarReal()) {
        if (self.callado) {
          self.callado = false;
          rtrue;
        }
        if (self.comportamiento == 1) {
          ! Se le da dos oportunidades. Después lo mata.
          if (self.oportunidad < 2) {
            switch (self.oportunidad) {
              0: if (self.tipo_de_movimiento == MOVIMIENTO_PERSEGUIR) {
!                   print "^¡El monstruo te está siguiendo! ¡Corre!^";
                   self.oportunidad = self.oportunidad + 2;
                   if (random(3) == 1) {
                     Damusix.TocarV(Rugido_ogg);
                   }
                 } else {
                   if (self.tiempo_restante ~= -1) {
                     print "^¡El monstruo está aquí! Por suerte, parece que no ";
                     if (jason in LugarReal()) {
                       print "os";
                     } else {
                       print "te";
                     }
                     print " ha visto...^";
                   }
                 }
!              1: print "^¡El monstruo gira su cabeza y te observa amenazador!^";
!              2: print "^¡El monstruo comienza a caminar hacia ti!^";
              1: print "^¡El monstruo gira la cabeza y, al verte, salta sobre ti!^";
                 Damusix.TocarV(Rugido_ogg);
            }
            if (self.tiempo_restante >= 0) {
              if (self.oportunidad > 0) {
                PNJ_Ruta(alien, MOVIMIENTO_PERSEGUIR, jugador);
                ArrancarReloj(self, random(5));
              }
            } else {
              self.tiempo_restante = 0;
            }
                  
            self.oportunidad++;
            rtrue;
          }
        } else if (self.comportamiento == 3) {
          if (self.oportunidad < 2) {
            switch (self.oportunidad) {
              0: print "^¡El monstruo te observa amenazador!^";
              1, 2: print "^¡El monstruo salta sobre ti!^";
                    Damusix.TocarV(Rugido_ogg);
            }
            self.oportunidad = self.oportunidad + 3;
            rtrue;
          }
        }   
      }
      
      if (self.oportunidad >= 2) {
        print "^";
        self.muerte_final();
      }
    ],
  has
    animado;


! El atributo general indica que se ha movido de su sitio (empujándolo)

Object robot "robot de mantenimiento" exterior3,
  with
    nombre 'robot' 'androide',
    adjetivos 'mantenimiento',
    descripcion "Son los restos de un robot humanoide, usado principalmente para llevar a
                 cabo tareas de mantenimiento. No cabe duda de que a este ejemplar en
                 concreto no le han tratado con mucho cuidado recientemente. Ha perdido
                 toda su estructura inferior, por lo que no tiene piernas ni posibilidad
                 de movimiento autónomo. Asimismo, todo su antebrazo izquierdo se encuentra
                 seriamente dañado. Pese a todo, tiene aspecto de conservar parte de su
                 capacidad de funcionamiento. Su brazo derecho termina en un dispositivo
                 con forma de soldador que parece plenamente operativo.",
    describir [;
      if (self hasnt general) {
        "^Tirado en el suelo, junto a una pared del complejo, se observa la parte
         superior de un androide.";
      } else {
        "^Hay un medio androide descansando en el suelo.";
      }
    ],
    ! "Decir" y "hablar" es lo mismo que "preguntar"
    reaccionar_antes [;
      Decir: <<Preguntar uno>>;
    ],
    ! Indica si el robot tiene permiso para decir algo aleatoriamente a continuación:
    hablar true,
    ! Indica si el robot ha hablado alguna vez (se actualiza en HablaRobot)
    ha_hablado false,
    antes [;
      Coger, Sacar:
        "El robot pesa demasiado como para poder cogerlo. Lo más que puedes hacer es
         intentar empujarlo...";
      Empujar, Tirar, Girar:
        "Prueba a empujarlo en una dirección concreta.";
      EmpujarDir:
        PermitirEmpujarDir();
        rtrue;
    ],
    despues [;
      Examinar:
        if (self hasnt general) {
          viewImageCenter(Robot_jpg);
          EsperarTecla();
          viewImageCenter(exterior3.sgw_img);
        }
      EmpujarDir:
        give self general;
        "Empujas al robot hacia ", (el) otro, "...";
    ],
    vida [ tema;
      Preguntar:
        self.hablar = false;
        tema = AveriguarTema(TemasRobot);
        switch (tema) {
          tema_robot_alien:
            HablaRobot("Foooorma de... vida dddeeessss... cconn... desconocidddda...
                        cortar... soldar... arreglar...");
          tema_robot_codigo:
            HablaRobot("Código... seeeeeeeeeisssss dígitosssssss...");
          tema_robot_complejo:
            HablaRobot("Colonia espacial... ppfpffkkkk... prospeccion mineral
                        autorizada... trabajo...");
          tema_robot_cortar_abrir:
            HablaRobot("Es posible... seccionar... usando... un
                        dispositivo cortador... adddddddd... adecuado...");
          tema_robot_gente:
            HablaRobot("Todos desaparecidos... ggiiiijjjj... señales de vida
                        cesaron... no queda nadie... gggg... de repente");
          tema_robot_procesador:
            HablaRobot("Debo... ir... processssssador atttmmmm... Arreglar...
                        Prioridad máximmma...");
          tema_robot_puerta:
            HablaRobot("Puerta al complejo... bloqueada... inutilizada...");
          tema_robot_robot:
            HablaRobot("Soy un robot... Soy un robot de mantenimiento
                        diseñado para... diseñado... arreglar... cortar...
                        seccionar... fijar...");
          tema_robot_semilla:
            HablaRobot("Semilla... Generador... Pseudoaleatorio... Nuevo código...
                        basado en semilla...");
          default:
            print "El robot se queda en silencio. Parece no entender lo que le acabas de
                   decir... pero finalmente dice:^";
            self.mensaje_aleatorio();
            rtrue;
        }
        rtrue;
    ],
    ordenes [;
      self.hablar = false;

      Arreglar, Soldar:        
        if (uno == cables) {
          "Unes los extremos de varios cables entre sí, para ayudar al robot a soldarlos,
           pero te das cuenta de que, por mucho que pruebas a unir unos con otros, el
           resultado siempre es negativo. Es evidente que el problema no es que la
           energía no puede circular, sino más bien que NO HAY energía...";
        } else if (uno == circuito) {
          give circuito general;
          HablaRobot("Soldando... soldando...");
          remove robot;
          give robot ausente;
          move robot_inutil to procesador;
          give jugador luz;
          print "^El robot mueve su brazo soldador hasta situarlo justo delante del circuito
                 dañado. Se ven y oyen ráfagas de chispas eléctricas saliendo del punto de
                 unión entre el circuito y la punta del soldador. Finalmente, retira
                 su brazo, justo en el momento en el que toda la maquinaria del procesador
                 comienza un peculiar traqueteo y las luces de la sala se encienden... ¡Está
                 funcionando! ¡Casi puedes oler el oxígeno a través de tu traje!^^
                 La peor parte se la lleva el robot: toda su energía se ha consumido en su
                 última operación, y yace inerte en el suelo como un trasto más.";
          EsperarTecla();
          " Ya no será necesario llevar trajes atmosféricos para poder respirar. Y en cuanto
           a la linterna, una vez que has reestablecido la luz eléctrica en toda la
           instalación, está claro que no hace falta llevarla encendida.";
        } else {
          HablaRobot("No soldable. No soldable.");
          rtrue;
        }
        
      NoComprendido:
        <<Preguntar self>>;
      default:
        self.hablar = false;
        HablaRobot("Comando incorrecto.");
        rtrue;
    ],
    cada_turno [;
      if (self.hablar) {
        if (self in junto_procesador && puerta_procesador hasnt abierta) {
          if (panel_control.codigo_viejo == 0) {
            HablaRobot("Código puerta... 759812... Código puerta... 759812...");
          }
        } else {
          if (random(100) < 60) {
            self.mensaje_aleatorio();
          }
        }
      } else {
        self.hablar = true;
      }
    ],
    mensaje_aleatorio [;
      switch (random(5)) {
        1,2: HablaRobot("Fffff... 759812... Zzzzzz...");
        3:   HablaRobot("Soy un robot de la serie UUR7-35A.");
        4:   HablaRobot("Directivas principales: cortar... soldar... arreglar...");
        5:   HablaRobot("Click... 65712... 12... 12...");
      }
    ],
  has
    ! Es transparente para que se pueda usar "pide cortador a robot"
    estatico animado transparente;


Object "brazo izquierdo"
  with
    nombre_m 'brazo',
    nombre_mp 'brazos',
    nombre_f 'mano',
    nombre_fp 'manos',
    adjetivos 'izquierdo' 'izquierda' 'robot' 'androide',
    genero G_MASCULINO,
    esta_en robot robot_inutil,
    descripcion "Al robot le falta su mano izquierda.",
  has
    escenario;


Object "brazo derecho"
  with
    nombre_m 'brazo',
    nombre_mp 'brazos',
    nombre_f 'mano',
    nombre_fp 'manos',
    adjetivos 'derecho' 'derecha' 'robot' 'androide',
    genero G_MASCULINO,
    esta_en robot robot_inutil,    
    descripcion "Su brazo derecho parece estar en buenas condiciones. El extremo del mismo
                 te recuerda a un rudimentario soldador.",
  has
    escenario;


Object "piernas"
  with
    nombre_f 'pierna',
    nombre_fp 'piernas',
    nombre_m 'pie',
    nombre_mp 'pies',
    adjetivos 'robot' 'androide',
    genero G_FEMENINO + G_PLURAL,
    esta_en robot robot_inutil,    
    descripcion "¡Pero si no tiene piernas!",
  has
    escenario;


Object soldador "soldador"
  with
    nombre_m 'soldador' 'extremo',
    nombre_f 'punta',
    adjetivos 'metalica' 'metalico' 'robot' 'androide',
    genero G_MASCULINO,
    esta_en robot robot_inutil,    
    descripcion "Es una punta metálica ligeramente decolorida y que emite un calor
                 apenas perceptible.",
    antes [;
      Tocar, Coger, Sacar:
        "¡Cuidado! Puedes quemarte...";
    ],
  has
    escenario;


Object "cabeza"
  with
    nombre_f 'cabeza',
    nombre_m 'cerebro' 'craneo',
    adjetivos 'robot' 'androide',
    genero G_FEMENINO,
    esta_en robot robot_inutil,
    descripcion "La cabeza del robot, aunque dañada, parece poder funcionar con
                 normalidad. No podrías decir lo mismo de su ~cerebro~ interior...",
  has
    escenario;


! Este robot sustituye al robot normal cuando se arregla el procesador atmosférico

Object robot_inutil "robot"
  with
    nombre 'robot' 'androide',
    descripcion "Cumplió adecuadamente su trabajo hasta el último momento.",
    inicial "El robot de mantenimiento reposa aquí, sin energía, sin vida...",
    antes [;
      "Este robot ya no sirve para nada. Aunque te duela, no es más que un montón de
       chatarra.";
    ],
  has
    transparente;



! ====================================================================
!
! TEMAS RAIZ
!
! ====================================================================



! Cuidado con los infinitivos. Si pones un infinitivo aquí, luego no se puede usar
! como verbo

! Temas del robot

Object TemasRobot;

Object tema_robot_puerta "la puerta" TemasRobot
  with
    nombre 'puerta' 'como' 'abierta' 'abrir' 'cerrada' 'cerrar';
    
Object tema_robot_robot "el robot" TemasRobot
  with
    nombre 'robot' 'androide' 'ti' 'si' 'mismo';

Object tema_robot_cortar_abrir "cortar o abrir" TemasRobot
  with
    nombre 'cortar' 'corta' 'abre' 'sacar' 'vaina' 'gelatina' 'mucosidad' 'moco' 'mocos'
           'viscosidad';
    
Object tema_robot_complejo "el complejo" TemasRobot
  with
    nombre 'complejo' 'colonia' 'minera' 'mina';
  
Object tema_robot_gente "la gente" TemasRobot
  with
    nombre 'gente' 'personas' 'humanos' 'alguien';
    
Object tema_robot_alien "el monstruo" TemasRobot
  with
    nombre 'alien' 'alienigena' 'monstruo' 'bicho' 'ser' 'criatura' 'xenoforme'
           'xenomorfo';
    
Object tema_robot_codigo "el código de acceso" TemasRobot
  with
    nombre 'codigo' 'clave' 'contrasena' 'panel' 'control' 'puerta' 'procesador';

Object tema_robot_procesador "el procesador atmosférico" TemasRobot
  with
    nombre 'procesador' 'atmosferico';

Object tema_robot_semilla "la semilla" TemasRobot
  with
    nombre 'semilla' 'valor' 'display' 'codigo' 'puerta' 'procesador';

! Temas de la computadora

Object TemasComputadora;

Object tema_madre_compuerta "la compuerta" TemasComputadora
  with
    nombre 'puerta' 'compuerta' 'abierta' 'abrir';

Object tema_madre_planeta "el planeta" TemasComputadora
  with
    nombre 'planeta' 'superficie';
    
Object tema_madre_atmosfera "la atmósfera" TemasComputadora
  with
    nombre 'atmosfera' 'aire' 'respirar';

Object tema_madre_procesador "el procesador atmosférico" TemasComputadora
  with
    nombre 'procesador' 'atmosferico';

Object tema_madre_arreglar "arreglar el procesador" TemasComputadora
  with
    nombre 'arreglar' 'arregla' 'arreglo' 'reparar' 'repara' 'reparo'
           'procesador' 'atmosferico';

Object tema_madre_trajes "los trajes" TemasComputadora
  with
    nombre 'donde' 'esta' 'estan' 'traje' 'trajes';

Object tema_madre_informe "el informe de situación" TemasComputadora
  with
    nombre 'informe' 'situacion' 'estado';

Object tema_madre_cargador "el cargador de baterías" TemasComputadora
  with
    nombre 'bateria' 'baterias' 'cargador' 'cargar' 'recargar';

Object tema_madre_holocubo "el holocubo" TemasComputadora
  with
    nombre 'holocubo' 'cubo' 'holografico';

Object tema_madre_ayuda "pedir ayuda" TemasComputadora
  with
    nombre 'ayuda' 'socorro';

Object tema_madre_mision "la misión" TemasComputadora
  with
    nombre 'mision' 'objetivo';

Object tema_madre_madre "la computadora" TemasComputadora
  with
    nombre 'ti' 'si' 'misma' 'quien' 'eres';

Object tema_madre_complejo "el complejo" TemasComputadora
  with
    nombre 'complejo' 'colonia' 'minera' 'mina';

Object tema_madre_codigo "el código de acceso" TemasComputadora
  with
    nombre 'codigo' 'clave' 'contrasena' 'panel' 'control' 'puerta' 'procesador' 'abrir';

Object tema_madre_semilla "la semilla" TemasComputadora
  with
    nombre 'calcula' 'calcular' 'nueva' 'nuevo' 'contrasena' 'clave' 'codigo' 'semilla'
           'valor' 'display' 'puerta' 'procesador';

Object tema_madre_jason "Jason" TemasComputadora
  with
    nombre 'quien' 'es' 'jason' 'soldado' 'mayor' 'marine' 'companero';

Object tema_madre_localizarme "localizarme" TemasComputadora
  with
    nombre 'localizame' 'localizarme' 'donde' 'estoy' 'estamos';

Object tema_madre_localizarle "localizarle" TemasComputadora
  with
    nombre 'localiza' 'localizar' 'donde' 'esta' 'jason';

Object tema_madre_alien "el monstruo" TemasComputadora
  with
    nombre 'alien' 'alienigena' 'monstruo' 'bicho' 'ser' 'criatura' 'xenoforme'
           'xenomorfo';

Object tema_madre_salir "salir" TemasComputadora
  with
    nombre 'salir' 'salgo' 'como' 'puedo' 'podemos' 'aqui' 'escapar' 'escapamos'
           'escapo';

Object tema_madre_cajafuerte "la caja fuerte" TemasComputadora
  with
    nombre 'caja' 'fuerte' 'combinacion' 'abrir';

Object tema_madre_ordenadores "los ordenadores" TemasComputadora
  with
    nombre 'ordenadores' 'ordenador' 'computadores' 'computador' 'computadoras'
           'computadora';

Object tema_madre_ascensor "el ascensor de emergencia" TemasComputadora
  with
    nombre 'ascensor' 'emergencia';

Object tema_madre_habitats "los hábitats" TemasComputadora
  with
    nombre 'habitat' 'habitats';

Object tema_madre_circuito "el circuito" TemasComputadora
  with
    nombre 'circuito' 'conexion' 'fotonico';

Object tema_madre_energia "la energía" TemasComputadora
  with
    nombre 'energia' 'luz';

! Temas de Jason

Object TemasJason;

Object tema_jason_donde_estas "dónde estas" TemasJason
  with
    nombre 'donde' 'estas' 'esta';

Object tema_jason_detector "el detector" TemasJason
  with
    nombre 'detector' 'movimiento' 'presencia';

Object tema_jason_jason "jason" TemasJason
  with
    nombre 'jason' 'ti' 'si' 'mismo' 'quien' 'eres';

Object tema_jason_rifle "el rifle" TemasJason
  with
    nombre 'rifle' 'arma' 'pistola';
      
Object tema_jason_comunicador "el comunicador" TemasJason
  with
    nombre 'comunicador' 'intercomunicador' 'transmisor';
    
Object tema_jason_colonia "la colonia" TemasJason
  with
    nombre 'colonia' 'minera' 'complejo';

Object tema_jason_gente "la gente" TemasJason
  with
    nombre 'gente' 'personas' 'humanos' 'alguien';

Object tema_jason_alien "el monstruo" TemasJason
  with
    nombre 'alien' 'alienigena' 'monstruo' 'bicho' 'ser' 'criatura' 'xenoforme'
           'xenomorfo';

Object tema_jason_planeta "el planeta" TemasJason
  with
    nombre 'planeta' 'superficie';

Object tema_jason_vaina "la vaina" TemasJason
  with
    nombre 'vaina' 'capullo' 'mucosa' 'gelatinosa' 'mucosidad' 'viscosidad' 'viscosa';

Object tema_jason_madre "Madre" TemasJason
  with
    nombre 'madre' 'computadora' 'computador' 'ordenador' 'nave';

Object tema_jason_mision "la misión" TemasJason
  with
    nombre 'mision' 'objetivo' 'objetivos';

Object tema_jason_robot "el robot" TemasJason
  with
    nombre 'robot' 'androide' 'robots' 'androides';



! ====================================================================
!
! OBJETOS QUE LLEVAN LOS PERSONAJES AL EMPEZAR
!
! ====================================================================



Object rifle "rifle de asalto" jason
  with
    nombre 'tu' 'rifle' 'asalto' 'arma' 'kolmogorov',
    descripcion "Un rifle de asalto Kolmogorov 773. Dispara proyectiles con un alto poder
                 de penetración. Lleva acoplado un detector de movimiento.",
!    inicial "Lo único que queda de Jason es su rifle tirado en el suelo como la huella de
!             un momento terrible.",
    reaccionar_antes [;
      Coger, Dejar:
        if (uno == detector_movimiento) {
          print_ret (_El) uno, " forma parte ", (del) self, ".";
        }
    ],
    antes [;
      Dar:
        if (otro == jason) {
          HablaJason("Quédeselo usted... Ya no lo quiero...");
          rtrue;
        } else {
          "Deberías quedártelo. Es demasiado valioso para desprenderte de él.";
        }
      Dejar:
        "Es demasiado valioso para desprenderte de él.";
      Coger:
        if (self in jason) {
          HablaJason("Lo siento, señor, pero preferiría continuar con mi arma.");
          rtrue;
        }
    ],
    despues [;
      Coger:
        if (detector_movimiento hasnt encendido) {
          give detector_movimiento encendido;
          "Al coger el rifle del suelo, no dudas ni un instante en activar el detector
           de movimiento que lleva acoplado. Está claro puede serte de mucha utilidad.";
        }
    ],
  has
    transparente;


Object detector_movimiento "detector de movimiento" rifle
  with
    nombre 'detector' 'sensor' 'localizador' 'dispositivo',
    adjetivos 'movimiento',
    descripcion "Es un dispositivo que detecta la distancia y dirección a la que se
                 encuentra un objeto móvil. Tiene un alcance de unos 50 metros, y está
                 programado para percibir objetos grandes o voluminosos, para así evitar
                 falsos positivos.",
    ! El pitido que está sonando por el detector. Se usa para que evitar el feo efecto
    ! de que se detiene y vuelve a sonar el mismo sonido que antes (ver "presencia"
    ! un poco más abajo):
!    esta_sonando null,
    antes [;
      Examinar:
        print (string) self.descripcion;
        if (self hasnt encendido) {
          " Está apagado.";
        } else {
          " Está encendido.";
        }
      Apagar:
        if (jason.sacado() || alien.comportamiento >= 1) {
          if (jason in LugarReal()) {
            jason.hablar = false;
            HablaJason("¿Está loco? Ese aparato es lo único que nos puede ayudar a
                        mantenernos con vida. ¿Y quiere apagarlo?");
            rtrue;
          } else {
            "Dada la situación, desactivarlo no parece lo más sensato.";
          }
        }
    ],
    presencia [;
    
      if (banderafin ~= 0) {
        rtrue;
      }

      if (self hasnt encendido) {
        print "Está apagado.";
        rtrue;
      }

      if (alien has ausente) {
        if (control_sonido_detector.sonido ~= null) {
          control_sonido_detector.desactivar();
        }
        rtrue;
      }

      ! Aquí se calcula la distancia del alien al detector. Se crea una ruta de persecución
      ! hasta el objetivo (el detector), y se copia en la propiedad distancia la longitud
      ! del camino que ha calculado PNJ_Ruta.
      move testigo to parent(alien);
      PNJ_Ruta(testigo, MOVIMIENTO_PERSEGUIR, parent(rifle));
      alien.distancia = testigo.longitud_precamino;
      alien.sentido = testigo.sentido;
      PNJ_Ruta(testigo, MOVIMIENTO_NINGUNO);
      remove testigo;

      ! El detector empieza a funcionar después de que el alien se haya llevado a Jason
      if (alien.comportamiento > 0) {
        self.sonar();
        if (alien in LugarReal()) {
          if (localizacion == laoscuridad) {
            style bold;
            print "^¡El detector capta movimiento aquí mismo!^";
            style roman;
          }
        } else {
          if (alien.distancia > 0 && alien.distancia < 6 && alien hasnt ausente) {
            print "^El detector capta movimiento a una distancia de ",
                   (number) 10 * alien.distancia, " metros hacia ", (el) alien.sentido;
            
            if (alien.comportamiento == 3) {
              ", ¡Y ACERCÁNDOSE!";
            } else {
              ".";
            }
          } else {
            print "";
          }
        }
      } else {
        print "";
      }
    ],
    despues [;
      Encender:
        give self encendido;
        
        if (rifle in jason) {
          jason.hablar = false;
          HablaJason("Déjeme hacerlo a mí...");
          "^Jason enciende el detector de movimiento.";
        }
      Apagar:
        give self ~encendido;
        control_sonido_detector.desactivar();

        if (rifle in jason) {
          jason.hablar = false;
          HablaJason("Déjeme hacerlo a mí...");
          "^Jason apaga el detector de movimiento.";
        }
    ],
    sonar [ s;
      if ((~~(control_sonido_detector.hay_detector)) ||
          control_sonido_detector.sonido == Sirena_ogg) {
        rfalse;
      }
      if (alien in LugarReal()) {
        if (control_sonido_detector.sonido ~= Detector5_ogg) {
          control_sonido_detector.sonido = Detector5_ogg;
          control_sonido_detector.activar(Detector5_ogg, SONIDO_BUCLE_INFINITO);
        }
      } else {
        if (alien.distancia > 0 && alien.distancia < 6 && alien hasnt ausente) {                   
          switch (alien.distancia) {
            5: s = Detector1_ogg;
            4: s = Detector2_ogg;
            3: s = Detector3_ogg;
            2: s = Detector4_ogg;
            1: s = Detector5_ogg;
          }
          if (s ~= control_sonido_detector.sonido) {
            control_sonido_detector.sonido = s;
            control_sonido_detector.activar(s, SONIDO_BUCLE_INFINITO);
          }                           
        } else {
          control_sonido_detector.desactivar();
        }
      }
    ],
    ! Imprimir en cada turno la distancia al alien
    cada_turno [;    
      if (rifle in jugador && self has encendido) {
        ImprimirOEjecutar(self, presencia);
      }
    ],
  has
    conmutable;


Object comunicador "comunicador" objjugador
  with
    nombre 'comunicador' 'intercomunicador' 'transmisor',
    descripcion "El comunicador permite ponerte en contacto directamente con Jason y 
                 Madre. De esta forma, podrás hablar con ellos y oírles aunque no os
                 encontréis cerca uno del otro. Lo llevas acoplado a tu pabellón auditivo.",
    antes [;
      Dejar, Desvestir:
        "No sería conveniente perder el contacto con Jason o la nave.";
    ],
    listarse [;
      if (etapa_inventario == 2) {
        if (self has puesto) {
          print " (acoplado a tu oído)";
        }
        rtrue;
      }
    ],    
  has
    puesto;



! ====================================================================
!
! OTROS OBJETOS
!
! ====================================================================



! Móvil "especial" creado para calcular la distancia y posición a la que se encuentra
! el alien. Se usa en la rutina "presencia" del detector de movimiento:

Object testigo "testigo"
  class Movil;


Object rastro_acido "rastro de ácido"
  class TipoRastro
  with
    adjetivos 'acido' 'verde',
    nombre_plural "rastros de ácido",
    visto_en_exterior false,
    visto_en_interior false,
    descripcion [;
      if (EsExterior(parent(self))) {
        if (self.visto_en_exterior) {
          print "Es el rastro de ácido corrosivo que ya viste antes. ";
        } else {
          self.visto_en_exterior = true;
          print "Son agujeros profundos en la tierra, unos grandes y otros pequeños,
                 formados por algún tipo de sustancia líquida corrosiva. ";
        }
      } else {
        if (self.visto_en_interior) {
          print "Son las gotas de sangre ácida que ya viste antes. ";
        } else {
          self.visto_en_interior = true;
          print "Si no fuese imposible, dirías que esas salpicaduras de ácido corrosivo
                 aparentan ser manchas de sangre verde. Las gotas agujerean el suelo a
                 enorme profundidad. ";
        }
      }
    ],
    inicial [ contador padre numero;
      padre = parent(self);

      if ((padre) && (padre ofclass LugarConRastro)) {
        numero = padre.numeroClase(self, nivelPerceptible);
      } else {
        numero = 0;
      }

      if (numero == 0) {
        "Ves un rastro muy difuso de algo verde en el suelo.";
      }

      print "Ves un rastro de ácido verde en el suelo que ";

      for (contador = 0 : contador < 8 : contador++) {
        ! Miramos la 'intensidad' para buscar una que sea cero
        if ((padre.&rastros-->(2 + contador * 5) == self) &&
            (padre.&rastros-->(3 + contador * 5) >= nivelPerceptible)) {
          if (padre.&rastros-->(4 + contador * 5) ~= 0)
            print "proviene";
          else
            print "se dirige";
          self.describeRastro(padre.&rastros-->(0 + contador * 5),   ! origen
                              padre.&rastros-->(1 + contador * 5),   ! direccion
                              padre.&rastros-->(3 + contador * 5),   ! intensidad
                              padre.&rastros-->(4 + contador * 5));  ! otra_direccion
          print ".^";
        }
      }
    ],
    multDisipacion 1,
    antes [;
      Tocar, Probar, Comer, Coger:
        "Yo que tú no lo haría... Es evidente que esa sustancia es altamente corrosiva.";
    ];


! Hace falta un objeto rastreable, así que creo uno "vacío":

Object cable_rastreable
  class ObjetoRastreable;


! El atributo general indica si es la primera vez que se examina

class RastroCable
  class TipoRastro
  with
    nombre 'cable' 'cables//p',
    color "",
    sala_origen "",
    nombre_plural "cables",
    listar_juntos "cables",
    descripcion [;
      print "Es un cable ", (string) self.color, " del que no te habías percatado antes,
             pues va semioculto por la parte superior de la pared. ";

      if (parent(self) == oficina or laboratorio) {
        print "Sale de detrás de la cámara de seguridad. ";
      } else {
        if (camara_seguridad.visto_en_oficina || camara_seguridad.visto_en_laboratorio) {
          print "Sin duda es el cable que sale de la cámara de seguridad ",
                (string) self.sala_origen, ". ";
        } else {
          print "Sin duda es el cable que sale de la consola que hay en la sala de
                 seguridad. ";
        }
      }
    ],
    antes [;
      Coger, Tocar:
        "No alcanzas. Está muy alto.";
    ],
    describeRastro [ origen direccion intensidad otra_direccion;
      origen = origen + intensidad;                ! Evita un warning
      if (otra_direccion ~= 0) {
        print " desde ", (el) direccion, " ", (al) otra_direccion;
      } else {
        print " hacia ", (el) direccion;
      }
    ],
    multDisipacion 0;                              ! No se disipa nunca


Object rastro_cable_blanco "cable blanco"
  class RastroCable
  with
    adjetivos 'blanco',
    color "blanco",
    sala_origen "de la oficina",
    encontrado false,
    describir [;
      if (rastro_cable_negro notin parent(self)) {
        if (self hasnt general) {
          give self general;
          "^Un cable (que no habías apreciado hasta ahora) te llama la atención en la
           parte superior de la pared.";
        } else {
          "^Mirando hacia arriba aprecias un cable ", (string) self.color, " cruzando la
           pared.";
        }
      } else {
        "^Mirando hacia arriba aprecias dos cables (uno blanco y otro negro) cruzando
          juntos la pared.";
      }
    ];


Object rastro_cable_negro "cable negro"
  class RastroCable
  with
    adjetivos 'negro',
    color "negro",
    sala_origen "del laboratorio",
    encontrado false,
    describir [;
      if (rastro_cable_blanco notin parent(self)) {
        if (self hasnt general) {
          give self general;
          "^Un cable (que no habías apreciado hasta ahora) te llama la atención en la parte
           superior de la pared.";
        } else {
          "^Mirando hacia arriba aprecias un cable ", (string) self.color, " cruzando la
           pared.";
        }
      } else {
        rtrue;
      }
    ];


Object tuberias "tuberías"
  with
    nombre 'tuberias' 'tuberia' 'estructuras' 'estructura' 'techo',
    descripcion "No son más que eso: tuberías que cruzan el techo y las paredes de un lado
                 a otro.",
    esta_en [ l;
      l = LugarReal();
      return (~~(EsExterior(l))) && (~~(l == ventilacion1 or hueco_techo));
    ],
  has
    escenario;


Object autodestruccion
  with
    tiempo 50,
    daemon [ t;
      if (self.tiempo > 0) {

        if (self hasnt general) {        
          t = self.tiempo * 10 / 50;
          
          if (t > 0) {
            style bold;
            print "^MEGAFONÍA: ";
            style roman;

            print "~Falta";
            
            if (t > 1) {
              print "n";
            }

            print " ", t, " minuto";
          
            if (t > 1) {
              print "s";
            }
            print " para la autodestrucción.~^";          
          }
              
          if (t == 0 && self hasnt general) {
            give self general;
            self.tiempo = 60;
          }
          
        } else {
          t = self.tiempo;
          style bold;
          print "^MEGAFONÍA: ";
          style roman;
          print "~Falta";

          if (t > 1) {
            print "n";
          }

          print " ", t, " segundo";
          
          if (t > 1) {
            print "s";
          }

          print " para la autodestrucción.~^";
        }
        self.tiempo--;
      } else {
        Bandera_Fin(1);
        control_sonido_detector.activar(Explosion_ogg);
        "^El complejo estalla en una gigantesca explosión, llevándose consigo todo lo que
         había en su interior y alrededores... incluyéndote a ti.^";
      }
    ];


! El objeto que se encarga de sacar y meter al alien en el juego, de forma aleatoria,
! simulando el hecho de que se mete por las tuberías de los túneles.
! Cuando el alien está en el juego, ~general. Cuando está fuera, general.

Object sacador_de_aliens
  with
    agujero,
    esta_en [;
      return alien.comportamiento == 1;
    ],
    max_laberintos 0,
    tiempo_restante 0,
    tiempo_agotado [;
      if (alien.comportamiento ~= 1) {
        rfalse;
      }
      if (self hasnt general) {                    ! Si está dentro del juego:
        if (parent(alien) ofclass Laberinto &&     ! Y está en un túnel y no nos persigue:
            (~~(alien.tipo_de_movimiento == MOVIMIENTO_PERSEGUIR &&
             alien.perseguido == jugador))) {
          self.sacar_alien();
        } else {
          ArrancarReloj(self, 1);                  ! Lo intentamos en el próximo turno
          rfalse;
        }
      } else {
        self.meter_alien();
      }
      self.tiempo_restante = 0;
    ],
    sacar_alien [;
      if (self has general) {
        rfalse;
      }
      ! Sacamos al alien del juego
      give self general;
      self.agujero = parent(alien);
      remove alien;
      give alien ausente;
      if (LugarReal() == self.agujero) {
        "^¡Ves que el monstruo se mete de repente entre los tubos de la pared y
         desaparece!";
      } else {
        if (control_sonido_detector.sonido ~= null) {
          print "^De repente, el detector de movimiento enmudece... ¡Aquello que
                 estaba detectando ha desaparecido!^";
          if (jason in LugarReal() && random(100) < 50) {
            jason.hablar = false;
            HablaJason("¿Qué pasa? ¿Dónde se ha metido?");
          }
          rtrue;
        }
      }
    ],
    meter_alien [ s;
      if (self hasnt general) {
        rfalse;
      }
      give self ~general;
      ! Volvemos a meter al alien en el juego, pero en un túnel aleatorio:
      give alien ~ausente;
      s = self.seleccionar_salida();
      move alien to s;     ! antes era self.agujero
      if (LugarReal() == s) {
        alien.callado = true;
        alien.tiempo_restante = 0;
        alien.oportunidad = 1;
        "^¡El monstruo aparece de repente entre los tubos y estructuras de la pared!";
      }
    ],
    cada_turno [;
      ! Si hay que volver a arrancar el reloj, lo activamos con más o menos tiempo
      ! dependiendo de si hay que sacar al alien o meterlo:
      if (self.tiempo_restante == 0) {    ! Antes < 0
        if (self hasnt general) {
          ArrancarReloj(self, 7 + random(10));
        } else {
          ArrancarReloj(self, 10 + random(10));
        }
      }
    ],
    calcular_max_laberintos [ i o;
      i = 0;
      objectloop (o ofclass Laberinto) {
        i++;
      }
      self.max_laberintos = i;
    ],
    seleccionar_salida [ o n i;
      ! Selecciona un túnel al azar:
      n = random(self.max_laberintos);
      i = 1;
      objectloop (o ofclass Laberinto) {
        if (i == n) {
          return o;
        } else {
          i++;
        }
      }
      return o;
    ],
  has
    oculto;


Object ruido_puerta_rota
  with
    tiempo_restante,
    tiempo_agotado [;
      "^¡Se oye un ruido terrible! Un estruendoso crujir y chirriar de metales rompiéndose
       y doblándose. El sonido proviene de la puerta del complejo.";
    ];


Object tt_inicial1
  class Escritura
  private
    sonido 1,
    PausaMensaje 250,
    elementos
      "> Aproximación a planeta 2003 UB313...^"
        POR_LETRA LETRA_FIJA PAUSA_NORMAL
      "^> Atmósfera irrespirable... Posible daño en el procesador atmosférico...^"
        POR_LETRA LETRA_FIJA PAUSA_DOBLE
      "^El objetivo de la misión está bien claro:^"
        POR_MENSAJE LETRA_CURSIVA PAUSA_NORMAL
      "^~Elaborar un informe completo de la situación.~^"
        POR_MENSAJE LETRA_NEGRITA SIN_PAUSA;


Object tt_inicial2
  class Escritura
  private
    sonido 1,
    PausaMensaje 500,
    elementos
      "^~Desde hace casi tres años, el Gobierno ha estado utilizando una rica mina
        de Vibrantium en el planeta 2003 UB313, pero llevamos quince días sin tener
        noticias de la colonia minera situada en el planeta.~^"
        POR_MENSAJE LETRA_NEGRITA PAUSA_DOBLE
      "^(¿Intereses comerciales? ¿Militares? ¿Ambas cosas?)^"
        POR_MENSAJE LETRA_CURSIVA PAUSA_DOBLE
      "^~Deberá partir inmediatamente a dicho planeta para hacer un informe de campo
        sobre la situación y restablecer las comunicaciones con la colonia. Para esta
        tarea, que no debería resultar peligrosa, le acompañará el Mayor Jason, un
        experto marine estelar que llevará consigo su equipo habitual de combate y
        dispondrá de pleno apoyo logístico para que le ayude en su misión.^"
        POR_MENSAJE LETRA_NEGRITA 5
      "^Le serán enviados más datos cuando se estime oportuno.~^"
        POR_MENSAJE LETRA_NEGRITA SIN_PAUSA;


Object tt_inicial3
  class Escritura
  private
    sonido 1,
    PausaMensaje 500,
    elementos
      "^(Estupendo: La misión no es peligrosa, pero me asignan a todo un experto marine.)^"
        POR_MENSAJE LETRA_CURSIVA PAUSA_NORMAL
      "^> Los sensores biométricos no detectan presencia de"
        POR_LETRA LETRA_FIJA SIN_PAUSA
      "^  vida orgánica evidente en la superficie...^"
        POR_LETRA LETRA_FIJA PAUSA_NORMAL
      "^~La nave, que estará autogobernada, les dejará en el punto de encuentro
        establecido junto a los edificios principales de la colonia. El primero de ellos
        está destinado a la purificación ambiental, mientras que el otro es el complejo
        principal, construido alrededor de una veta central de mineral.^"
        POR_MENSAJE LETRA_NEGRITA 5
      "^Le serán enviados más datos cuando se estime oportuno.~^"
        POR_MENSAJE LETRA_NEGRITA SIN_PAUSA;


Object tt_inicial4
  class Escritura
  private
    sonido 1,
    PausaMensaje 500,
    elementos
      "^(El mineral, no hay duda. Nada les corrompe más que el dinero, salvo el poder.)^"
        POR_MENSAJE LETRA_CURSIVA PAUSA_DOBLE
      "^~El objetivo de su misión será elaborar un informe detallado de la situación,
        restablecer las comunicaciones y, en el improbable caso de que no lo consiga,
        traer consigo algún elemento que permita a la cúpula militar evaluar el estado
        actual de la colonia.~^"
        POR_MENSAJE LETRA_NEGRITA PAUSA_TRIPLE
      "^(¿Por qué me temo que ese ~improbable caso~ será finalmente el más probable?)^"
        POR_MENSAJE LETRA_CURSIVA SIN_PAUSA;


Object tt_inicial5
  class Escritura
  private
    sonido 1,
    PausaMensaje 500,
    elementos
      "^> Confirmado contacto con la superficie... Maniobra correcta...^"
        POR_LETRA LETRA_FIJA PAUSA_NORMAL
      "^La nave se posa suavemente en la superficie, y todo está en silencio.^"
        POR_MENSAJE LETRA_NORMAL PAUSA_DOBLE
      "^Demasiado silencio...^"
        POR_MENSAJE LETRA_CURSIVA SIN_PAUSA;


Object tt_escape1
  class Escritura
  private
    sonido 1,
    PausaMensaje 500,
    elementos
      "Rápidamente, Jason y tú entráis en el ascensor. El marine no pierde un segundo en
       pulsar el botón del lateral, tras lo cual las puertas del ascensor se cierran y
       éste empieza a subir lentamente.^"
        POR_MENSAJE LETRA_NORMAL PAUSA_DOBLE
      "^La sirena de emergencia sigue sonando con la misma estridencia que antes, y la
       sensación agobiante de estar encerrados en una pequeña cabina apenas es un ligero
       inconveniente frente a la idea de saber que estáis escapando de una muerte segura.^"
        POR_MENSAJE LETRA_NORMAL PAUSA_DOBLE
      "^JASON: ~¿Por qué no sube más deprisa este cacharro?~^"
        POR_MENSAJE LETRA_NEGRITA PAUSA_DOBLE
      "^Os miráis por unos segundos, y ambos observáis la misma expresión de alarma en
       vuestros rostros. Algo ocurre, sin duda. Y como si una misteriosa fuerza invisible
       os empujara, os acercáis al borde del ascensor, miráis hacia abajo y...^"
        POR_MENSAJE LETRA_NORMAL PAUSA_DOBLE
      "^¡AHÍ ESTÁ LA CRIATURA!^"
        POR_MENSAJE LETRA_NEGRITA PAUSA_DOBLE
      "^El monstruoso ser se encuentra agarrado a la estructura inferior del ascensor. Lo
       véis colgando de un brazo, mientras que el otro lo agita salvajemente contra la
       puerta del habitáculo. Casi podrías decir que ¡está sonriendo! Una sonrisa malévola,
       casi obscena, una sonrisa que anuncia dolor e inhumanidad.^"
        POR_MENSAJE LETRA_NORMAL PAUSA_DOBLE
      "^El ascensor sigue subiendo renqueante, lentamente, casi con dolor, mientras trata
       de luchar contra el enorme peso de ese monstruo. Los sonidos lastimeros de sus
       motores y mecanismos os dicen que su estructura no resistirá mucho así.^"
        POR_MENSAJE LETRA_NORMAL PAUSA_DOBLE
      "^Y con convicción y decisión supremas, le pides a Jason que abra el ascensor. Al
       principio parece congelado, bloqueado... Pero finalmente reacciona. Sabe que es
       la única oportunidad que tenéis de salir vivos. Pulsa el botón de apertura justo en
       el momento en el que el ser intenta trepar.^"
        POR_MENSAJE LETRA_NORMAL PAUSA_DOBLE
      "^Las puertas se abren, ¡y en ese momento el monstruo pone su otra mano dentro del
       ascensor!^"
        POR_MENSAJE LETRA_NEGRITA PAUSA_NORMAL;


Object tt_escape2
  class Escritura
  private
    sonido 1,
    PausaMensaje 500,
    elementos
      "^No hay vuelta atrás, ni segundas oportunidades. Das la vuelta al rifle...
       ¡y golpeas la mano del monstruo con la culata! Un grito desgarrador sale de su
       enorme boca abierta, transformando su risa en dolor. Momento que aprovecha Jason
       para patear su cabeza... ¡y lanzarlo al vacío!^"
        POR_MENSAJE LETRA_NORMAL PAUSA_DOBLE
      "^El ascensor, ahora sí, va adquiriendo velocidad, subiendo más y más deprisa. Cierras
       la puerta y te agarras a donde puedes. Jason hace lo mismo, mientras el ascensor
       sube y sube hasta que...^"
        POR_MENSAJE LETRA_NORMAL PAUSA_NORMAL
      "^... emergéis del suelo, fuera del edificio de la colonia. Salís enseguida del
       habitáculo, casi sin creerlo... ¡Estáis vivos!^"
        POR_MENSAJE LETRA_NORMAL ESPERAR_TECLA;


Object tt_final
  class Escritura
  private
    sonido 1,
    PausaMensaje 500,
    elementos
      "^Ordenas a Madre, jadeando y casi sin aliento, que despegue inmediatamente la
       aeronave. El rugido de sus potentes motores suena a música en tus oídos, y casi
       puedes decir que percibes en tu piel el momento exacto en el que el tren de
       aterrizaje deja de tener contacto con la superficie del planeta.^"
        POR_MENSAJE LETRA_NORMAL PAUSA_DOBLE
      "^Desde el aire, ya a salvo, ves y oyes la explosión que destruye por completo el
       complejo y su terrible inquilino.^"
        POR_MENSAJE LETRA_NORMAL PAUSA_DOBLE
      "^(Al menos, eso esperas...)^"
        POR_MENSAJE LETRA_NORMAL PAUSA_DOBLE
      "^~Jason, ¿estás bien?~^"
        POR_MENSAJE LETRA_CURSIVA PAUSA_DOBLE
      "^~¿Jason?~^"
        POR_MENSAJE LETRA_CURSIVA PAUSA_DOBLE
      "^~¡¿JASON?!~^"      
        POR_MENSAJE LETRA_INVERSA PAUSA_DOBLE
      "^Y el grito de dolor inenarrable proveniente de la garganta del marine coincide
       en el tiempo con el crujido de sus costillas al romperse...^"
        POR_MENSAJE LETRA_NORMAL ESPERAR_TECLA;



! ====================================================================
!
! RUTINAS
!
! ====================================================================



! Rutina principal y obligatoria de todo juego InformATE
[ Inicializar intro i;

  if (~~testGraphics()) {
    print "^Lo sentimos, pero este juego necesita un intérprete Glulx
            con capacidades gráficas.^^";
    EsperarTecla();
    quit;
  }

  ! Inicialización gráfica
  initializeSGW(altoVentanaGrafica);
  clearMainWindow();
  localizacion = laoscuridad;

  ! Inicialización sonora
  IniciarSonidos();

  dibujarEstado = false;
  print "¿Quieres ver la introducción? (S/N): ";
  intro = false;

  if (SiONo() == 1) {
    intro = true;
    Introduccion();
  } 

  dibujarEstado = true;

  ! Esto es para que el juego dé siempre la descripción de la habitación, aunque
  ! ya la hayamos visitado
  modomirar = 2;
  
  ! Inventario en una sola frase
  estilo_inventario = ESPANOL_BIT + RECURSIVO_BIT + INFOTOTAL_BIT;

  ! Usamos un parser más "tonto":
  parser_listo = 0;
  
  ! Nombre de la oscuridad
  laoscuridad.nombre_corto = "Zona de oscuridad";

  ! Calcular número total de laberintos:
  sacador_de_aliens.calcular_max_laberintos();

  ! Localización donde comienza el jugador
  localizacion = nave1;

  clearTextWindow();

  PNJ_Ruta(jason, MOVIMIENTO_PERSEGUIR, jugador);
  PNJ_Ruta(alien, MOVIMIENTO_NINGUNO);
  PNJ_Ruta(testigo, MOVIMIENTO_NINGUNO);
  IniciarMoviles();  
  IniciarPuertas();
  IniciarRastros();

  EncenderMusicaSub(true);

  if (~~intro) {
    i = 0; while (i < 15) {
      viewImageCenter(ALIEN_png, true); EsperarTecla(0, 10); i++;
    }
  }

  print "Copyright (c) 2008-2011 Alpha Aventuras";
  EsperarTecla();
  clearMainWindow();
  viewImageCenter(Nave1_jpg);
];


[ IniciarSonidos;
  control_sonido_fondo.iniciar();
  control_sonido_detector.iniciar();
  control_sonido_teletipo.iniciar();
];


[ InitGlkWindow winrock;
  switch (winrock) {
    GG_MAINWIN_ROCK:
      glk_stylehint_set(wintype_TextBuffer, style_Normal,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextBuffer, style_Normal,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_Normal,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Normal,
                        stylehint_Weight, 0);
      glk_stylehint_set(wintype_TextBuffer, style_Normal,
                        stylehint_Proportional, 1);

      glk_stylehint_set(wintype_TextBuffer, style_Input,
                        stylehint_TextColor, $00ff00);
      glk_stylehint_set(wintype_TextBuffer, style_Input,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_Input,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Input,
                        stylehint_Proportional, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Input,
                        stylehint_Weight, 1);

      glk_stylehint_set(wintype_TextBuffer, style_User1,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextBuffer, style_User1,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_User1,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_User1,
                        stylehint_Proportional, 1);
      glk_stylehint_set(wintype_TextBuffer, style_User1,
                        stylehint_Weight, 1);
  
      glk_stylehint_set(wintype_TextBuffer, style_User2,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextBuffer, style_User2,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_User2,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_User2,
                        stylehint_Proportional, 0);
      glk_stylehint_set(wintype_TextBuffer, style_User2,
                        stylehint_Weight, 1);
      glk_stylehint_set(wintype_TextBuffer, style_User2,
                        stylehint_Justification, stylehint_just_LeftRight);

      glk_stylehint_set(wintype_TextBuffer, style_Alert,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextBuffer, style_Alert,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_Alert,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Alert,
                        stylehint_Proportional, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Alert,
                        stylehint_Weight, 1);
         
      glk_stylehint_set(wintype_TextBuffer, style_Emphasized,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextBuffer, style_Emphasized,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_Emphasized,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Emphasized,
                        stylehint_Proportional, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Emphasized,
                        stylehint_Oblique, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Emphasized,
                        stylehint_Weight, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Emphasized,
                        stylehint_Justification, stylehint_just_LeftRight);

      glk_stylehint_set(wintype_TextBuffer, style_Preformatted,
                        stylehint_TextColor, $00aa00);
      glk_stylehint_set(wintype_TextBuffer, style_Preformatted,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_Preformatted,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Preformatted,
                        stylehint_Proportional, 0);
      glk_stylehint_set(wintype_TextBuffer, style_Preformatted,
                        stylehint_Weight, 0);
      glk_stylehint_set(wintype_TextBuffer, style_Preformatted,
                        stylehint_Justification, stylehint_just_LeftRight);

      glk_stylehint_set(wintype_TextBuffer, style_Header,
                        stylehint_TextColor, $00ff00);
      glk_stylehint_set(wintype_TextBuffer, style_Header,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_Header,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_Header,
                        stylehint_Weight, 1);

      glk_stylehint_set(wintype_TextBuffer, style_Subheader,
                        stylehint_TextColor, $00ff00);
      glk_stylehint_set(wintype_TextBuffer, style_Subheader,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextBuffer, style_SubHeader,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextBuffer, style_SubHeader,
                        stylehint_Weight, 1);

      glk_stylehint_set(wintype_TextBuffer, style_Note,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextBuffer, style_Note,
                        stylehint_BackColor, $000000);

      glk_stylehint_set(wintype_TextBuffer, style_BlockQuote,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextBuffer, style_BlockQuote,
                        stylehint_BackColor, $000000);


      glk_stylehint_set(wintype_TextGrid, style_Normal,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextGrid, style_Normal,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_Normal,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_Normal,
                        stylehint_Weight, 0);
      glk_stylehint_set(wintype_TextGrid, style_Normal,
                        stylehint_Proportional, 1);
         
      glk_stylehint_set(wintype_TextGrid, style_Input,
                        stylehint_TextColor, $00ff00);
      glk_stylehint_set(wintype_TextGrid, style_Input,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_Input,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_Input,
                        stylehint_Proportional, 1);
      glk_stylehint_set(wintype_TextGrid, style_Input,
                        stylehint_Weight, 1);
 
      glk_stylehint_set(wintype_TextGrid, style_User1,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextGrid, style_User1,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_User1,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_User1,
                        stylehint_Proportional, 1);
      glk_stylehint_set(wintype_TextGrid, style_User1,
                        stylehint_Weight, 1);
  
      glk_stylehint_set(wintype_TextGrid, style_User2,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextGrid, style_User2,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_User2,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_User2,
                        stylehint_Proportional, 0);
      glk_stylehint_set(wintype_TextGrid, style_User2,
                        stylehint_Weight, 1);
      glk_stylehint_set(wintype_TextGrid, style_User2,
                        stylehint_Justification, stylehint_just_LeftRight);

      glk_stylehint_set(wintype_TextGrid, style_Alert,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextGrid, style_Alert,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_Alert,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_Alert,
                        stylehint_Proportional, 1);
      glk_stylehint_set(wintype_TextGrid, style_Alert,
                        stylehint_Weight, 1);
         
      glk_stylehint_set(wintype_TextGrid, style_Emphasized,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextGrid, style_Emphasized,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_Emphasized,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_Emphasized,
                        stylehint_Proportional, 1);
      glk_stylehint_set(wintype_TextGrid, style_Emphasized,
                        stylehint_Oblique, 1);
      glk_stylehint_set(wintype_TextGrid, style_Emphasized,
                        stylehint_Weight, 1);
      glk_stylehint_set(wintype_TextGrid, style_Emphasized,
                        stylehint_Justification, stylehint_just_LeftRight);

      glk_stylehint_set(wintype_TextGrid, style_Preformatted,
                        stylehint_TextColor, $00aa00);
      glk_stylehint_set(wintype_TextGrid, style_Preformatted,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_Preformatted,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_Preformatted,
                        stylehint_Proportional, 0);
      glk_stylehint_set(wintype_TextGrid, style_Preformatted,
                        stylehint_Weight, 1);
      glk_stylehint_set(wintype_TextGrid, style_Preformatted,
                        stylehint_Justification, stylehint_just_LeftRight);

      glk_stylehint_set(wintype_TextGrid, style_Header,
                        stylehint_TextColor, $00ff00);
      glk_stylehint_set(wintype_TextGrid, style_Header,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_Header,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_Header,
                        stylehint_Weight, 1);

      glk_stylehint_set(wintype_TextGrid, style_Subheader,
                        stylehint_TextColor, $00ff00);
      glk_stylehint_set(wintype_TextGrid, style_Subheader,
                        stylehint_BackColor, $000000);
      glk_stylehint_set(wintype_TextGrid, style_SubHeader,
                        stylehint_Size, 1);
      glk_stylehint_set(wintype_TextGrid, style_SubHeader,
                        stylehint_Weight, 1);

      glk_stylehint_set(wintype_TextGrid, style_Note,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextGrid, style_Note,
                        stylehint_BackColor, $000000);

      glk_stylehint_set(wintype_TextGrid, style_BlockQuote,
                        stylehint_TextColor, $ffffff);
      glk_stylehint_set(wintype_TextGrid, style_BlockQuote,
                        stylehint_BackColor, $000000);
  }

  rfalse; ! si te olvidas esta linea, el juego no funcionará bien
];


[ HandleGlkEvent ev snd r;
  SGW_HandleGlk(ev);
  switch (ev-->0) {
    evtype_Timer:
      if (EsExterior(LugarReal()) && random(100) < 55) {
        r = 20 * random(5);
        if (r >= 80) {
          viewImageCenter(Fondo_blanco_jpg);
        }
        Damusix.TocarV(Trueno_ogg, r);
        if (r >= 80) {
          glk($00D6, 100);                    ! request_timer_events
          while (1) {
            glk($00C0, gg_arguments);         ! glk_select(gg_arguments);
            if ((gg_arguments-->0) == 1) {
              glk($00D6, TIMER_TRUENO);       ! request_timer_events
              break;
            }
          }
          viewImageCenter(localizacion.sgw_img);
        }
      } else if ((LugarReal() ofclass Laberinto || LugarReal() ofclass Balcon)
                  && random(100) < 75) {
        r = 20 * random(3) + 40;
        switch (random(4)) {
          1: snd = Gota_Oscura_ogg;
          2: snd = Gotas_Agua_ogg;
          3: snd = Cueva_Oscura_ogg;
          4: snd = Monstruo_ogg; r = 100 / alien.distancia;
        }
        Damusix.TocarV(snd, r);
      }
  }
];


[ BuscarEnSub;
  <<Examinar uno>>;
];


[ ConsultarSub;
  <<Examinar uno>>;
];


[ HablarSub;
  ComoHablar();
];


[ ResponderSub;
  ComoHablar();
];


[ DecirSub;
  ComoHablar();
];


[ ComoHablar;
  print "[Para hablar con un personaje, usa la forma ~";
  style bold;
  print "PERSONAJE, lo que sea";
  style roman;
  print "~. Por ejemplo: ~";
  style bold;
  print "JASON, QUÉ OPINAS DE MADRE";
  style roman;
  "~.]";
];


! Guarda el estado anterior al RESTAURAR, REINICIAR o UNDO:
[ RestaurarSonidoAntes s;
  control_sonido_fondo.desactivar();
  s = control_sonido_detector.sonido;
  control_sonido_detector.desactivar();
  return s;
];


! Restaura el estado anterior al RESTAURAR, REINICIAR o UNDO:
[ RestaurarSonidoDespues s;
  IniciarSonidos();
  
  if (control_sonido_fondo.hay_sonido &&
      LugarReal() provides sgw_mus) {
    control_sonido_fondo.sonido = null;
    EncenderMusicaSub(true);
  }

  if ((control_sonido_detector.hay_detector && s ~= null) ||
      s == Sirena_ogg) {
    control_sonido_detector.activar(s, SONIDO_BUCLE_INFINITO);
  }
];


[ ReiniciarSub s;
  s = RestaurarSonidoAntes();                                     ! (c) Alpha
  
  M__L(##Reiniciar,1);
  if (SiONo()~=0) {
    @restart;
    M__L(##Reiniciar,2);
  }
  
  RestaurarSonidoDespues(s);                                      ! (c) Alpha
];


[ RestaurarSub res fref s;
  s  = RestaurarSonidoAntes();                                    ! (c) Alpha

  fref = glk($0062, $01, $02, 0); ! fileref_create_by_prompt
  if (fref == 0)
    jump RFailed;
  gg_savestr = glk($0042, fref, $02, GG_SAVESTR_ROCK); ! stream_open_file
  glk($0063, fref); ! fileref_destroy
  if (gg_savestr == 0)
    jump RFailed;

  @restore gg_savestr res;

  glk($0044, gg_savestr, 0); ! stream_close
  gg_savestr = 0;

.RFailed;
  M__L(##Restaurar,1);

  RestaurarSonidoDespues(s);                                      ! (c) Alpha
];


[ Teclado  a_buffer a_table  nw i w w2 x1 x2 s;

    ActualizarEstatus();
    .FreshInput;

!  Save the start of the buffer, in case "oops" needs to restore it
!  to the previous time's buffer

    for (i=0:i<64:i++) oops_workspace->i = a_buffer->i;

!  In case of an array entry corruption that shouldn't happen, but would be
!  disastrous if it did:

#Ifdef TARGET_ZCODE;
   a_buffer->0 = INPUT_BUFFER_LEN;
   a_table->0 = 15;  ! Allow to split input into this many words
#Endif; ! TARGET_

!  Print the prompt, and read in the words and dictionary addresses

    M__L(##Prompt);
    TrasElPrompt();
    #IfV5; DibujarLineaEstado(); #Endif;
    KeyboardPrimitive(a_buffer, a_table);
#Ifdef TARGET_ZCODE;
    nw=a_table->1;
#Ifnot; ! TARGET_GLULX
    nw=a_table-->0;
#Endif; ! TARGET_

!  If the line was blank, get a fresh line
    if (nw == 0)
    { M__L(##Miscelanea,10); jump FreshInput; }

!  Unless the opening word was "oops", return
!  Conveniently, a_table-->1 is the first word in both ZCODE and GLULX.

    w=a_table-->1;
    if (w == OOPS1__WD or OOPS2__WD or OOPS3__WD) jump DoOops;

#IfV5;
!  Undo handling

!  In Graham's 6/9 code, the following line tests (parse->1==1) instead
!  of (nw==1). I believe that's wrong. In particular, it prevents "undo"
!  from working during "Which do you mean...?" disambiguation, because
!  input at that prompt goes into parse2 instead of parse.

    if ((w == ANULAR1__WD or ANULAR2__WD or ANULAR3__WD) && (nw==1))
    {   if (turnos==1)
        {   M__L(##Miscelanea,11); jump FreshInput;
        }
        if (bandera_deshacer==0)
        {   M__L(##Miscelanea,6); jump FreshInput;
        }
        if (bandera_deshacer==1) jump UndoFailed;
#Ifdef TARGET_ZCODE;
        ! The just_undone check shouldn't be done in Glulx, as multiple
        ! undo is possible.
        if (just_undone==1)
        {   M__L(##Miscelanea,12); jump FreshInput;
        }
        @restore_undo i;
#Ifnot; ! TARGET_GLULX
        s = RestaurarSonidoAntes();                               ! (c) Alpha

        @restoreundo i;

        RestaurarSonidoDespues(s);                                ! (c) Alpha

        i = (~~i);
#Endif; ! TARGET_
        if (i==0)
        {   .UndoFailed;
            M__L(##Miscelanea,7);
        }
        jump FreshInput;
    }
#Ifdef TARGET_ZCODE;
    @save_undo i;
#Ifnot; ! TARGET_GLULX
    @saveundo i;
    if (i == -1) {
        GGRecoverObjects();
        i = 2;
    }
    else
        i = (~~i);
#Endif; ! TARGET_
    just_undone=0;
    bandera_deshacer=2;
    if (i==-1) bandera_deshacer=0;
    if (i==0) bandera_deshacer=1;
    if (i==2)
    {
#Ifdef TARGET_ZCODE;
        style bold;
#Ifnot; ! TARGET_GLULX
        glk($0086, 4); ! set subheader style
#Endif; ! TARGET_
        print (name) localizacion, "^";
#Ifdef TARGET_ZCODE;
        style roman;
#Ifnot; ! TARGET_GLULX
        glk($0086, 0); ! set normal style
#Endif; ! TARGET_
        M__L(##Miscelanea,13);
        just_undone=1;
        jump FreshInput;
    }
#Endif;

    return nw;

    .DoOops;
    if (eepa_desde == 0)
    {   M__L(##Miscelanea,14); jump FreshInput; }
    if (nw == 1)
    {   M__L(##Miscelanea,15); jump FreshInput; }
    if (nw > 2)
    {   M__L(##Miscelanea,16); jump FreshInput; }

!  So now we know: there was a previous mistake, and the jugador has
!  attempted to correct a single word of it.

    for (i=0:i<INPUT_BUFFER_LEN:i++) buffer2->i = a_buffer->i;
#Ifdef TARGET_ZCODE;
    x1 = a_table->9; ! Start of word following "oops"
    x2 = a_table->8; ! Length of word following "oops"
#Ifnot; ! TARGET_GLULX
    x1 = a_table-->6; ! Start of word following "oops"
    x2 = a_table-->5; ! Length of word following "oops"
#Endif; ! TARGET_

!  Repair the buffer to the text that was in it antes the "oops"
!  was typed:

    for (i=0:i<64:i++) a_buffer->i = oops_workspace->i;
    Tokenise__(a_buffer,a_table);

!  Work out the position in the buffer of the word to be corrected:

#Ifdef TARGET_ZCODE;
    w = a_table->(4*eepa_desde + 1); ! Start of word to go
    w2 = a_table->(4*eepa_desde);    ! Length of word to go
#Ifnot; ! TARGET_GLULX
    w = a_table-->(3*eepa_desde);      ! Start of word to go
    w2 = a_table-->(3*eepa_desde - 1); ! Length of word to go
#Endif; ! TARGET_

!  Write spaces over the word to be corrected:

    for (i=0:i<w2:i++) a_buffer->(i+w) = ' ';

    if (w2 < x2)
    {   ! If the replacement is longer than the original, move up...

        for (i=INPUT_BUFFER_LEN-1:i>=w+x2:i--)
            a_buffer->i = a_buffer->(i-x2+w2);

        ! ...increasing buffer size accordingly.

#Ifdef TARGET_ZCODE;
        a_buffer->1 = (a_buffer->1) + (x2-w2);
#Ifnot; ! TARGET_GLULX
        a_buffer-->0 = (a_buffer-->0) + (x2-w2);
#Endif; ! TARGET_

    }

!  Write the correction in:

    for (i=0:i<x2:i++) a_buffer->(i+w) = buffer2->(i+x1);

    Tokenise__(a_buffer,a_table);
#Ifdef TARGET_ZCODE;
    nw=a_table->1;
#Ifnot; ! TARGET_GLULX
    nw=a_table-->0;
#Endif; ! TARGET_

    return nw;
];


[ PermitirEmpujarDir i tmp;
  if (parent(otro) ~= Brujula)         return M__L (##EmpujarDir, 2, uno);
  if (otro == obj_arriba or obj_abajo) return M__L (##EmpujarDir, 3, uno);
  RutinasDespues(); i = uno; move i to jugador;
  tmp = localizacion;                                                ! (c) Alpha
  estoyEmpujando = true;                                             ! (c) Alpha
  <Ir otro>;
  estoyEmpujando = false;                                            ! (c) Alpha
  if (localizacion == LaOscuridad) move i to localizacion_real;
  else                             move i to localizacion;
  if (tmp ~= localizacion && localizacion ~= laoscuridad)            ! (c) Alpha
    ImprimirOEjecutar(i, describir);                                 ! (c) Alpha
];


[ EsperarTecla s delay timer;
  if (s) print (string) s;
  timer = control_timer.timer_actual;   ! Guardamos timer actual (0 si no hay)
  glk($00D6, delay * 5);                ! request_timer_events
  glk($00D2, gg_mainwin);               ! glk_request_char_event(gg_mainwin);
  while (1) {
    glk($00C0, gg_arguments);           ! glk_select(gg_arguments);
    if ((gg_arguments-->0) == 2) break; ! 2=evType_CharInput
    if ((gg_arguments-->0) == 1) {
      glk($00D3, gg_mainwin);           ! cancel_char_event
      glk($00D6, timer);                ! request_timer_events
      return 0;
    }
  }
  glk($00D6, timer);                    ! request_timer_events
  return gg_arguments-->2;
];


[ Introduccion i;
  clearMainWindow();
  viewImageCenter(Fondo_negro_jpg);
  control_sonido_fondo.activar(Intro_ogg, SONIDO_BUCLE_INFINITO);
  tt_inicial1.visualiza();
  i = 0; while (i < 15) { viewImageCenter(A_png, true); EsperarTecla(0, 20); i++; }
  EsperarTecla(0, 300);
  tt_inicial2.visualiza();
  i = 0; while (i < 15) { viewImageCenter(AI_png, true); EsperarTecla(0, 20); i++; }
  EsperarTecla(0, 900);
  tt_inicial3.visualiza();
  i = 0; while (i < 15) { viewImageCenter(AIN_png, true); EsperarTecla(0, 20); i++; }
  EsperarTecla(0, 900);
  tt_inicial4.visualiza();
  i = 0; while (i < 15) { viewImageCenter(AIEN_png, true); EsperarTecla(0, 20); i++; }
  EsperarTecla(0, 800);
  tt_inicial5.visualiza();
  EsperarTecla(0, 600);
  i = 0; while (i < 15) { viewImageCenter(ALIEN_png, true); EsperarTecla(0, 20); i++; }
  Damusix.SimpleFadeOut(Intro_ogg, 3000);
];


[ DejarRastros t;
  t = 40;
  procesador.marcaRastro(alien, rastro_acido, obj_afuera, t + 20);
  junto_procesador.marcaRastro(alien, rastro_acido, obj_s, t + 22);
  exterior1.marcaRastro(alien, rastro_acido, obj_s, t + 24);
  exterior3.marcaRastro(alien, rastro_acido, obj_e, t + 26);
  junto_puerta.marcaRastro(alien, rastro_acido, obj_adentro, t + 28);
  entrada.marcaRastro(alien, rastro_acido, obj_s, t + 30);
  balcon1.marcaRastro(alien, rastro_acido, obj_o, t + 32);
  laberinto1.marcaRastro(alien, rastro_acido, obj_s, t + 34);
  balcon2.marcaRastro(alien, rastro_acido, obj_s, t + 36);
  laberinto11.marcaRastro(alien, rastro_acido, obj_e, t + 38);
  balcon4.marcaRastro(alien, rastro_acido, obj_abajo, t + 40);
  balcon8.marcaRastro(alien, rastro_acido, obj_abajo, t + 42);
  balcon12.marcaRastro(alien, rastro_acido, obj_e, t + 42);
  laberinto36.marcaRastro(alien, rastro_acido, obj_arriba, t + 64);
  ventilacion1.marcaRastro(alien, rastro_acido, obj_e, t + 66);
  ! Si no pongo esta última línea, el rastro de ácido no aparecerá en la localidad del
  ! procesador hasta que salga y vuelva a entrar:
  move rastro_acido to procesador;
];


! El cable que sale de la oficina:

[ PonerCableBlanco;
  if (~~(rastro_cable_blanco.encontrado)) {
    rastro_cable_blanco.encontrado = true;
    oficina.marcaRastro(cable_rastreable, rastro_cable_blanco, obj_e, nivelClaro,
                        camara_seguridad);
    balcon2.marcaRastro(cable_rastreable, rastro_cable_blanco, obj_s, nivelClaro,
                        obj_o);
    laberinto11.marcaRastro(cable_rastreable, rastro_cable_blanco, obj_e, nivelClaro,
                            obj_n);
    balcon4.marcaRastro(cable_rastreable, rastro_cable_blanco, obj_abajo, nivelClaro,
                        obj_o);
    balcon8.marcaRastro(cable_rastreable, rastro_cable_blanco, obj_o, nivelClaro,
                        obj_arriba);
    laberinto18.marcaRastro(cable_rastreable, rastro_cable_blanco, obj_s, nivelClaro,
                            obj_e);
    seguridad.marcaRastro(cable_rastreable, rastro_cable_blanco, consola, nivelClaro,
                          obj_n);
  }
  rfalse;
];


! El cable que sale del laboratorio:

[ PonerCableNegro;
  if (~~(rastro_cable_negro.encontrado)) {
    rastro_cable_negro.encontrado = true;
    laboratorio.marcaRastro(cable_rastreable, rastro_cable_negro, obj_n, nivelClaro,
                            camara_seguridad);
    laberinto19.marcaRastro(cable_rastreable, rastro_cable_negro, obj_o, nivelClaro,
                            obj_s);
    balcon8.marcaRastro(cable_rastreable, rastro_cable_negro, obj_o, nivelClaro,
                        obj_e);
    laberinto18.marcaRastro(cable_rastreable, rastro_cable_negro, obj_s, nivelClaro,
                            obj_e);
    seguridad.marcaRastro(cable_rastreable, rastro_cable_negro, consola, nivelClaro,
                          obj_n);
  }
  rfalse;
];


[ HablaJason msg1 obj1 msg2 obj2 msg3 obj3 msg4;
  style bold;

  if (jason notin LugarReal()) {
    Damusix.TocarV(Transmisor_ogg);
    print "^JASON (por el comunicador): ";
  } else {
    print "^JASON: ";
  }
    
  style roman;
  print "~", (string) msg1;
    
  if (obj1 ~= 0) {
    if (obj1 == traje_jason && actor == jason) {
      print "mi traje";
    } else {
      print (el) obj1;
    }
  }
    
  if (msg2 ~= 0) {
    print (string) msg2;
  }
  
  if (obj2 ~= 0) {
    print (o) obj2;
  }
    
  if (msg3 ~= 0) {
    print (string) msg3;
  }
  
  if (obj3 ~= 0) {
    print (del) obj3;
  }

  if (msg4 ~= 0) {
    print (string) msg4;
  }
  
  print "~^";
];


[ HablaRobot msg;
  robot.ha_hablado = true;
  style bold;
  print "^ROBOT: ";
  style roman;
  print "~", (string) msg, "~^";
];


Object tt_computadora
  class Escritura
  private
    sonido 1,
    elementos
      "" POR_LETRA LETRA_FIJA SIN_PAUSA;

  
[ HablaMadre msg;
  style bold;
    
  if (LugarReal() ~= nave1 or nave2) {
    Damusix.TocarV(Transmisor_ogg);
    print "^MADRE (por el comunicador): ";
  } else {
    print "^MADRE: ";
  }

  style roman;
  print "~";
  
  tt_computadora.escribe(0, msg);
  tt_computadora.visualiza();

  print "~^";
  style roman;
];


[ Bandera_Fin bf;
  control_sonido_fondo.desactivar();
  control_sonido_detector.desactivar();
  
  if (bf == 1) {
    Damusix.TocarV(Grito_Jugador_ogg);
  }
  
  banderafin = bf;
  rfalse;
];


[ SegundaParte o;
  alien.primera_vez = false;
  ! Jason ya no lleva el traje:
  remove traje_jason;
  give traje_jason ausente;
  ! A Jason se le cae todo cuando se lo lleva el alien:
  objectloop (o) {
    if (o in jason) move o to procesador;
  }
  move jason to vaina;
  PNJ_Ruta(jason, MOVIMIENTO_NINGUNO);
  ! Pasa a moverse aleatoriamente después de dejar a Jason en la vaina:
  alien.comportamiento = 1;
  move alien to laberinto16;
  ! El alien empieza a moverse aleatoriamente
  PNJ_Ruta(alien, MOVIMIENTO_ALEATORIO);
  ! La puerta ahora está rota
  puerta_rara.abrir();
  junto_puerta.sgw_img = Junto_Puerta_Abierta_jpg;
!  give puerta_rara abierta;
  ! Pero no ya no permite abrirla. Esto se hace para que los PNJ no abran ni cierren la
  ! puerta cuando la crucen automáticamente.
!  give puerta_rara ~abrible;
  ! Deja los rastros de ácido por el camino hasta la vaina
  DejarRastros();
];


! Rutina para comprobar si una localidad es exterior o interior
! Devuelve true si es exterior
[ EsExterior l;
  return l == exterior1 or exterior3 or junto_procesador or junto_puerta;
];


[ LugarReal;
  if (localizacion == laoscuridad) {
    return localizacion_real;
  } else {
    return localizacion;
  }
];


! El alien ha llegado a bloquear la puerta, y empieza a moverse aleatoriamente:
[ AlienBloqueaPuerta;
  remove puerta_rara;
  give puerta_rara ausente;
  junto_puerta.adentro = puerta_viscosa;
  entrada.afuera = puerta_viscosa;
  puerta_viscosa.adentro = entrada;
  puerta_viscosa.afuera = junto_puerta;
  move puerta_viscosa to entrada;
  IniciarPuertas();
  max_longitud_camino = 10;
  alien.pnj_ha_llegado = alien.pnj_ha_llegado_default;
  alien.comportamiento = 1;
  PNJ_Ruta(alien, MOVIMIENTO_ALEATORIO);
];



! ====================================================================
!
! LINEAS DE GRAMATICA
!
! ====================================================================



Include "Gramatica";
Include "Decir";           ! Para poder usar DECIR ... A ...

Verb 'gilipollas'
  *               -> Tacos;

Verb 'guarro'
  *               -> Tacos;

Verb 'madre,'
  * 'semilla' number                                         -> Semilla
  * 'semilla' 'es'/'vale' number                             -> Semilla
  * 'la' 'semilla' 'es'/'vale' number                        -> Semilla
  * 'semilla' 'es' 'igual' 'a' number                        -> Semilla
  * 'la' 'semilla' 'es' 'igual' 'a' number                   -> Semilla
  * 'valor' 'de' 'semilla' number                            -> Semilla
  * 'valor' 'de' 'semilla' 'es'/'vale' number                -> Semilla
  * 'valor' 'de' 'la' 'semilla' number                       -> Semilla
  * 'valor' 'de' 'la' 'semilla' 'es'/'vale' number           -> Semilla
  * 'el' 'valor' 'de' 'semilla' 'es'/'vale' number           -> Semilla
  * 'el' 'valor' 'de' 'la' 'semilla' 'es'/'vale' number      -> Semilla
  * 'valor' 'de' 'semilla' 'igual' 'a' number                -> Semilla
  * 'valor' 'de' 'semilla' 'es' 'igual' 'a' number           -> Semilla
  * 'valor' 'de' 'la' 'semilla' 'igual' 'a' number           -> Semilla
  * 'valor' 'de' 'la' 'semilla' 'es' 'igual' 'a' number      -> Semilla
  * 'el' 'valor' 'de' 'semilla' 'es' 'igual' 'a' number      -> Semilla
  * 'el' 'valor' 'de' 'la' 'semilla' 'es' 'igual' 'a' number -> Semilla;

Verb 'usa' 'utiliza'
  *                 -> Usar
  * noun            -> Usar
  * noun noun       -> Usar
  * noun 'con' noun -> Usar;

Verb meta 'sonido' 'sonidos'
  *                                      -> Sonido
  * 'on' / 'encendido' / 'si'            -> EncenderSonido
  * 'off' / 'apagado' / 'no'             -> ApagarSonido
  * 'detector'                           -> SonidoDetector
  * 'detector' 'on' / 'encendido' / 'si' -> EncenderSonidoDetector
  * 'detector' 'off' / 'apagado' / 'no'  -> ApagarSonidoDetector
  * 'fondo'                              -> Musica
  * 'fondo' 'on' / 'encendida' / 'si'    -> EncenderMusica
  * 'fondo' 'off' / 'apagada' / 'no'     -> ApagarMusica;

Verb meta 'musica'
  *                                   -> Musica
  * 'fondo' 'on' / 'encendida' / 'si' -> EncenderMusica
  * 'fondo' 'off' / 'apagada' / 'no'  -> ApagarMusica
  * 'on' / 'encendida' / 'si'         -> EncenderMusica
  * 'off' / 'apagada' / 'no'          -> ApagarMusica;

Verb 'activa'
  * noun -> Encender;

Extend 'une' first
  * noun 'con' noun           -> Atar
  * noun 'entre' 'si'         -> UnirEntreSi
  * noun 'uno' 'con' 'otro'   -> UnirEntreSi
  * noun 'una' 'con' 'otra'   -> UnirEntreSi
  * noun 'unos' 'con' 'otros' -> UnirEntreSi
  * noun 'unas' 'con' 'otras' -> UnirEntreSi;

Extend 'conecta' first
  * noun 'con' noun           -> Atar
  * noun 'entre' 'si'         -> UnirEntreSi
  * noun 'uno' 'con' 'otro'   -> UnirEntreSi
  * noun 'una' 'con' 'otra'   -> UnirEntreSi
  * noun 'unos' 'con' 'otros' -> UnirEntreSi
  * noun 'unas' 'con' 'otras' -> UnirEntreSi;
  
Verb 'desactiva'
  * noun -> Apagar;

Verb 'teclea' 'escribe' 'introduce'
  *                 -> TeclearMal
  * topic 'en'      -> TeclearMal
  * topic 'en' noun -> Teclear
  * noun            -> TeclearMal
  * noun topic      -> TeclearMal
!  * topic           -> TeclearMal
  * 'en' noun topic -> TeclearMal
  * 'en' noun       -> TeclearMal;

VerboIrregular "introducir" with imperativo 'introduce';

Verb 'sigue'
  * creature         -> Seguir
  * creature 'aqui'  -> Seguir;

Verb 'ven'
  *                  -> Venir
  * 'aqui'           -> Venir
  * 'conmigo'        -> Venir;

Verb 'vamos'
  *                  -> Venir;

Verb 'vamonos' 'vayamonos'
  *                  -> Venir
  * 'de' 'aqui'      -> Venir;

VerboIrregular "seguir" with imperativo 'sigue';
VerboIrregular "venir" with imperativo 'ven';

Verb 'queda'
  *                 -> Quedar
  * 'aqui'          -> Quedar
  * creature        -> Quedar
  * creature 'aqui' -> Quedar;

VerboIrregular "quedarse" with imperativo 'queda';

Verb 'deten' 'para'
  * creature -> Quedar
  *          -> Quedar
  * 'aqui'   -> Quedar;
  
VerboIrregular "detenerse" with imperativo 'deten';
VerboIrregular "pararse" with imperativo 'para';

Verb 'esperate'
  *          -> Esperar
  * 'aqui'   -> Esperar;

Extend 'espera'
  * 'aqui'   -> Esperar;

Verb meta 'creditos'
  * -> Creditos;
  
Extend 'examina'
  * 'dentro' 'de' noun -> BuscarEn
  * 'en' noun          -> BuscarEn;

Extend 'x//'
  * 'en' noun          -> BuscarEn
  * 'dentro' 'de' noun -> BuscarEn;

VerboIrregular "mostrar las salidas" with imperativo 'salidas';

Ifdef DEBUG;
Verb meta 'xrecarga'
  * -> XRecargar;
EndIf;

Verb 'recarga' 'carga'
  *                  -> Recargar
  * noun             -> Recargar
  * noun 'en' noun   -> RecargarEn;

Ifdef DEBUG;
Verb meta 'primera'
  *         -> PrimeraParte;
  
Verb meta 'segunda'
  *         -> SegundaParte;

Verb meta 'cuarta'
  *         -> CuartaParte;
Endif;

Extend 'salidas'
  * 'on' / 'si' -> ActivarSalidas
  * 'off' / 'no' -> DesactivarSalidas;

Verb 'ayuda' 'socorro' 'auxilio'
  *          -> Ayudar
  * creature -> Ayudar;

VerboIrregular "pedir ayuda" with imperativo 'ayuda';

Verb 'hola'
  *                  -> Saludar
  * creature         -> Saludar;

Extend 'saluda' first
  *                  -> Saludar;

Verb 'dispara'
  *                     -> Disparar
  * noun                -> Disparar
  * 'a//'/'contra' noun -> Disparar;

Extend 'sientate' first
  *           -> Sentarse
  * 'en' noun -> Sentarse;

Extend 'empuja' first
  * noun                          -> Empujar
  * 'a//' noun                    -> Empujar
  * noun 'a//'/'hacia' noun       -> EmpujarDir
  * 'a//' noun 'a//'/'hacia' noun -> EmpujarDir
  * 'a//' noun noun               -> EmpujarDir;

Extend 'gira' first
  * 'a//' noun      -> Girar;

Verb 'arregla' 'repara'
  * noun            -> Arreglar
  * noun noun       -> Arreglar
  * noun 'con' noun -> Arreglar;

Verb 'suelda'
  * noun            -> Soldar
  * noun noun       -> Soldar
  * noun 'con' noun -> Soldar;

VerboIrregular "soldar" with imperativo 'suelda';

Extend 'rompe' first
  * noun 'con' noun  -> AtacarCon;

Verb meta 'graficos'
  *                           -> Graficos
  * 'on' / 'encendido' / 'si' -> EncenderGraficos
  * 'off' / 'apagado' / 'no'  -> ApagarGraficos
  * 'pequenos'                -> GraficosPequenos
  * 'medianos'                -> GraficosMedianos
  * 'grandes'                 -> GraficosGrandes;
 
Extend 'sal' last
  * 'por' noun      -> Meterse;

Extend 'salte' last
  * 'por' noun      -> Meterse;

Extend 'quita' last
  * noun 'con' held -> QuitarCerrojo;

Extend 'desatornilla' last
  * noun 'con' held -> Girar;

Extend 'atornilla' last
  * noun 'con' held -> Girar;

Extend 'saca' last
  * multiinside 'de' 'dentro' 'de' noun -> Sacar;

Extend 'coge' last
  * multiinside 'de' 'dentro' 'de' noun -> Sacar;

Verb 'pincha' 'clava' 'inyecta'
  * creature held                -> Pinchar reverse
  * held 'en' noun               -> Pinchar
  * held 'a//' creature          -> Pinchar
  * held noun                    -> Pinchar
  * 'con' held 'en' noun         -> Pinchar
  * 'con' held 'a//' creature    -> Pinchar
  * 'con' held noun              -> Pinchar
  * noun 'con' held              -> Pinchar reverse
  * 'a//' creature 'con' held    -> Pinchar reverse;

Verb 'inyectale' 'pinchale' 'clavale'
  * creature held                -> Pinchar reverse
  * held 'a//' creature          -> Pinchar
  * 'con' held 'a//' creature    -> Pinchar
  * 'a//' creature 'con' held    -> Pinchar reverse;

Verb 'despega'
  *            -> Despegar
  * 'nave'     -> Despegar;

Extend 'empuja' first
  * noun 'a//'/'hacia' puerta                    -> EmpujarHaciaPuerta
  * noun puerta                                  -> EmpujarHaciaPuerta
  * 'a//' noun 'a//'/'hacia' puerta              -> EmpujarHaciaPuerta
  * 'a//' noun puerta                            -> EmpujarHaciaPuerta;


Verb 'dentro' 'adentro'
  *                 -> Entrar;

Verb 'bloquea'
  * noun            -> EcharCerrojo
  * noun 'con' held -> EcharCerrojo;

Verb 'desbloquea'
  * noun            -> QuitarCerrojo
  * noun 'con' held -> QuitarCerrojo;

Verb meta 'pistas'
  *                 -> Pistas
  * 'no'            -> PistasNo;

Extend 'quita' first
  *                 -> Empujar;

Extend 'habla' first
  * creature              -> Hablar
  * 'con'/'a//' creature  -> Hablar;

Extend 'responde' first
  * creature              -> Hablar
  * 'a//' creature        -> Hablar
  * topic                 -> Hablar;

Extend 'di' first
  * creature              -> Hablar
  * 'a//' creature        -> Hablar
  * topic                 -> Hablar;



! ====================================================================
!
! RUTINAS VERBOSUB
!
! ====================================================================



[ GraficosSub;
  glk($0086, style_Emphasized);
  print "[Por favor, usa la forma ~";
  style bold;
  print "GRAFICOS SI";
  glk($0086, style_Emphasized);
  print "~ o ~";
  style bold;
  print "GRAFICOS NO";
  glk($0086, style_Emphasized);
  print "~ para activar o desactivar, respectivamente, el uso de gráficos.
         También puedes cambiar el tamaño de la ventana gráfica usando ~";
  style bold;
  print "GRAFICOS PEQUEÑOS";
  glk($0086, style_Emphasized);
  print "~, ~";
  style bold;
  print "GRAFICOS MEDIANOS";
  glk($0086, style_Emphasized);
  print "~ o ~";
  style bold;
  print "GRAFICOS GRANDES";
  glk($0086, style_Emphasized);
  print "~.]^";
  style roman;
];


[ GraficosPequenosSub;
  altoVentanaGrafica = GRAFICOS_PEQUENOS;
  EncenderGraficosSub();
];

[ GraficosMedianosSub;
  altoVentanaGrafica = GRAFICOS_MEDIANOS;
  EncenderGraficosSub();
];


[ GraficosGrandesSub;
  altoVentanaGrafica = GRAFICOS_GRANDES;
  EncenderGraficosSub();
];


[ SonidoSub;
  glk($0086, style_Emphasized);
  print "[Por favor, usa la forma ~";
  style bold;
  print "SONIDO SI";
  glk($0086, style_Emphasized);
  print "~ o ~";
  style bold;
  print "SONIDO NO";
  glk($0086, style_Emphasized);
  print "~ para activar o desactivar, respectivamente, todos los efectos sonoros.]^";
  style roman;
];


[ MusicaSub;
  glk($0086, style_Emphasized);
  print "[Por favor, usa la forma ~";
  style bold;
  print "SONIDO FONDO SI";
  glk($0086, style_Emphasized);
  print "~ o ~";
  style bold;
  print "SONIDO FONDO NO";
  glk($0086, style_Emphasized);
  print "~ para activar o desactivar, respectivamente, el sonido de fondo.]^";
  style roman;
];


[ SonidoDetectorSub;
  glk($0086, style_Emphasized);
  print "[Por favor, usa la forma ~";
  style bold;
  print "SONIDO DETECTOR SI";
  glk($0086, style_Emphasized);
  print "~ o ~";
  style bold;
  print "SONIDO DETECTOR NO";
  glk($0086, style_Emphasized);
  print "~ para activar o desactivar, respectivamente, el sonido del detector de movimiento.]^";
  style roman;
];


[ TeclearMalSub;
  glk($0086, style_Emphasized);
  print "[Por favor, usa la forma ~";
  style bold;
  print "TECLEA tal EN cual";
  glk($0086, style_Emphasized);
  print "~. Por ejemplo: ~";
  style bold;
  print "TECLEA HOLA EN EL TECLADO";
  glk($0086, style_Emphasized);
  print "~.]^";
  style roman;
];


[ SemillaSub;
  rfalse;
];


[ EmpujarHaciaPuertaSub o i l;
  objectloop (o in brujula) {
    i = o.direcc_puerta;
    l = LugarReal();
    if (l provides i && l.i has puerta) {
      <<EmpujarDir uno o>>;
    }
  }
];


[ DespegarSub;
  if (LugarReal() == nave1 or nave2) {
    "El control de la nave está en manos de Madre.";
  } else {
    "Debes estar dentro de la nave si quieres despegar.";
  }
];


[ PincharSub;
  "No tiene sentido clavar ", (el) uno, " en ", (el) otro, ".";
];


[ UnirEntreSiSub;
  <<Atar uno uno>>;
];


[ EncenderGraficosSub t;
  hayGraficos = true;
  openGraphicWindow(altoVentanaGrafica);
  clearGraphicWindow();
  if (LugarReal() provides sgw_img) {
    viewImageCenter(LugarReal().sgw_img);
  } else {
    viewImageCenter(Sin_Imagen_jpg);
  }
  if (~~t) {
    glk($0086, style_Emphasized);
    print "[Activados los gráficos (tamaño ";
    switch (altoVentanaGrafica) {
      GRAFICOS_PEQUENOS: print "pequeño";
      GRAFICOS_MEDIANOS: print "mediano";
      GRAFICOS_GRANDES:  print "grande";
    }
    print ").]^";
    style roman;
  }
];


[ ApagarGraficosSub t;
  hayGraficos = false;
  closeGraphicWindow();
  if (~~t) {
    glk($0086, style_Emphasized);
    print "[Desactivados los gráficos.]^";
    style roman;
  }
];


[ UsarSub;
  "Los verbos ~USAR~ o ~UTILIZAR~ no tienen efecto en esta aventura. Prueba a indicar más
   concretamente qué acción deseas realizar.";
];


[ AyudarSub;
  print "Prueba a pedir ayuda a alguien. Si necesitas pistas más elaboradas, puedes usar
         el sistema de pistas interactivas tecleando el comando ";
  style bold;
  print "PISTAS";
  style roman;
  ".";
];


[ AtacarConSub;
  <<Abrir uno otro>>;
];


[ ArreglarSub;
  "¿Arreglar cómo?";
];


[ SoldarSub;
  "¿Soldar cómo?";
];


[ RecargarSub;
  if (cargador in LugarReal()) {
    <<RecargarEn cargador>>;
  } else {
    "Debes indicar con qué quieres recargar. O tal vez no tengas un recargador a mano...";
  }
];


[ RecargarEnSub;
  if (uno ~= cargador) {
    "Sólo se puede recargar si hay un recargador cerca.";
  }
];


[ DispararSub;
  if (rifle notin jugador) {
    "¿Cómo pretendes disparar, si no llevas ningún arma?";
  }
  if (uno == nothing) {
    "¿A quién o qué hay que disparar?";
  }
  if (uno == jugador) {
    "¿Tan desesperado estás?";
  }
  "Será mejor que guardes las balas para mejor ocasión.";
];


[ SaludarSub;
  if (uno ~= nothing) {
    "Saludas a ", (el) uno, ".";
  } else {
    "Mueves la mano en actitud amistosa.";
  }
];


[ VenirSub;
  <<Seguir jugador>>;
];


[ SeguirSub;
  "Tú no tienes que seguir a nadie. Si acaso, deberían seguirte a ti.";
];


[ QuedarSub;
  "Ahora mismo estás parado...";
];


[ ActivarSalidasSub;
  mostrarSalidas = true;
!  give mostrar_salidas ~oculto;
  glk($0086, style_Emphasized);
  print "[Activado el listado automático de salidas.]^";
  style roman;
];


[ DesactivarSalidasSub;
  mostrarSalidas = false;
!  give mostrar_salidas oculto;
  glk($0086, style_Emphasized);
  print "[Desactivado el listado automático de salidas.]^";
  style roman;
];


Ifdef DEBUG;
[ PrimeraParteSub;
  move tu_traje to jugador;
  give tu_traje puesto;
  move traje_jason to jason;
  give traje_jason puesto;
  move bateria to linterna;
  move linterna to jugador;
  move destornillador to jugador;
  linterna.encender();
!  puerta_procesador.abrir();
  move jason to junto_procesador;

  JugadorA(junto_procesador);
];


[ SegundaParteSub;
  
  SegundaParte();

  move tu_traje to jugador;
  give tu_traje puesto;
  move bateria to linterna;
  move linterna to jugador;
  linterna.encender();
  give detector_movimiento encendido;
  puerta_procesador.abrir();
  move rifle to jugador;
  move bisturi to jugador;
  give circuito general;
  move tarjeta to jugador;
  remove robot;
  give robot ausente;
  move robot_inutil to procesador;
  give jugador luz;

  JugadorA(entrada);
];


[ CuartaParteSub;
  move bateria to linterna;
  move linterna to jugador;
  move destornillador to jugador;
  linterna.encender();
  remove puerta_procesador;
  give puerta_procesador ausente;
  move puerta_viscosa to entrada;
  alien.primera_vez = false;
  move rifle to jugador;
  give detector_movimiento encendido;
  remove traje_jason;
  give traje_jason ausente;
  move traje_inutil to hueco_techo;
  move jason to aula;
  PNJ_Ruta(jason, MOVIMIENTO_PERSEGUIR, jugador);
  move alien to laberinto18;                         ! Por ejemplo
  DejarRastros();
  remove tu_traje;
  give tu_traje ausente;
  puerta_procesador.abrir();
  move bisturi to jugador;
  give circuito general;  
  give vaina abierta;
  AlienBloqueaPuerta();
  move tarjeta to jugador;
  move holocubo to proyector;
  give proyector ~abierto;
  
  JugadorA(aula);
];


[ XRecargarSub;
  bateria.energia = MAX_NIVEL_CARGA;
  "[Baterías recargadas.]";
];
Endif;


[ EncenderSonidoSub;
  Damusix.ActivarAudio();
  EncenderMusicaSub(true);
  if (control_sonido_detector.sonido ~= null) {
    control_sonido_detector.activar(control_sonido_detector.sonido, SONIDO_BUCLE_INFINITO);
  }
  glk($0086, style_Emphasized);
  print "[Activados todos los efectos sonoros.]^";
  style roman;
];


[ ApagarSonidoSub;
  Damusix.DesactivarAudio();
  control_sonido_fondo.hay_sonido = false;
  glk($0086, style_Emphasized);
  print "[Desactivados todos los efectos sonoros.]^";
  style roman;
];


[ EncenderMusicaSub i sf;

  if (LugarReal() provides sgw_mus) {
    sf = (LugarReal()).sgw_mus;
  } else {
    sf = null;
  }

  if (sf ~= null && control_sonido_fondo.sonido ~= sf ||
      ~~(control_sonido_fondo.hay_sonido)) {
    control_sonido_fondo.activar(sf, SONIDO_BUCLE_INFINITO);
  }

  control_sonido_fondo.hay_sonido = true;
    
  if (~~i) {
    glk($0086, style_Emphasized);
    print "[Activado el sonido de fondo.]^";
    style roman;
  }
];

  
[ ApagarMusicaSub;
  control_sonido_fondo.hay_sonido = false;
  control_sonido_fondo.desactivar();
  glk($0086, style_Emphasized);
  print "[Desactivado el sonido de fondo.]^";
  style roman;
];


[ EncenderSonidoDetectorSub;
  control_sonido_detector.hay_detector = true;
  glk($0086, style_Emphasized);
  print "[Activado el sonido del detector de movimiento.]^";
  style roman;
];


[ ApagarSonidoDetectorSub;
  control_sonido_detector.hay_detector = false;
  if (control_sonido_detector.sonido ~= Sirena_ogg) {
    control_sonido_detector.desactivar();
  }
  glk($0086, style_Emphasized);
  print "[Desactivado el sonido del detector de movimiento.]^";
  style roman;
];


[ CreditosSub;
  if (hayGraficos) {
    viewImageCenter(Sin_Imagen_jpg);
  }
  print "A·L·I·E·N : LA AVENTURA - EDICIÓN ESPECIAL^";
  print "(c) 2008-2011 Alpha Aventuras^^";
  print "- PROGRAMACIÓN Y DISEÑO:^";
  print "    Ricardo Pérez López^^";
  print "- GRÁFICOS E IDEA ORIGINAL:^";
  print "    Manuel Millán Ruiz^^";
  print " - TEXTOS:^";
  print "    Ricardo Pérez López^";
  print "    Francisco J. Roldán Domínguez^";
  print "    Manuel I. Monge García^^";
  print " - MAPEADO Y MANUALES:^";
  print "    Francisco J. Roldán Domínguez^^";
  print "- PRUEBAS:^";
  print "    Jenesis^";
  print "    Ricardo Pérez López^";
  print "    Francisco J. Roldán Domínguez^";
  print "    Manuel Millán Ruiz^";
  print "    Francisco Picazo Millán^";
  print "    Manuel I. Monge García^^";
  EsperarTecla();
  print "Desarrollado usando el lenguaje Inform, de Graham Nelson,^";
  print "con la ayuda de las siguientes librerías:^^";
  print "- InformATE 6/10+, de Zak et al.^";
  print "- Adaptación de SGW+DMX, de Eliuk Blau^";
  print "- Damusix, de Eliuk Blau^";
  print "- PNJactor_NG y PNJacciones_NG (adaptaciones de los originales de Zak y Carlos)^";
  print "- Rastros_NG (adaptación de Rastros, de Mel Hython)^";
  print "- Moviles_NG (adaptación de Moviles, de Mel Hython)^";
  print "- PnjPuertas_NG (Adaptación de PnjPuertas, de Jaevius)^";
  print "- Decir, de Zak^";
  print "- Etemas, de Zak^";
  print "- Decorado_NG (adaptación de Decorado, de Zak y Mel Hython)^";
  print "- Escr_NG (adaptación de Escr, de Baltasarq)^";
  print "- Adaptación de Barra, de Presi^";
  print "- Adaptación de BajoNivel, de Zak y Baltasarq^";
  print "- Adaptación de ZIPI, de Zak";
  EsperarTecla();
  print "^^Nuestro agradecimiento a todos los integrantes del CAAD que nos han ayudado,
         directa o indirectamente, en el desarrollo de esta aventura.^^
         (c) 2008-2011 Alpha Aventuras.^^
         Este programa es software libre, publicado bajo la licencia GNU GPL v3.
         El texto completo de la licencia se encuentra en la siguiente dirección:^^";
  style fixed;
  print "http://www.gnu.org/licenses/gpl.txt^";
  style roman;
  EsperarTecla();
  if (hayGraficos) {
    viewImageCenter(LugarReal().sgw_img);
  }
];


[ TeclearSub;
  if (uno == 0) {
    TeclearMalSub();
    rtrue;
  } else {
    "No puedes teclear nada ahí.";
  }
];


[ SentarseSub;
  if (LugarReal() ~= nave1) {
    "Ahora no es momento de descansar.";
  }
  rfalse;
];


[ PistasSub;
  switch (hayPistas) {
    0: hayPistas = 1;
       jason.hablar = computadora.hablar = robot.hablar = false;
       glk($0086, style_Emphasized);
       print "[Advertencia: Es sabido que la tentación de la ayuda es a veces
              tan fuerte que se suelen obtener pistas prematuramente. Por tanto,
              en cualquier momento durante el juego puedes poner ";
       style bold;
       print "PISTAS NO";
       glk($0086, style_Emphasized);
       print ", y eso te impedirá obtener ayuda en la sesión de juego actual.
              Si sigues queriendo una pista, pon ";
       style bold;
       print "PISTAS";
       glk($0086, style_Emphasized);
       print " otra vez.]^";
       style roman;
    1: ApagarGraficosSub(true);
       barra_estado.numero_lineas = 26;
       barra_estado.dibujar();
       ZIPI_Empezar();
       barra_estado.numero_lineas = 1;
       barra_estado.dibujar();
       EncenderGraficosSub(true);
       <<Mirar>>;
    2: glk($0086, style_Emphasized);
       print "[Las pistas han sido desactivadas.]^";
       style roman;
  }
];


[ PistasNoSub;
  switch (hayPistas) {
    2:       glk($0086, style_Emphasized);
             print "[Las pistas ya han sido desactivadas.]^";
             style roman;
    default: hayPistas = 2;
             glk($0086, style_Emphasized);
             print "[PISTAS desactivadas.]^";
             style roman;
  }
];

