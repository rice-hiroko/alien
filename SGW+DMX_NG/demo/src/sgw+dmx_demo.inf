!% -~S~D
!% +language_name=Spanish
!%
!===============================================================================
!  SGW+DMX DEMO
!  Codigo Fuente
!===============================================================================
!
!  Archivo :  sgw+dmx_demo.inf
!  Fecha   :  2009/02/10
!  Version :  1.2
!  Autor   :  Eliuk Blau
!  e-Mail  :  eliukblau (AT) gmail.com
!
!  Descripcion: Sencillo ejemplo de las funcionalidades que provee la
!               extension SGW+DMX.
!
!               Se muestra el uso basico del Marco de Trabajo y la
!               posibilidad de compilar junto con Damusix para el Audio.
!
!               Esta demostracion esta inspirada en el buen ejemplo
!               "Test para SGW", de Mapache y Depresiv.
!
!               Si modificas este programa, estare muy agradecido si me mandas
!               el codigo fuente por e-Mail.
!
!               If you modify this program, I would be grateful if you send me
!               the source code by e-Mail.
!
!               Copyright (c) 2008 Nicolas Merino Quezada (aka Eliuk Blau).
!
!               This program is free software: you can redistribute it and/or
!               modify it under the terms of the GNU Lesser General Public
!               License as published by the Free Software Foundation, either
!               version 3 of the License.
!
!               This program is distributed in the hope that it will be useful,
!               but WITHOUT ANY WARRANTY; without even the implied warranty of
!               MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
!               GNU Lesser General Public License for more details.
!
!               You should have received a copy of the GNU Lesser General
!               Public License along with this program. If not, see
!               <http://www.gnu.org/licenses/>.
!
!===============================================================================
!
!  Log:
!  2009/02/10  Eliuk Blau - Nueva version: ** SGW+DMX Demo v1.2 **
!                         - Se hace una minima correccion en rutina
!                           EjecutarDemo().
!  2008/08/08  Eliuk Blau - Nueva version: ** SGW+DMX Demo v1.1 **
!                         - Se cambian todos los KeyCharPrimitive() de Lib.
!                           6/11 por nueva rutina wait() de SGW+DMX v1.4
!  2008/07/13  Eliuk Blau - Se termina de programar y corregir BUGs.
!                           Lista la version 1.0 de esta demostracion.
!  2008/07/11  Eliuk Blau - Se crea el archivo.
!
!-------------------------------------------------------------------------------

Constant Story "^SGW+DMX Demo v1.2 @00";
Constant Headline "^Extensión ~Simple Glulx Wrapper con Damusix~ de Eliuk Blau.
                   ^- SGW+DMX está basada en SGW v1.6.1 de Alessandro Schillaci.
                   ^- Demo inspirada en el ~Test para SGW~ de Mapache y Depresiv.^^";

! Si queremos usar Damusix para gestionar el audio
Constant SGW_CON_DAMUSIX;

! Si no queremos usar graficos
! Constant SGW_SIN_GRAFICOS;

! Si no queremos usar el Marco de Trabajo
! Constant SGW_SIN_MARCO_DE_TRABAJO;

! Para definir los colores, podemos usar las "constantes de color"
! faciles de SGW+DMX o usar un valor hexadecimal en formato RRGGBB
Constant SCBACK = $000000;      ! estilo fondo
Constant SCTEXT = $B0C2FB;      ! estilo texto
Constant SCEMPH = $FFFFFF;      ! estilo enfasis
Constant SCHEAD = $BBDDFF;      ! estilo cabecera (nombre localidades)
Constant SCINPU = $DDEEFF;      ! estilo input del jugador
Constant SCUSR1 = CLR_GG_GREEN; ! estilo configurable User1
Constant SCUSR2 = CLR_GG_AZURE; ! estilo configurable User2

Include "Parser";

Include "sgw+dmx";           ! Incluir la extension SGW+DMX
Include ">sgw+dmx_demo.bli"; ! Incluir recursos multimedia (imagenes y sonidos)

Include "VerbLib";

[ Initialise ;
    ! INICIALIZAMOS SGW+DMX para mostrar imagenes con 200px de altura.
    initializeSGW(200);
    !---------------------------------------------------------------------------
    ! NOTA TECNICA:
    ! Siempre se suman automaticamente 20px a la altura indicada, asi que el
    ! tamaño de la ventana quedara finalmente en 220px. Siempre se aplica un
    ! borde de 10px a toda imagen que se muestra en la ventana superior, asi
    ! que sumando esos 20px, la ventana superior tendra espacio suficiente
    ! para mostrar una imagen de efectivamente 200px de altura.
    !
    ! En otras palabras:
    ! 200px img. + 20px bordes arriba/abajo = 220px "reales" para vent. grafica
    !---------------------------------------------------------------------------

    !---------------------------------------------------------------------------
    ! Lo siguiente es simplemente para cambiar el mensaje del Titulo y no tiene
    ! absolutamente nada que ver con el funcionamiento de SGW+DMX (un capricho)
    !---------------------------------------------------------------------------
      #ifdef TARGET_GLULX;
        #ifdef _DAMUSIX_H_; ! si usamos Damusix
          string 0 "(usando Damusix)";
        #ifnot; ! si usamos audio nativo de SGW+DMX
          string 0 "(usando Audio Nativo)";
        #endif; ! _DAMUSIX_H_
      #ifnot; ! TARGET_ZCODE
        string 0 "(versión Máquina-Z)";
      #endif; ! TARGET_
    !---------------------------------------------------------------------------

    ! Lugar de comienzo.
    location = Localidad_01;

    ! Activamos esta variable para que la descripción de los lugares
    ! se repita siempre, incluso si el jugador ya ha estado alli.
    lookmode = 2;

    ! mostramos titulo y subtitulo del juego con los estilos de SGW+DMX
    print (s_head) Story;
    print (string) Headline;

    ! para que no se muestre el Banner con el mensaje de version
    return 2;
];

Include "SpanishG";


!===============================================================================
! LOCALIDADES Y OBJETOS
!-------------------------------------------------------------------------------

Object Localidad_01 "Lejos del Final"
  has light,
  with
    sgw_img LOC_01,       ! el grafico de la localidad
    sgw_mus MUSICA_FONDO, ! la musica de la localidad
    sgw_vol VOLUMEN_BAJO, ! el volumen de la musica
    description "En el norte hay algo que ves realmente muy borroso.
      Una extraña melodía proviene desde ese mismo lugar y
      parece muy lejana.",
    n_to Localidad_02,
;

Object Localidad_02 "Más cerca del Final"
  has light,
  with
    sgw_img LOC_02,       ! el grafico de la localidad
    sgw_mus MUSICA_FONDO, ! la musica de la localidad
    sgw_vol VOLUMEN_MEDIO, ! el volumen de la musica
    description "En el norte hay algo que ves un poco borroso.
      Una extraña melodía proviene desde ese mismo lugar y
      parece cercana.",
    n_to Localidad_03,
    s_to Localidad_01,
;

Object Localidad_03 "Casi en el Final"
  has light,
  with
    sgw_img LOC_03,       ! el grafico de la localidad
    sgw_mus MUSICA_FONDO, ! la musica de la localidad
    sgw_vol VOLUMEN_ALTO, ! el volumen de la musica
    description [ ;
      print "¡Vaya! La música proviene de aquí... Y lo que se veía borroso
        era nada menos que el logo de ~";
      print (s_user1)"SGW+DMX: ";
      print (s_user2)"Simple Glulx Wrapper con Damusix","~.^";
      print "^Cerca tuyo está una chica bien guapa, con cara de intelectual.
        Te mira desde un lado como esperando que le hables.^^";
      print "Hacia el norte parece que se encuentra el final.^";
    ],
    n_to Localidad_04,
    s_to Localidad_02,
;

Object Localidad_04 "Casi en el Final"
  has light,
  with
    sgw_img LOC_04,       ! el grafico de la localidad
    sgw_mus MUSICA_DMX,   ! la musica de la localidad
    sgw_vol VOLUMEN_ALTO, ! el volumen de la musica
    description [ ;
      print "¡Jejeje! Aquí está el reluciente logo de ";
      print (s_user2)"Damusix";
      print "... ¡Y su pegajosa musiquita! ¡Yeah! @@94_@@94^^
        Si vas hacia el norte terminará todo...^";
    ],
    s_to Localidad_03,
    n_to [;
      deadflag = 2;
      #ifdef _DAMUSIX_H_; ! si usamos Damusix
        Damusix.SimpleFadeOut(location.sgw_mus,50);
        Damusix.PararTodo();
        Damusix.TocarV(VICTORIA);
      #ifnot; ! si usamos audio nativo de SGW+DMX
        silenceAll();
        playSound(chan1,VICTORIA,1,100);
      #endif; ! _DAMUSIX_H_;
      viewImageCenter(DESPEDIDA); ! pintamos la imagen de despedida
      "Te vas del extraño lugar, feliz, entre vitores celestiales en tu honor.";
    ],
;

Object Peluche_Wookie "peluche de wookie" Localidad_02
  with
    sgw_img WOOKIE, ! el grafico del objeto
    name 'peluche' 'wookie',
    description "El peluche producirá un curioso sonido si
      lo aprietas o golpeas.",
    before [ ;
      Squeeze, Attack:
        #ifdef _DAMUSIX_H_; ! si usamos Damusix
          Damusix.TocarV(GRITO);
        #ifnot; ! si usamos audio nativo de SGW+DMX
          playSound(chan1,GRITO,1,100);
        #endif; ! _DAMUSIX_H_;
        "~Grrrruuuaaaaaawww!!~";
    ],
;

Object Chica_Intelectual "chica" Localidad_03
  has female animate concealed,
  with
    sgw_img CHICA, ! el grafico del objeto
    name 'chica',
    adjectives 'guapa' 'intelectual',
    description "La chica está promoviendo la Librería INFSP.",
    life [;
      Answer, Tell:
        clearMainWindow(); ! limpamos ventana principal (textos e imagen superior)
        viewImageCenter(location.sgw_img); ! re-pintamos la imagen de la localidad
        viewImageSlide(self.sgw_img,1); ! sin Slide para mantener abierta la ventana
        print "^^La chica te mira y dice:^^";
        if(self hasnt general) {
          give self general;
          print "¡Vaya! Por fin me hablaste. Estoy aquí para enseñarte algunas
            cosas interesantes de la extensión ",(s_user1)"SGW+DMX",". ";
        }
        else{
          print "¿Qué? ¿Quieres ver nuevamente la demostración? De acuerdo. @@94_@@94
            Vamos a ello...";
        }
        wait();
        print "^^";
        EjecutarDemo();    ! EJECUTAMOS LA DEMOSTRACION
        closeImageSlide(); ! cerramos la ventana grafica pequeña
        clearMainWindow(); ! limpiamos toda la ventana principal
        viewImageCenter(location.sgw_img); ! re-pintamos imagen de localidad
        !-----------------------------------------------------------------------
        ! ... y volvemos a reproducir la musica de fondo de la localidad...
        !-----------------------------------------------------------------------
        #ifdef _DAMUSIX_H_; ! si usamos Damusix
          Damusix.SimpleFadeOut(MUSICA_DEMO,50);
          Damusix.Parar(MUSICA_DEMO); ! muy importante: paramos musica de demo
          Damusix.Volumen(location.sgw_mus,100); ! por el SimpleFadeOut del comienzo
          Damusix.Tocar(location.sgw_mus); ! tocamos musica de localidad actual
        #ifnot; ! si usamos audio nativo de SGW+DMX
          silenceChannel(music); ! silenciamos todo y tocamos musica...
          playSound(music,location.sgw_mus,-1,100); ! ... de localidad actual
        #endif; ! _DAMUSIX_H_;
        !-----------------------------------------------------------------------
        new_line;
        <<Look>>; ! descripcion de localidad y devuelve 'true' (necesario)
    ],
;

! Ejecuta la Demostracion de funcionalidades de SGW+DMX
! (aislamos el codigo en una rutina solo para tener mas orden)
[ EjecutarDemo ;
    wait("Pero primero quitemos la música de fondo... ");
    #ifdef _DAMUSIX_H_; ! si usamos Damusix
      Damusix.SimpleFadeOut(location.sgw_mus,50);
      Damusix.PararTodo();
    #ifnot; ! si usamos audio nativo de SGW+DMX
      silenceAll();
    #endif; ! _DAMUSIX_H_;
    wait("^... y pongamos una más ~ad-hoc~. ");
    #ifdef _DAMUSIX_H_; ! si usamos Damusix
      Damusix.AsignarCanal(MUSICA_DEMO,0,-1,100);
      Damusix.Tocar(MUSICA_DEMO);
    #ifnot; ! si usamos audio nativo de SGW+DMX
      playSound(music,MUSICA_DEMO,-1,100);
    #endif; ! _DAMUSIX_H_;
    wait("^^Ok, ahora está mucho mejor. ");
    wait("^^Vamos a mostrar otro gráfico en la ventana superior,
      uno con más garra... ");
    viewImageCenter(CAAD);
    wait("¡Voilà! El Logo del CAAD. @@94_@@94 ");
    wait("^^La imagen de arriba está ~centrada~, ");
    wait("pero si queremos la podemos poner... ");
    viewImageLeft(CAAD);
    wait("a la ~izquierda~... ");
    viewImageRight(CAAD);
    wait("o a la ~derecha~. ");
    wait("^^También podemos cerrar todas las ventanas gráficas. ");
    closeAllWindows();
    clearMainWindow();
    wait("^^¡Vaya! ¡Qué vacuidad!... ");
    wait("Mejor volvemos a abrir la ventana gráfica superior... ");
    openGraphicWindow(200); ! abrimos de nuevo la vent. grafica con 200px
    clearMainWindow(); ! limpiamos el texto por puro gusto, la verdad =P
    viewImageCenter(location.sgw_img); ! re-pintamos imagen de localidad
    wait("^^¡Mucho más acogedor! @@94_@@94 ");
    wait("^^¡Ups! No aparezco yo. Tengo que abrir mi ventana también... ");
    viewImageSlide(Chica_Intelectual.sgw_img,1);
    wait("^^Ya regresé. @@94_@@94 ¡Ji ji ji! ");
    wait("^^En la ventana gráfica donde estoy se pueden mostrar
      imágenes pequeñas, por ejemplo: objetos del juego. ");
    wait("^^Ahora voy a retirarme un momento... ");
    closeImageSlide();
    wait("para poder mostrarte... ");
    print "^^¡Mi mapa del tesoro! "; ! aqui la pausa la esta haciendo
    viewImageSlide(MAPA);            ! el propio efecto de Slide ahora
    wait("^^¿Viste el deslizamiento? Es un efecto muy bonito... ");
    wait("^^Se muestra la imagen con un deslizamiento de entrada, ");
    wait("se espera unos segundos ");
    wait("y luego se quita la imagen con un deslizamiento de salida. ");
    wait("Todo automáticamente. ");
    wait("^^Podemos mostrar la imagen sin el deslizamiento... ");
    viewImageSlide(MAPA,1);
    wait("pero entonces tendremos que cerrar nosotros mismos la ventana... ");
    closeImageSlide();
    wait("^^... tal como lo acabamos de hacer ahora. @@94_@@94 ");
    wait("^^No te asustes, no voy a abandonarte... ");
    viewImageSlide(Chica_Intelectual.sgw_img,1);
    wait("Ya he regresado, ¿lo ves? @@94_@@94 ");
    wait("^^¡Vale, vale! Eso es todo. ");
    print "Disfruta programando con: ",(s_user1)"SGW+DMX",". "; ! no usamos wait() por lo del '(estilo)'
    wait();                                                     ! pero ponerlo aqui hace el mismo efecto
    wait("^^Y ahora, vuelve a tu realidad... ");
];
